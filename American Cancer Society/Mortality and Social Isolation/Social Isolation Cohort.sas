*****************************************************************************************************;
* SOCIAL SUPPORT AND MORTALITY ANALYSIS
* PI'S: KASSANDRA ALCARAZ AND SUE GAPSTUR
* ANALYST: JENNY BLASE	
* COHORT CODE MODIFIED FROM JANET: S:\USER\JHILDEBRAND\BMI AND MORTALITY GLOBAL\ANALYTIC POPULATION.sas	*;
*	30 YEAR FU																						*;
*****************************************************************************************************;

*	CAN ALSO LOOK AT: S:\USER\RDIVER\PRCA and Shiftwork\PRCA and Shiftwork Cohort.sas	*;

LIBNAME SOCIAL 'D:\USER\JBLASE\Social Support\DATA';
LIBNAME MASTER 'D:\CPS\MASTER';
LIBNAME MORT 'D:\CPS\MORTALITY';
LIBNAME PYEAR 'D:\CPS\CPS2\PYEAR';
LIBNAME SURV82M 'D:\CPS\CPS2\ORIGINAL';
LIBNAME DERV 'D:\CPS\CPS2\DERIVED';
LIBNAME LIBRARY 'D:\USER\JBLASE\Social Support\DATA';
%INCLUDE "D:\USER\JBLASE\Macros\ALCNEW.SAS";


PROC FORMAT LIBRARY=LIBRARY; 
                                                                                
    VALUE ALLCAN   	 0 = 'ALIVE'                                              
                     1 = 'CANCER DEATH';
 
	VALUE SEX	 	 0 = '0:Males'
					 1 = '1:Females'
					 ;				

	VALUE FRIENDS	0 = '0 OR NONE'
					96 = '96 OR MORE'
					97 = 'VERY FEW'
					98 = 'MANY, A LARGE NUMBER'
					99 = 'UNSPECIFIED (INFO GIVEN)';

	VALUE CHURCH	0 = '0 OR NONE'
					96 = '96 OR MORE'
					97 = '6 TIMES A YEAR, ONLY DURING HOLIDAYS, ETC.'
					98 = 'FREQUENTLY'
					99 = 'UNSPECIFIED (INFO GIVEN)'
					. = 'MISSING';

	VALUE CLUBS		0 = '0 OR NONE'
					96 = '96 OR MORE'
					97 = 'VERY FEW'
					98 = 'FREQUENTLY'
					99 = 'UNSPECIFIED (INFO GIVEN)'
					. = 'MISSING';

	VALUE $ MS	'1' = 'SINGLE'
				'2' = 'SEPARATED'
				'3' = 'WIDOWED'
				'4' = 'MARRIED'
				'5' = 'DIVORCED'
				' ' = 'MISSING'; 

	VALUE EXCLU	1="NO PERSON-TIME BEFORE AGE LIMIT"
				2="PREVALENT DISEASE"
				3="MISSING EXPOSURE"
				4="MISSING/OTHER RACE"
				5="INCOMPLETE SMOKING DATA"
				6="MISSING SEX"
				99="FINAL COHORT";

	VALUE ALLCAUSE   0 = 'ALIVE'                                              
                     1 = 'DEAD';

	VALUE CIGCAT 	0 = 'NEVER SMOKED'
					1 = 'FORMER <=10 CPD' 	
					2 = 'FORMER 11-20 CPD'
					3 = 'FORMER 21-30 CPD'
					4 = 'FORMER 31-40 CPD'
					5 = 'FORMER >40 CPD'
					6 = 'CURRENT <=10 CPD' 	
					7 = 'CURRENT 11-20 CPD'
					8 = 'CURRENT 21-30 CPD'
					9 = 'CURRENT 31-40 CPD'
					10 = 'CURRENT >40 CPD'
					;	

	VALUE EXCLUSNF	1='PREVALENT DISEASE'
				2='SCALE VARIABLES MISSING'
				3='OTHER/HISPANIC/MISSING RACE'
				4='INCOMPLETE SMOKING DATA'
				5='SEX MISSING'
				99='FINAL COHORT';

VALUE BMI 1='1:<18.5'
          2='2:18.5-<25'
		  3='3:25-<30'
		  4='4:30+'
		  9='9:Missing'
		  ;

VALUE EDUNEW 1='1: <High School'
		     2='2: High School Grad'
			 3='3: Some College'
			 4='4: College Grad'
			 9='9: Missing'
			 ;

value $ occnow '0'='0: Not Currently'
               '1'='1: Currently'
			   '9'='9: Unknown'
			   ;

VALUE YESNO         1='1:Yes'
                    0='0:No'
					;
VALUE HORMONE	1="1:IF PRE/PERI UNKNOWN"
				2="2:NEVER"
				3="3:EVER"
				9="9:MISSING";

VALUE ASP	0="0:Never"
			1="1:Occasional"
			2="2:<15/Month"
			3="3:15-<30/Month"
			4="4:30+/Month"
			9="9:MISSING";

VALUE RDPRGRP       1='1: <=3 Red/processed meat per week'
		            2='2: >3-5  Red/processed meat per week'
		            3='3: >5-7  Red/processed meat per week'
		            4='4: 8+  Red/processed meat per week'
					9='9: Incomplete info'
					;
VALUE REGION 1='1:Northeast'
             2='2:South'
    		 3='3:Midwest'
    		 4='4:West'
			 9='9:Missing/unk';

VALUE PA  1='1:None'
		  2='2:Slight'
		  3='3:Moderate'
		  4='4:Heavy'
		  9='9:Missing';

VALUE ALCOHOL
0="0: NO ALCOHOL USE"
1="1: YES ALCOHOL USE"
9="MISSING"
;

VALUE SMOKENEW 0='0:Never'
			   1='1:Current, <20 Cigs/Day'
			   2='2:Current, >=20 Cigs/Day'
			   3='3:Current, Unknown CPD or Duration' 
			   4='4:Former, <10 Since Quit'
			   5='5:Former, 10-<20 Since Quit'
			   6='6:Former, >=20 Since Quit'
			   7='7:Former, YRS Since Quit Unk'
			   8='8:Ever cigar/pipe';

VALUE BSINDEX 0='0:HIGHEST SOCIAL SUPPORT (SCORE=4)'
			  1='1:HIGH SOCIAL SUPPORT (SCORE=3)'
			  2='2:MODERATE SOCIAL SUPPORT (SCORE=2)'
			  3='3:LOW SOCIAL SUPPORT (SCORE=1)'
			  4='4:LOWEST SOCIAL SUPPORT (SCORE=0)'; 

VALUE BSINDEX_3COMP 0='0:HIGHEST SOCIAL SUPPORT (SCORE=3)'
			  1='1:HIGH SOCIAL SUPPORT (SCORE=2)'
			  2='2:MODERATE SOCIAL SUPPORT (SCORE=1)'
			  3='3:LOWEST SOCIAL SUPPORT (SCORE=0)';

VALUE FRIENDREL_R 0='1:6 OR FEWER FRIENDS'
				  1='0: 7 OR MORE FRIENDS';

VALUE MARRIED_R 0='1:UNMARRIED'
				1='0: MARRIED';

VALUE CLUBGRP_R 0='1:NO CLUB OR GROUP ATTENDANCE'
				1='0: ATTENDS CLUBS OR GROUPS AT LEAST 1X/MONTH';

VALUE CHURCH_R  0='1:NO CHURCH ATTENDANCE'
				1='0: ATTENDS CHURCH AT LEAST 1X/MONTH';

VALUE $ BSINDEXF '0'='0:HIGHEST SOCIAL SUPPORT (SCORE=4)'
			  '1'='1:HIGH SOCIAL SUPPORT (SCORE=3)'
			  '2'='2:MODERATE SOCIAL SUPPORT (SCORE=2)'
			  '3'='3:LOW SOCIAL SUPPORT (SCORE=1)'
			  '4'='4:LOWEST SOCIAL SUPPORT (SCORE=0)';

RUN;

***PULLING IN STATE DATA FOR REGION CODING***;
/*DATA SOCIAL.ADDRESS;*/
/*	SET DDUDAS.cps2baseaddress (KEEP=ID STATE);*/
/*RUN;*/
/**/
/*PROC FREQ DATA=SOCIAL.ADDRESS;*/
/*TABLES STATE; RUN;*/


DATA SOCIAL.INITIALCOHORT;  ***BASE COHORT OF MEN AND WOMEN;
   MERGE MORT.smgmrt15d141215 (IN=INMORT KEEP=ID CODE1 CODETYPE)
 
         MASTER.cps2smgmst15m (IN=INMEN KEEP=ID RACE AGE_INT VS)  
         MASTER.cps2smgmst15f (IN=INFEM KEEP=ID RACE AGE_INT VS)  
 
		 DERV.CPS2MENKEYVAR3 (KEEP=ID CA82 WTCHG82 BMI82 SMK82 CPD82 XCPD82 AGEQT82 OCCLIFE
			TOTALC NUTFAT NUTRI ASPIRINC TYLENOLC VITAC VITCC VITEC MULTIC BPC DIURC
		   	THYRC HRTC ACIDC LIBRIUMC SLEEPC TAGAMETC DUR82 XDUR82 OCCNOW ASPIRINC DTINT82) 
 
		 DERV.CPS2FEMKEYVAR3 (KEEP=ID CA82 WTCHG82 BMI82 SMK82 CPD82 XCPD82 AGEQT82 OCCLIFE MENOP82
			ERTEVR82 TOTALC NUTFAT NUTRI ASPIRINC TYLENOLC VITAC VITCC VITEC MULTIC BPC DIURC
		   	THYRC HRTC ACIDC LIBRIUMC SLEEPC TAGAMETC DUR82 XDUR82 OCCNOW ASPIRINC DTINT82) 
 
         SURV82M.CPS2MENORIGINAL (KEEP=ID HT WT WT1YR HD ST SICKNOW CB EPHYS ASTHMA COFFEEC DBT MSITE FSITE S1SITE S2SITE S3SITE S4SITE S5SITE S6SITE ASPIRINX ASPIRINB 
			EDUCATE EXERCISE MARITAL CABBAGE CARROTS GRVEG RAWVEG SQUASH TOMATO SMK_TYPE FRIENDS XCHURCH CLUB GROUP SMKCPD SMKCNYR SMKCAGE SMKCYR XSMKCPD DECAFC
            XSMKCFYR XSMKCNYR XSMKCAGE XSMKCYR SMKCQUIT SMKCFYR BEERC BEERYR BEERPR BEERPRYR WINEC WINEYR WINEPR WINEPRYR LIQC LIQYR LIQPR LIQPRYR BEEF PORK HAM LIVER MEAT 
			SAUSAGE BACON FRHAM)                                            

		 SURV82M.CPS2FEMORIGINAL (KEEP=ID HT WT WT1YR HD ST SICKNOW CB EPHYS ASTHMA COFFEEC DBT MSITE FSITE S1SITE S2SITE S3SITE S4SITE S5SITE S6SITE ASPIRINX ASPIRINB
			EDUCATE EXERCISE MARITAL CABBAGE CARROTS GRVEG RAWVEG SQUASH TOMATO FRIENDS XCHURCH CLUB GROUP SMKCPD SMKCNYR SMKCAGE SMKCYR XSMKCPD DECAFC
            XSMKCFYR XSMKCNYR XSMKCAGE XSMKCYR SMKCQUIT SMKCFYR BEERC BEERYR BEERPR BEERPRYR WINEC WINEYR WINEPR WINEPRYR LIQC LIQYR LIQPR LIQPRYR BEEF PORK HAM LIVER MEAT 
			SAUSAGE BACON FRHAM) 

         PYEAR.cps2menpyr1212_agelim (KEEP=ID BEGDATE BDAYDATE AGELIM DATEDD_AGELIM FAIL_AGELIM) 
         PYEAR.cps2fempyr1212_agelim (KEEP=ID BEGDATE BDAYDATE AGELIM DATEDD_AGELIM FAIL_AGELIM)

		 SOCIAL.ADDRESS
		 MASTER.cps2smgmst15_ssn;          

BY ID;    

IF INMEN OR INFEM; 

	 IF INMEN THEN SEX=0;
ELSE IF INFEM THEN SEX=1;

FORMAT SEX SEX.;

*** FLAG WHITE, BLACK, HISPANIC, AND OTHER/MISSING RACE; 
WHITE=0; BLACK=0; HISPANIC=0; RACEX=0;

IF RACE='1' THEN WHITE=1;  
IF RACE='3' THEN BLACK=1;
IF RACE IN ('2','4','5') THEN HISPANIC=1;  
IF RACE IN ('6','7',' ') THEN RACEX=1;

LABEL BLACK='BLACK RACE';
LABEL WHITE='WHITE RACE';

*****************************************************************; 
*** ALL CAUSE MORTALITY AND CAUSE OF DEATH 						*;
*****************************************************************; 

IF INMORT AND AGELIM=0 THEN DEAD=1;
ELSE DEAD=0;

VS_CL=VS; *CREATING CLEANED VITAL STATUS VARIABLE*;
***************************************************************************;
*CODE FROM JANET*;
*FROM S:\USER\JHILDEBRAND\BMI AND MORTALITY GLOBAL\ANALYTIC POPULATION.sas*;
***************************************************************************;
                                                                                
*****************************************************************; 
*** ALL CAUSE MORTALITY AND CAUSE OF DEATH                                    *;
*****************************************************************; 
ENDFU=MDY(12,31,2012);

*** DEATH ANY CAUSE AND CAUSE-SPECIFIC  -- ALL GROUPS NOT MUTUALLY EXCLUSIVE DO NOT USE ELSE STATEMENT;                                         
ALLCAUSE=0; ALLVAS=0; ALLCHD=0; ALLSTK=0; ALLCAN=0; ALLRES=0;

IF DEAD THEN DO;
      ALLCAUSE=1; 
      DTHDATE=DATEDD_AGELIM;
      AGE_DTH=FLOOR(DTHDATE-BDAYDATE)/365.25 + 1/365.25; 
 
    *** ALL VASCULAR DISEASE; 
   IF (CODETYPE='1' AND CODE1 IN ('01','02','03','04','05','06',                
                                   '07','08','09','10','0A','0B')) OR           
      (CODETYPE='2' AND (('390 ' <= CODE1 <= '4599') | ('798 ' <= CODE1 <= '7989'))) OR
        (CODETYPE='3' AND (('I00 ' <= CODE1 <= 'I999') | ('R96 ' <=CODE1 <= 'R969'))) 
                  THEN ALLVAS = 1;  

    *** ALL CANCER -- NEOPLASTIC DISEASES ;                                                                       
    IF (CODETYPE='1' AND '30' <= CODE1 <= '99' AND CODE1 NE '7C') OR
       (CODETYPE='2' AND ('140 ' <= CODE1 <= '2399')) OR
       (CODETYPE='3' AND (('C00' <= CODE1 <= 'C979') | ('D00 ' <= CODE1 <= 'D489')))
                THEN ALLCAN= 1;            
END;

ELSE DO;
	DEAD=0;
	CODE1=' ';
	CODE2=' ';
	CODE3=' ';
	CODETYPE=' ';

	IF AGELIM=1 THEN VS_CL='A';
END;


*****************************************************************; 
*** DEFINE PREVALENT CHRONIC DISEASE AT BASELINE 				*;  	
*****************************************************************;
*INCLUDED CANCER,STROKE, HEART DISEASE, CHRONIC BRONCHITIS AND EMPHYSEMA*;
PREDIS = 0; 
IF CA82 NOT IN  ('N','73') 
	OR (HD = '1') OR (ST = '1')
	OR   (CB = '1') OR (EPHYS = '1')
	THEN PREDIS = 1;

*********************;
* MAIN EXPOSURES	*;
*********************;

*RECODING FRIENDS - CATEGORIZING NOMINAL CATEGORIES AS CONTINUOUS*;
FRIENDSR=FRIENDS; *KEEPING MISSINGS AS MISSING*;
IF FRIENDS IN (97,99) THEN FRIENDSR=.; *OBS WHO SAID THEY HAD 'VERY FEW' FRIENDS OR RESPONSE WAS UNSPECIFIED BUT INFO WAS GIVEN*;
ELSE IF FRIENDS=98 THEN FRIENDSR=96;

*0/1 VARIABLE FOR CLOSE FRIENDS/RELATIVES*;
IF FRIENDSR GT . AND FRIENDSR LE 6 THEN FRIENDREL=0;
ELSE IF FRIENDSR GE 7 THEN FRIENDREL=1;
ELSE IF FRIENDSR=. THEN FRIENDREL=.;

**********************;
*SOCIAL SUPPORT INDEX*;
**********************;

*MARRIED*;
IF MARITAL IN ('1','2','3','5') THEN MARRIED=0;
ELSE IF MARITAL='4' THEN MARRIED=1;
ELSE MARRIED=.; 

*RECODING CLUB MEMBERSHIP TO COMBINE*;
CLUBR=CLUB;
IF CLUB=97 THEN CLUBR=1;
ELSE IF CLUB=98 THEN CLUBR=96;
ELSE IF CLUB=. THEN CLUBR=0;*IF THE SPACE IS BLANK, MAKING IT A 0*;
ELSE IF CLUB=99 THEN CLUBR=.; *WROTE IN A NON-NUMBER SO MADE IT MISSING*;

*RECODING GROUP MEMBERSHIP TO COMBINE*;
GROUPR=GROUP;
IF GROUP=97 THEN GROUPR=1;
ELSE IF GROUP=98 THEN GROUPR=96;
ELSE IF GROUP=. THEN GROUPR=0;*IF THE SPACE IS BLANK, MAKING IT A 0*;
ELSE IF GROUP=99 THEN GROUPR=.; *WROTE IN A NON-NUMBER SO MADE IT MISSING*;

*COMBINING CLUBS AND GROUPS*;
CLUBGRP=CLUBR+GROUPR; *IF ONE IS MISSING THEN SUM WILL BE MISSING*;

*0/1 VARIABLE FOR CLUB AND GROUP PARTICIPATION*;
IF CLUBGRP=0 THEN CLUBGRP_SCALE=0;
ELSE IF CLUBGRP GE 1 THEN CLUBGRP_SCALE=1;
ELSE CLUBGRP_SCALE=.;

*RECODING CHURCH*;
CHURCHR=XCHURCH;
IF XCHURCH=97 THEN CHURCHR=0;
ELSE IF XCHURCH=98 THEN CHURCHR=96;
ELSE IF XCHURCH=99 THEN CHURCHR=.;

*0/1 VARIABLE FOR CHURCH ATTENDANCE*;
IF CHURCHR=0 THEN CHURCH_SCALE=0;
ELSE IF CHURCHR GE 1 THEN CHURCH_SCALE=1;
ELSE IF CHURCHR=. THEN CHURCH_SCALE=.;

*IF ONE IS MISSING THEN SUM WILL BE MISSING*;
BSINDEX_TEMP=MARRIED+FRIENDREL+CLUBGRP_SCALE+CHURCH_SCALE;

*CHANGING THE ORDERING OF CODING TO MAKE HIGHEST SOCIAL SUPPORT GROUP THE REFERENCE AND LOWEST SOCIAL SUPPORT GROUP THE MOST HIGHLY EXPOSED*;
IF BSINDEX_TEMP=4 THEN BSINDEX=0;
ELSE IF BSINDEX_TEMP=3 THEN BSINDEX=1;
ELSE IF BSINDEX_TEMP=2 THEN BSINDEX=2;
ELSE IF BSINDEX_TEMP=1 THEN BSINDEX=3;
ELSE IF BSINDEX_TEMP=0 THEN BSINDEX=4;

*FORMAT BSINDEX BSINDEXF.;
LABEL BSINDEX='SOCIAL SUPPORT INDEX';

*RECODING BSINDEX FOR RATE MACRO*;
BSINDEXR=BSINDEX+1;

*BS INDEX THAT DOESN'T INCLUDE MARRIAGE - ONLY FRIENDS/CLUBS AND GROUPS/CHURCH ATTENDANCE (SCALE: 0-3)*;
BSINDEX_3COMP_TEMP=FRIENDREL+CLUBGRP_SCALE+CHURCH_SCALE;

FORMAT BSINDEX_3COMP BSINDEX_3COMP.;

*CHANGING THE ORDERING OF CODING TO MAKE HIGHEST SOCIAL SUPPORT GROUP THE REFERENCE AND LOWEST SOCIAL SUPPORT GROUP THE MOST HIGHLY EXPOSED*;
IF BSINDEX_3COMP_TEMP=3 THEN BSINDEX_3COMP=0;
ELSE IF BSINDEX_3COMP_TEMP=2 THEN BSINDEX_3COMP=1;
ELSE IF BSINDEX_3COMP_TEMP=1 THEN BSINDEX_3COMP=2;
ELSE IF BSINDEX_3COMP_TEMP=0 THEN BSINDEX_3COMP=3;

FORMAT MARRIED MARRIED_R. FRIENDREL FRIENDREL_R. CLUBGRP_SCALE CLUBGRP_R. CHURCH_SCALE CHURCH_R.;

************;
*COVARIATES*;
************;
*SMOKING CODE FROM ANUSILA'S PANCREAS ANALYSIS AT S:\USER\ADEKA\PROGRAMS\ALCOHOL AND PANCREAS\FINAL\COHORT with ALC exclusion 6-16-10.sas*;
*USE SMOKEN VARIABLE*;

***SMOKING STATUS***;
*NONSMOKER=0 CURRENT SMOKER=1 FORMER SMOKER=2*;
*EVER SMOKER, STAT UNK=3, EVER SMOKER PIPE/CIGAR=4, AND MISSING/BAD SMOKING INFO=9*;
IF SMK82='1' THEN SMKGRP=0;
ELSE IF SMK82 IN ('2','4') THEN SMKGRP=1;
ELSE IF SMK82 IN ('3','5') THEN SMKGRP=2;
ELSE IF SMK82='6' THEN SMKGRP=3;
ELSE IF SMK82='8' THEN SMKGRP=4;
ELSE SMKGRP=9;   

SMKGRP0=0;
SMKGRP1=0;
SMKGRP2=0;
SMKGRP3=0;
SMKGRP4=0;
SMKGRP9=0;

IF SMKGRP=0 THEN SMKGRP0=1;
ELSE IF SMKGRP=1 THEN SMKGRP1=1;
ELSE IF SMKGRP=2 THEN SMKGRP2=1;
ELSE IF SMKGRP=4 THEN SMKGRP3=1;
ELSE IF SMKGRP=3 THEN SMKGRP4=1;
ELSE IF SMKGRP=9 THEN SMKGRP9=1;

LABEL 	SMKGRP='COLLAPSED SMK VAR 82'
		SMKGRP0='NEVER SMOKER'
		SMKGRP1='CURRENT SMOKER'
		SMKGRP2='FORMER SMOKER'
		SMKGRP3='EVER PIPE/CIGAR'
		SMKGRP4='CURRENT, UNK STATUS'
		SMKGRP9='MISSING'
		;

***CREATE CIGS/DAY CATEGORIES FOR CURRENT SMOKERS***;
IF SMKGRP=1 THEN DO;
	IF CPD82 IN (.,99) THEN CPDA=9;
	ELSE IF CPD82<20 THEN CPDA=1;
	ELSE IF CPD82=20 THEN CPDA=2;
	ELSE IF CPD82>20 THEN CPDA=3;
END;

*****CREATE YEARS SMOKING FOR CURRENT SMOKERS*****;
IF SMKGRP=1 THEN DO;
	IF 0<=DUR82<20 THEN SMKYR=1;
	ELSE IF 20<=DUR82<30 THEN SMKYR=2;
	ELSE IF DUR82>=30 THEN SMKYR=3;
	ELSE SMKYR=9;
END;

*****CREATE YEARS SINCE QUIT SMOKING FOR FORMER SMOKERS*****;
IF SMKGRP=2 THEN DO;
	YEARSQT=INT(AGE_INT-AGEQT82);
	IF AGEQT82 IN (.M,.U) THEN FSMKGRP=9;
	ELSE IF YEARSQT<10 THEN FSMKGRP=1;
	ELSE IF 10<=YEARSQT<20 THEN FSMKGRP=2;
	ELSE IF YEARSQT>=20 THEN FSMKGRP=3;
END;

***SMOKING VAR BASED ON ERIC'S RECOMMENDATIONS TO SEPARATE CURRENT CIG SMOKERS WHO
	REPORTED CIGAR SMOKING TOO***;

	***WOMEN*** (VAR STAYS THE SAME);
	IF SEX=1 THEN DO;
		IF SMK82 IN ('1','8') THEN SMKSTAT=1; *NEVER SMOKER*;
		ELSE IF SMK82 IN ('3','5') THEN SMKSTAT=2; *FORMER SMOKER*;
		ELSE IF SMK82 IN ('2','4') THEN SMKSTAT=3; *CURRENT SMOKER*;
		ELSE IF SMK82 IN ('6','7') THEN SMKSTAT=9; *UNKNOWN*;
	END;

	***MEN***;
	IF SEX=0 THEN DO;
		IF SMK_TYPE IN ('1','4','7') THEN SMKSTAT=1; *NEVER CIGARETTE*;
		ELSE IF SMK_TYPE IN ('5','6','8') THEN SMKSTAT=2; *FORMER CIG = EX CIG OR EX CIG/EX PIPE
														CIGAR OR EX CIG, CURRENT PIPE CIGAR*;
		ELSE IF SMK_TYPE IN ('2','3') THEN SMKSTAT=3; *CURRENT CIG OR CURRENT CIG AND PIPE/CIGAR*;
		ELSE IF SMK_TYPE='9' THEN SMKSTAT=9; *UNKNOWN*;
	END;

	LABEL SMKSTAT='CIG SMK STATUS 82';

	***CREATE CIGAR	VARS***;
	CIGARSTAT=1; *NEVER PIPE/CIGAR SMOKER*;
	IF SMK82='8' THEN CIGARSTAT=2; *EVER PIPE/CIGAR SMOKER*;
	LABEL CIGARSTAT='CIGAR SMOKING STATUS 1982';

	***CREATE NEW SMOKING VARS***;
	CIGAR=9; 											*UNKNOWN CIGAR*;
	IF SEX=1 THEN CIGAR=1; 							*NEVER CIGAR*;
	ELSE IF SMK_TYPE IN ('1','2','5','9') THEN CIGAR=1; *NEVER CIGAR*;
	ELSE IF SMK_TYPE IN ('6','7') THEN CIGAR=2;			*FORMER CIGAR*;
	ELSE IF SMK_TYPE IN ('3','4','8') THEN CIGAR=3;		*CURRENT CIGAR*;
	LABEL CIGAR='CIGAR SMOKING STATUS 1982';

	*COMBINE CIGARETTE AND SMOKING VARIABLES***;
	SMKCIG=9;
	IF SMKSTAT=9 					THEN SMKCIG=9;	*UNKNOWN SMOKER*;
	ELSE IF SMKSTAT=3 				THEN SMKCIG=4; 	*CURRENT CIGARETTE*;
	ELSE IF SMKSTAT=2 AND CIGAR=3 	THEN SMKCIG=3;	*CURRENT CIGAR* (FORMER CIG);
	ELSE IF SMKSTAT=2 AND CIGAR=9 	THEN SMKCIG=9;	*UNKNOWN SMOKER*;
	ELSE IF SMKSTAT=2 				THEN SMKCIG=2; 	*FORMER CIGARETTE/CIGAR*;
	ELSE IF SMKSTAT=1 AND CIGAR=1 	THEN SMKCIG=1;	*NEVER CIGARETTE/CIGAR*;
	ELSE IF SMKSTAT=1 AND CIGAR=2	THEN SMKCIG=2;	*FORMER CIGAR*;
	ELSE IF SMKSTAT=1 AND CIGAR=3 	THEN SMKCIG=3;	*CURRENT CIGAR*;
	ELSE IF SMKSTAT=1				THEN SMKCIG=9;	*UNKNOWN SMOKER*;

	*IN CIGAR CATEGORY, DIFFERENTIATE BETWEEN CURRENT, FORMER, AND NEVER CIGARETTE SMOKERS*;
	
  	IF SMK82='8' THEN DO;
		IF (SMKCPD NE 0 AND SMKCPD NE . )
		OR (SMKCFYR NE . AND SMKCFYR NE 0)
		OR (SMKCNYR NE . AND SMKCNYR NE 0)
		OR (SMKCAGE NE . AND SMKCAGE NE 0)
		OR (SMKCYR NE . AND SMKCYR NE 0) THEN CURRSMK=1; 
		ELSE CURRSMK=0;

		IF (XSMKCPD NE 0 AND XSMKCPD NE . )
		OR (XSMKCFYR NE . AND XSMKCFYR NE 0)
		OR (XSMKCNYR NE . AND XSMKCNYR NE 0)
		OR (XSMKCAGE NE . AND XSMKCAGE NE 0)
		OR (XSMKCYR NE . AND XSMKCYR NE 0)
		OR (SMKCQUIT NE . AND SMKCQUIT NE 0)THEN FORMSMK=1; 
		ELSE FORMSMK=0;

		*CREATE DURATION OF SMOKING VAR FOR THOSE WITH SMK82=8 (EVER PIPE/CIGAR)*;

		DUR82N=DUR82;
		XDUR82N=XDUR82;

			*CURRENT SMOKERS*;
			IF CURRSMK=1 AND FORMSMK=0 THEN DO;
				IF (SMKCPD NE . AND SMKCPD NE 0 AND SMKCPD NE 99) AND
				((SMKCAGE NE . AND SMKCAGE NE 0 AND SMKCAGE LE AGE_INT) OR (SMKCYR NE .))THEN DO;
                	SMK82_N='2';
					IF SMKCYR NE . THEN DUR82N=SMKCYR;
					ELSE IF (SMKCAGE NE . AND SMKCAGE NE 0 AND SMKCAGE LE AGE_INT)
						THEN DUR82N=AGE_INT-SMKCAGE;
            	END;
            	ELSE DO;
                	SMK82_N='4';
					IF SMKCYR NE . THEN DUR82N=SMKCYR;
					ELSE IF (SMKCAGE NE . AND SMKCAGE NE 0 AND SMKCAGE LE AGE_INT) 
						THEN DUR82N=AGE_INT-SMKCAGE;
            	END;
			END;

        	*FORMER SMOKERS*;
        	ELSE IF CURRSMK=0 AND FORMSMK=1 THEN DO;
				IF (SMKCQUIT NE . AND SMKCQUIT NE 0) AND
				(XSMKCPD NE . AND XSMKCAGE NE 0 AND SMKCQUIT>=XSMKCAGE) OR (XSMKCYR NE .)THEN DO;
					SMK82_N='3';
					IF XSMKCYR NE . THEN XDUR82N=XSMKCYR;
					ELSE IF (XSMKCAGE NE . AND XSMKCAGE NE 0 AND SMKCQUIT>=XSMKCAGE)
                	THEN XDUR82N=SMKCQUIT-XSMKCAGE;
           		END;
            	ELSE DO;
                	SMK82_N='5';
					IF XSMKCYR NE . THEN XDUR82N=XSMKCYR;
					ELSE IF (XSMKCAGE NE . AND XSMKCAGE NE 0 AND SMKCQUIT>=XSMKCAGE)
					THEN XDUR82N=SMKCQUIT-XSMKCAGE;
            	END;
        	END;

        *NEVER SMOKER, UNCLASSIFIABLES*;
        ELSE IF CURRSMK=1 AND FORMSMK=1 THEN SMK82_N='6'; *EVER SMOKER- NOT CLASSIFIABLE*;
        ELSE IF CURRSMK=0 AND FORMSMK=0 THEN SMK82_N='1'; *NEVER CIGARETTE SMOKER;
    END;


***CREATE NEW SMOKING VARIABLE WITH CIGARETTE SMOKERS IN SMK82=8 FOR EVER SMOKING;
		 IF SMK82='1' OR SMK82_N='1' THEN SMK82_EV=0; *NO CIGARETTE SMOKING;
    ELSE IF SMK82 IN ('2','3','4','5','6') OR SMK82_N IN ('2','3','4','5','6') THEN SMK82_EV=1;
    ELSE IF SMK82 IN ('7') THEN SMK82_EV=9;

***CREATE NEW STATUS AT BASELINE (NEVER, CURRENT, FORMER);
         IF SMK82 IN ('2','4') OR SMK82_N IN ('2','4') 	THEN SMK82N=1; *CURRENT CIGARETTE*;
    ELSE IF SMK82='8' AND CIGAR=3 AND SMK82_N NE '6' 	THEN SMK82N=2; *CURRENT CIGAR*;
    ELSE IF SMK82 IN ('3','5') OR SMK82_N IN ('3','5') 	THEN SMK82N=3; *FORMER CIGARETTE*;
    ELSE IF SMK82='8' AND CIGAR=2 AND SMK82_N NE '6' 	THEN SMK82N=4; *FORMER CIGAR*;
    ELSE IF SMK82_EV=0                                	THEN SMK82N=0; *NEVER*;
    ELSE IF SMK82='6' OR SMK82_N='6'			     	THEN SMK82N=9; *EVER, UNCLASSIFIED*;
    ELSE                                                     SMK82N=9; *UNKNOWN*;


***CREATE CIGS/DAY CATEGORIES FOR CURRENT SMOKERS***;
IF SMK82N=1 THEN DO;
	IF CPD82 IN (.,99) THEN CPDAN=9;
	ELSE IF CPD82<20 THEN CPDAN=1;
	ELSE IF CPD82>=20 THEN CPDAN=2;
END;

*****CREATE YEARS SMOKING FOR CURRENT SMOKERS*****;
IF SMK82N=1 THEN DO;
	IF 0<=DUR82<35 THEN SMKYRN=1;
	ELSE IF DUR82>=35 THEN SMKYRN=2;
	ELSE SMKYRN=9;
END;

*****CREATE YEARS SINCE QUIT SMOKING FOR FORMER SMOKERS*****;
IF SMK82N=3 THEN DO;
	YEARSQTN=INT(AGE_INT-AGEQT82);
	IF AGEQT82 IN (.M,.U) THEN FSMKGRPN=9;
	ELSE IF YEARSQTN<10 THEN FSMKGRPN=1;
	ELSE IF 10<=YEARSQTN<20 THEN FSMKGRPN=2;
	ELSE IF YEARSQTN>=20 THEN FSMKGRPN=3;
END;

*** CREATE COMBINED VARIABLE FOR SMOKE STATUS, #/DAY AND DURATION ***;
*** DURATION AND #/DAY FOR CURRENT SMOKERS ONLY ******;
*** YEARS SINCE QUITTING FOR FORMER SMOKERS*****;

IF SMK82N=0 THEN SMOKEN=0; **NEVER SMOKERS**;

ELSE IF SMK82N=1 THEN DO; **CURRENT SMOKERS**;     	  **CIG/DAY**DUR**;
     IF CPDAN=1 AND SMKYRN=1          THEN SMOKEN=1;  *<20   	*<35*;
ELSE IF CPDAN=2 AND SMKYRN=1          THEN SMOKEN=2;  *>=20  	*<35*;

ELSE IF CPDAN=1 AND SMKYRN=2          THEN SMOKEN=3;  *<20   	*>=35*;
ELSE IF CPDAN=2 AND SMKYRN=2          THEN SMOKEN=4;  *>=20  	*>=35*;

ELSE                                       SMOKEN=5; *CIG/DAY UNK OR DUR UNK;
END;

ELSE IF SMK82N=3 THEN DO;  **FORMER SMOKERS**;		  **YRS SINCE QUIT**;
	 IF FSMKGRPN=1 					  THEN SMOKEN=6;   *<10 SINCE*;
ELSE IF FSMKGRPN=2					  THEN SMOKEN=7;   *10-<20 SINCE*;
ELSE IF FSMKGRPN=3					  THEN SMOKEN=8;   *>=20 SINCE*;
ELSE 								       SMOKEN=9;  *YRS SINCE UNK.*;
END;

ELSE IF SMK82N=2 THEN SMOKEN=10;     					*CURRENT CIGAR*;
ELSE IF SMK82N=4 THEN SMOKEN=11;						*FORMER CIGAR*;
 
ELSE IF SMK82N=9 THEN SMOKEN=99;                      **MISSING OR BAD SMOKING INFO**;

*FORMAT SMOKEN SMOKEN.; 
LABEL SMOKEN='11 CATEGORY SMOKE VARIABLE';

*COLLAPSING SMOKEN INTO X CATEGORIES PER SG - 7/2/15*;
IF SMOKEN=0 THEN SMOKENEW=0; *NEVER*;
IF SMOKEN IN (1,3) THEN SMOKENEW=1; *CURRENT, <20 CIGS/DAY*;
ELSE IF SMOKEN IN (2,4) THEN SMOKENEW=2; *CURRENT, >=20 CIGS/DAY*;
ELSE IF SMOKEN=5 THEN SMOKENEW=3; *CURRENT, UNKNOWN CIGS/DAY OR DURATION*;
ELSE IF SMOKEN=6 THEN SMOKENEW=4; *FORMER, <10 SINCE*;
ELSE IF SMOKEN=7 THEN SMOKENEW=5; *FORMER, 10-<20 SINCE*;
ELSE IF SMOKEN=8 THEN SMOKENEW=6; *FORMER, >=20 SINCE*;
ELSE IF SMOKEN=9 THEN SMOKENEW=7; *FORMER, YRS SINCE UNK.*;
ELSE IF SMOKEN IN (10,11) THEN SMOKENEW=8; *ALL CIGAR*;

FORMAT SMOKENEW SMOKENEW.;

*USE BMI82 FOR CONTINUOUS VARIABLE*;
***BMI (WHO)****;
IF .<BMI82<18.5 THEN BMI=1;
ELSE IF 18.5<=BMI82<25 THEN BMI=2;
ELSE IF 25<=BMI82<30 THEN BMI=3;
ELSE IF 30<=BMI82 THEN BMI=4;
ELSE IF BMI82=. THEN BMI=9;

LABEL BMI='WHO Body Mass Index (BMI)'; FORMAT BMI BMI.;

**EDUCATION***;
IF EDUCATE IN('1','2') THEN EDUNEW=1;
ELSE IF EDUCATE IN('3') THEN EDUNEW=2;
ELSE IF EDUCATE IN('4','5') THEN EDUNEW=3;
ELSE IF EDUCATE IN('6','7') THEN EDUNEW=4;
ELSE IF EDUCATE=' ' THEN EDUNEW=9;
LABEL EDUNEW='Education'; FORMAT EDUNEW EDUNEW.;

***DIABETES IN 1982***;
	DB82=0;
	IF DBT='1' THEN DB82=1;

LABEL DB82='1982 DIABETES STATUS';
FORMAT DB82 YESNO.;


*FAMILY HISTORY OF CANCER-FROM CHRISSY NEWTON.

	***FAMILY HISTORY OF CANCER AMONG BLOOD RELATIVES (RELATIVE AFFECTED, AGE AT ONSET);
		FAMHX=0; *NO-FAMILY HISTORY;
		IF MSITE  NOTIN ('N','73',' ') OR FSITE  NOTIN ('N','73',' ') OR S1SITE NOTIN ('N','73',' ') OR 
		   S2SITE NOTIN ('N','73',' ') OR S3SITE NOTIN ('N','73',' ') OR S4SITE NOTIN ('N','73',' ') OR 
		   S5SITE NOTIN ('N','73',' ') OR S6SITE NOTIN ('N','73',' ') THEN FAMHX=1; *YES-FAMILY HISTORY;
		LABEL FAMHX='Family History of Cancer';
		FORMAT FAMHX YESNO.;


IF SEX=1 THEN DO;

**** MENOPAUSAL STATUS ****;

IF MENOP82 = '1'  THEN MENO =1;
	ELSE IF MENOP82='2' THEN MENO =2;
	ELSE IF MENOP82='3' THEN MENO =3;
	ELSE IF MENOP82 IN('4','5') THEN MENO =4;
	ELSE IF MENOP82='9' THEN MENO =9;
	
LABEL MENO ='Menopausal status';

** EVER/NEVER  - JUST FOR POST-MENOPAUSAL WOMEN **;
IF ERTEVR82 IN('0','1','9') AND MENO  IN(1,2,9) THEN ERTEVER =1;	*** IF PRE/PERI UNKNOWN;
	ELSE IF MENO  IN(3,4) AND ERTEVR82='0' THEN ERTEVER =2;	*** NEVER ***;
	ELSE IF MENO  IN(3,4) AND ERTEVR82='1' THEN ERTEVER =3;	*** EVER ***;
	ELSE IF MENO  IN(3,4) AND ERTEVR82='9' THEN ERTEVER =9;	*** MISSING ***;
LABEL ERTEVER ='Postmenopausal ERT'; FORMAT ERTEVER HORMONE.;
END;

IF SEX=0 THEN ERTEVER=2; *ADDING MEN TO THE NEVER USER GROUP*;

*****VEGGIES/RED MEAT - FROM CHRISSY NEWTON, ORIGINALLY FROM CARI*****;  
                                                                        
	ARRAY ITEM(*) BEEF PORK HAM LIVER MEAT SAUSAGE BACON FRHAM ; 
	DO I = 1 TO DIM(ITEM);                                                  
	     IF ITEM(I) = . THEN ITEM(I) =0;                 
	ELSE IF ITEM(I) = 9 THEN ITEM(I) =0.25;                 
	ELSE IF ITEM(I) = 8 THEN ITEM(I) =0.50;                 
	END;                     
   
	PROCMT82 = SUM(HAM,MEAT,SAUSAGE,BACON);                                              
	FRRDMT82 = SUM(BEEF,PORK,LIVER,FRHAM);                                                                              
	REDPRC82 = SUM(PROCMT82,FRRDMT82);                                         
	                                                                        
	LABEL  PROCMT82 = 'PROCESSED MEATS 1982 IN DAYS/WEEK'                     
	       FRRDMT82 = 'FRESH RED MEATS 1982 IN DAYS/WEEK'                
	       REDPRC82 = 'TOTAL FRESH RED AND PROCESSED MEATS 1982 IN DAYS/WK';

	*SET INCOMPLETE INFO EQUAL TO MISSING*;
	IF NUTRI = '1' THEN DO;                                                 
	   PROCMT82 = .; FRRDMT82 = .; REDPRC82 = .;                                                         
	END; 

*IMPUTING MISSING VALUES FOR CONTINUOUS RED/PROCESSED MEAT BY USING THE MEDIAN FOR EACH SEX/RACE GROUP - SEE UNIVARIATES AT BOTTOM OF CODE*;
IF REDPRC82=. THEN DO;
	IF SEX=0 AND BLACK=1 THEN REDPRC82=7.5;
	ELSE IF SEX=1 AND BLACK=1 THEN REDPRC82=6.5;
	ELSE IF SEX=0 AND WHITE=1 THEN REDPRC82=7.5;
	ELSE IF SEX=1 AND WHITE=1 THEN REDPRC82=6.0;
END;

****CREATING CATEGORIES FOR TOTAL RED & PROCESSED MEAT 1982*****;  

	     IF         REDPRC82 = .  THEN RDPRGRP =9;  *Missing info;
	ELSE IF         REDPRC82 <= 3 THEN RDPRGRP =1;  *3 or less;                     
	ELSE IF    3 <  REDPRC82 <= 5 THEN RDPRGRP =2;  *>3-5;                     
	ELSE IF    5 <  REDPRC82 <= 7 THEN RDPRGRP =3;  *>5-7;                     
	ELSE IF    7 <  REDPRC82      THEN RDPRGRP =4;  *8+;                     
                     
	LABEL RDPRGRP = 'Red and Processed Meat';
	FORMAT RDPRGRP RDPRGRP.;


*ALCOHOL USE - USE DRINKS VARIABLE FROM MACRO*;
%ALCVARS2; * sue and janet's macro *;
     IF ALCNEW IN(1,2) THEN ALCOHOL=0;
ELSE IF ALCNEW=99 THEN ALCOHOL=9;
ELSE ALCOHOL=1;
LABEL ALCOHOL="CURRENT ALCOHOL USE YES/NO";
FORMAT ALCOHOL ALCOHOL.;

LABEL DRINKS ='1982 CONTINUOUS DRINKS/DAY';

***ASPIRIN- FROM ASPIRIN/TOTAL CANCER ANALYSIS***;     
	*ASPIRINX,TYLENOLX:90-UNINTERP.,95-95+,96-ENTIRE SECTION BLANK,97-AS NEEDED,98-OCCASIONAL,99-CHECK MARK;
	ASPGRP=9;
		 IF ASPIRINX IN (.,0,96) AND ASPIRINB IN (.,0,96) THEN ASPGRP=0; *NEVER;
	ELSE IF ASPIRINX IN (97,98) THEN ASPGRP=1; *OCCASIONAL;
	ELSE IF 1<=ASPIRINX<15 		THEN ASPGRP=2; *<15/MONTH;
	ELSE IF 15<=ASPIRINX<30 	THEN ASPGRP=3; *15-<30/MONTH;
	ELSE IF 30<=ASPIRINX<90 OR 91<=ASPIRINX<=95 THEN ASPGRP=4; *30+/MONTH;
	LABEL ASPGRP='ASPIRIN USE IN 1982';
	FORMAT ASPGRP ASP.;

*CONTINUOUS COFFEE*;

	* ASSIGN NUMERIC VALUE (0.25) FOR OCCASIONAL DRINKERS AND CHANGE MISSING TO 0. (MISSING IS CONSIDERED A NONDRINKER IN THIS ANALYSIS);
        IF 50 LE COFFEEC LE 96 OR COFFEEC IN (98, 99) THEN CAFAMT=.25;
		ELSE IF COFFEEC=. THEN CAFAMT=0;
		ELSE CAFAMT=COFFEEC;

        IF 50 LE DECAFC LE 96 OR DECAFC IN (98, 99) THEN DECAMT=.25;
		ELSE IF DECAFC=. THEN DECAMT=0;
		ELSE DECAMT=DECAFC;

	*CREATE TOTAL COFFEE AMOUNT (CONTINUOUS VARIABLE);
 		COFFEET=CAFAMT+DECAMT;

LABEL COFFEET='Total coffee, continuous';

*1982 REGION CODE FROM RYAN DIVER, BASED OFF OF MICHELLE TURNER'S REGION CODING*;
IF STATE IN ('CT','ME','MA','NH','NJ','NY','PA','RI','VT') THEN REGION=1;
ELSE IF STATE IN ('AL','AR','DE','DC','FL','GA','KY','LA','MD','MS','NC','SC','TN','TX','VA','WV','OK') THEN REGION=2;
ELSE IF STATE IN ('IL','IN','IA','KS','MI','MN','OH','WI','MO','NE','ND','SD') THEN REGION=3;
ELSE IF STATE IN ('AZ','CO','ID','MT','NV','NM','OR','UT','WA','WY','CA','AK','HI') THEN REGION=4; 
ELSE REGION=9; *MISSING/UNKNOWN*;

FORMAT REGION REGION.;

*PHYSICAL ACTIVITY*;
PA82=INPUT(EXERCISE,1.);
IF EXERCISE=' ' THEN PA82=9;
FORMAT PA82 PA.;
LABEL PA82='1982 PHYSICAL ACTIVITY';

*CREATING BIRTH COHORT VARIABLES*;

IF YEAR(BDAYDATE) LE 1899 THEN B_COHORT=1; *NO ONE IS MISSING BIRTHDAY*;
ELSE IF YEAR(BDAYDATE) GE 1900 AND YEAR(BDAYDATE) LE 1909 THEN B_COHORT=2;
ELSE IF YEAR(BDAYDATE) GE 1910 AND YEAR(BDAYDATE) LE 1919 THEN B_COHORT=3;
ELSE IF YEAR(BDAYDATE) GE 1920 AND YEAR(BDAYDATE) LE 1929 THEN B_COHORT=4;
ELSE IF YEAR(BDAYDATE) GE 1930 AND YEAR(BDAYDATE) LE 1939 THEN B_COHORT=5;
ELSE IF YEAR(BDAYDATE) GE 1940 AND YEAR(BDAYDATE) LE 1949 THEN B_COHORT=6;
ELSE IF YEAR(BDAYDATE) GE 1950 THEN B_COHORT=7;

************************;
*FOLLOW UP TIME PERIODS*;
************************;
*CALCULATING FAIL TIME IN MONTHS FOR THE AGE LIMITED FAIL VARIABLE*;
FAILAGELIM_MO=(FAIL_AGELIM)*12;

BEG2B=.; BEG3B=.; ENDDATE=MDY(12,31,2012);
END1B=MDY(12,31,1991); END2B=MDY(12,31,2001);
DATEDD1B=DATEDD_AGELIM; DATEDD2B=DATEDD_AGELIM; DATEDD3B=DATEDD_AGELIM; 

IF DATEDD_AGELIM > END1B THEN DO; DATEDD1B=END1B; BEG2B=MDY(1,1,1992); END;                             
IF DATEDD_AGELIM > END2B THEN DO; DATEDD2B=END2B; BEG3B=MDY(1,1,2002); END; 
IF DATEDD_AGELIM > ENDDATE THEN DATEDD3B=ENDDATE;                        
   
FAIL1B = (DATEDD1B-BEGDATE)/365.25 + 1/365.25; *IN YEARS*;                 
IF DATEDD2B > END1B THEN FAIL2B = (DATEDD2B - END1B)/365.25;          
IF DATEDD3B > END2B THEN FAIL3B = (DATEDD3B - END2B)/365.25; 

*ADDING IN ATTAINED AGE VARIABLES*;
BEGAGE82=BEGDATE-BDAYDATE;
BEGAGE82_YRS=BEGAGE82/365.25+1/365.25; *IN YEARS*;

*CONVERTING FAIL TIME IN YEARS TO MONTHS SO MODELS WILL RUN FASTER*;
FAIL1B_MO=INT(FAIL1B*12);
FAIL2B_MO=INT(FAIL2B*12);
FAIL3B_MO=INT(FAIL3B*12);

IF FAIL1B_MO=0 THEN FAIL1B_MO=0.5;
IF FAIL2B_MO=0 THEN FAIL2B_MO=0.5;
IF FAIL3B_MO=0 THEN FAIL3B_MO=0.5;

*** CENSORING VARIABLES FOR EACH FAIL TIME - THE ORDER IS IMPORTANT;
*** DEATH FROM ANY CAUSE;
CORE1B=0; CORE2B=0; CORE3B=0;         
SELECT (ALLCAUSE);                          
  WHEN (1) DO;                        
    IF FAIL3B NE . THEN CORE3B=1;        
    ELSE IF FAIL2B NE . THEN CORE2B=1;        
    ELSE IF FAIL1B NE . THEN CORE1B=1;        
  END;                                      
  OTHERWISE;                                
END; 

*** DEATH FROM CANCER;
CORE1B_CCR=0; CORE2B_CCR=0; CORE3B_CCR=0;         
SELECT (ALLCAN);                          
  WHEN (1) DO;                        
    IF FAIL3B NE . THEN CORE3B_CCR=1;        
    ELSE IF FAIL2B NE . THEN CORE2B_CCR=1;        
    ELSE IF FAIL1B NE . THEN CORE1B_CCR=1;        
  END;                                      
  OTHERWISE;                                
END; 

*** DEATH FROM CARDIOVASCULAR DISEASE;
CORE1B_VAS=0; CORE2B_VAS=0; CORE3B_VAS=0;         
SELECT (ALLVAS);                          
  WHEN (1) DO;                        
    IF FAIL3B NE . THEN CORE3B_VAS=1;        
    ELSE IF FAIL2B NE . THEN CORE2B_VAS=1;        
    ELSE IF FAIL1B NE . THEN CORE1B_VAS=1;        
  END;                                      
  OTHERWISE;                                
END; 



*************************************************;
*EXCLUSIONS                                  	*;
*************************************************;

***NO PERSON-TIME PRIOR TO AGE LIMIT***;
EX1=0;
IF FAIL_AGELIM=. THEN EX1=1;

***PREVALENT CANCER, STROKE OR HEART DISEASE****;
EX2=0;
IF PREDIS=1 THEN EX2=1;

***FRIENDS, MARTITAL STATUS, CLUBS/GROUPS/CHURCH***;
*ANY/ALL SCALE VARIABLES MISSING ->SCALE MISSING*;
EX3=0;
IF BSINDEX=. THEN EX3=1;

*OTHER/MISSING RACE*;
EX4=0;
IF RACEX=1 OR HISPANIC=1 THEN EX4=1;

*INCOMPLETE SMOKING DATA*;
EX5=0;
IF SMOKEN=99 THEN EX5=1;

*MISSING SEX*;
EX6=0;
IF SEX=. THEN EX6=1;

***Final Exclusion Cascade***;
IF EX1=1 THEN EXCLUSN=1;
ELSE IF EX2=1 THEN EXCLUSN=2;
ELSE IF EX3=1 THEN EXCLUSN=3;
ELSE IF EX4=1 THEN EXCLUSN=4;
ELSE IF EX5=1 THEN EXCLUSN=5;
ELSE IF EX6=1 THEN EXCLUSN=6;
ELSE EXCLUSN=99;

RUN;

DATA SOCIAL.FINALCOHORT;
SET SOCIAL.INITIALCOHORT;
IF EXCLUSN=99;
RUN;

*OUTPUTTING SMALLER DATASET THAT WILL IMPORT TO R MORE QUICKLY*;
DATA SOCIAL.FINALCOHORTR (KEEP=ID FAIL_AGELIM ALLCAUSE BSINDEX SMOKENEW EDUNEW BMI DB82 AGE_INT SEX BLACK WHITE);
SET SOCIAL.FINALCOHORT;
RUN; 


DATA SOCIAL.BLKMALE SOCIAL.BLKFEM SOCIAL.WHTMALE SOCIAL.WHTFEM;                         
SET SOCIAL.FINALCOHORT;

*BLACK MALES*;
IF SEX=0 AND BLACK=1 THEN OUTPUT SOCIAL.BLKMALE;

*BLACK FEMALES*;
IF SEX=1 AND BLACK=1 THEN OUTPUT SOCIAL.BLKFEM;

*WHITE MALES*;
IF SEX=0 AND WHITE=1 THEN OUTPUT SOCIAL.WHTMALE;

*WHITE FEMALES*;
IF SEX=1 AND WHITE=1 THEN OUTPUT SOCIAL.WHTFEM;

RUN; 


PROC CONTENTS DATA=SOCIAL.FINALCOHORT; RUN;




*USING A COHORT THAT HAS THE CUTOFF AS 12/31/1997*;
DATA TRUNC_COHORT;
SET ALL;

IF EXCLUSN=99;

RUN;

*COHORT CODE WITH END OF FU ON 12/31/1997*;
/*PROC FREQ DATA=TRUNC_COHORT;*/
/*WHERE ALLCAUSE=1;*/
/*TABLES SEX*WHITE SEX*BLACK/LIST MISSING;
/*RUN;*/
/**/
/*PROC FREQ DATA=TRUNC_COHORT;*/
/*WHERE ALLCAN=1;*/
/*TABLES SEX*WHITE SEX*BLACK/LIST MISSING;*/
/*RUN;*/
/**/
/*PROC FREQ DATA=TRUNC_COHORT;*/
/*WHERE ALLVAS=1;*/
/*TABLES SEX*WHITE SEX*BLACK/LIST MISSING;*/
/*RUN;*/
/**/
/*PROC FREQ DATA=TRUNC_COHORT;*/
/*TABLES ALLCAUSE ALLCAN ALLVAS;*/
/*RUN;*/
/**/
/*PROC FREQ DATA=SOCIAL.FINALCOHORT;*/
/*TABLES ALLCAUSE ALLCAN ALLVAS;*/
/*RUN;*/

DATA FCOHORT;
SET ALL;
IF EXCLUSN=99;
RUN;

/**BLACK MALES - MEDIAN: 7.5*;*/
/*PROC UNIVARIATE DATA=SOCIAL.FINALCOHORT;*/
/*WHERE SEX=0 AND BLACK=1;*/
/*VAR REDPRC82;*/
/*RUN;*/
/**/
/**BLACK FEMALES - MEDIAN: 6.5*;*/
/*PROC UNIVARIATE DATA=SOCIAL.FINALCOHORT;*/
/*WHERE SEX=1 AND BLACK=1;*/
/*VAR REDPRC82;*/
/*RUN;*/
/**/
/**WHITE MALES - MEDIAN 7.5*;*/
/*PROC UNIVARIATE DATA=SOCIAL.FINALCOHORT;*/
/*WHERE SEX=0 AND WHITE=1;*/
/*VAR REDPRC82;*/
/*RUN;*/
/**/
/**WHITE FEMALES - MEDIAN 6.0*;*/
/*PROC UNIVARIATE DATA=SOCIAL.FINALCOHORT;*/
/*WHERE SEX=1 AND WHITE=1;*/
/*VAR REDPRC82;*/
/*RUN;*/


