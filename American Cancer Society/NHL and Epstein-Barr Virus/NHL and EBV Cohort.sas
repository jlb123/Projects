/*============================================================================================================
  Project Name     : NHL & EBV Cohort Creation
  PI               : LAUREN TERAS
  Last update      : 6/28/2013 - CHANGED SO IT WOULD RUN ON JENNY BLASE S-DRIVE
  					 7/15/2013 - JB - REWORKED PROGRAM TO INCORPORATE ALL PREVIOUS PROGRAMS
					 1/2/2014 - JB - CHANGED COVARIATES TO BE FROM 1999 IN ACCORDANCE WITH POLYOMAVIRUS ANALYSIS
  ===========================================================================================================*/
 
TITLE 'NHL and EBV' ; 
 
    FOOTNOTE1  "Program: D:\USER\JBLASE\EBV\EBV and NHL Cohort.sas"; 
    FOOTNOTE2  "Last run by JENNY BLASE on &sysdate";

LIBNAME EBV    'D:\USER\JBLASE\EBV Program Review\DATA'; 
LIBNAME ORIG82 'D:\CPS\CPS2\ORIGINAL';
LIBNAME ORG    'D:\CPS\NUTRITION\ORIGINAL';
LIBNAME DER    'D:\CPS\NUTRITION\DERIVED';
LIBNAME LLDRIV 'D:\CPS\BIOSPECIMENS\Lifelink\Derived';
LIBNAME VERIFY 'D:\CPS\NUTRITION\VERIFIED';
LIBNAME MAST   'D:\CPS\MASTER';


PROC FORMAT; 

VALUE  CACOF   1 = "1: CASE"
			   0 = "0: CONTROL"; 

VALUE $ SMOKE '1' = 'NEVER'
			  '2' = 'CURRENT'
			  '3' = 'FORMER'
			  '9' = 'MISSING';

VALUE BMIWHO 	0='0: <25.0 (UNDER/NORMAL)'				
			 	1='1: 25.0-<30.0 (OVERWEIGHT)'
			 	2='2: >=30.0 (OBESE)'
			 	9='9: UNKNOWN'; 

VALUE $ REGIONF  '1' = '1: Northeast' 
				 '2' = '2: West'      
				 '3' = '3: South'     
				 '4' = '4: MidWest'; 

VALUE EDUCAF    1 = '1: Less than high school'
				2 = '2: High school/missing education'
				3 = '3: Some college/vocational school'
				4 = '4: College/graduate school'; 

VALUE QUARTS    1 = '<= 25 Percentile'
                2 = '25-75 Percentile'
			   	3 = '>75   Percentile'; 

VALUE NPF      0 = '0. Seronegative'
               1 = '1. Seropositive'; 

VALUE EXCLUSION 1 = 'Missing lab data'
				2 = 'Hodgkin Lymphoma'
				3 = 'Multiple Myeloma'
				.='Final Cohort';

VALUE SUB1CAT	0 = 'Control'
				1 = 'DLBCL'
				2 = 'CLL/SLL'
				3 = 'Follicular'
				4 = 'Other NHL';

VALUE SEX		0 = 'Female'
				1 = 'Male';

VALUE ALC	1 = '0 DRINKS/DAY'
			2 = '<=1 DRINKS/DAY'
			3 = '>1 DRINKS/DAY'
			9 = 'MISSING';

VALUE RACENEW 1 = 'White'
			  2 = 'Black'
			  3 = 'Other/Missing';

RUN;

*========================== MERGING BIOSPECIMEN DATA WITH CACO DATA AND REMOVING QC OBSERVATIONS =============================; 

PROC SORT DATA=EBV.INFNHL1000 OUT = INFNHL1000_TEMP; BY BARCODE; RUN; 
PROC SORT DATA=EBV.QCS OUT = QCS_TEMP; BY BARCODE; RUN; 
PROC SORT DATA=EBV.CACO391 OUT = CACO391; BY BARCODE; RUN;


DATA ALYDATA;
MERGE INFNHL1000_TEMP (KEEP=BARCODE EA_D EBNA_TRUNCATED EBV_CLASS VCA_P18 ZEBRA) QCS_TEMP (IN=B KEEP=BARCODE SUBJID) CACO391; 
BY BARCODE; 
IF B=0; 

*ONE ID HAS DIFFERENT DXDATE IN THE FILE CARI PREPARED AND THE VERIFIED FILE; 
IF ID='24001048002011' THEN DXDATE=MDY(06,15,2002);

RUN; 

DATA VERIFY; 
SET VERIFY.FEB2013VERIFIED; 

	***CLEAN AND CONVERT DX DAY, MONTH, AND YEAR TO NUMERIC DATA**;
	IF DODDY IN ('99', 'XX') THEN DODDYN=15; ELSE DODDYN=INPUT(DODDY,2.);              
	IF DODMO='99' THEN DODMON=6; ELSE DODMON=INPUT(DODMO,2.);               
	IF DODMON IN (4,6,9,11) AND DODDYN > 30 THEN DODDYN=30;
	ELSE IF DODMON = 2 AND DODDYN > 28 THEN DODDYN=28; 
	 
	DODYRN=INPUT(DODYR,4.);  

	DATEVRF=MDY(DODMON,DODDYN,DODYRN); 
    
    DXDATE = DATEVRF; 

RUN; 

PROC SORT DATA=VERIFY; BY ID DXDATE; RUN; 
PROC SORT DATA=ALYDATA; BY ID DXDATE; RUN; 

DATA JB; * MERGED VERIFIED AND JB IMPORT FILE *;
MERGE ALYDATA(IN=ALY)
      VERIFY (IN = VER KEEP=DATASRC HISTOGY ID DXDATE); 

BY ID DXDATE; *MERGING BY DXDATE ENSURES THAT THE PEOPLE WHO HAVE MULTIPLE VERIFIED CANCER DIAGNOSES ARE MATCHED TO THE CORRECT DIAGNOSIS (THE NHL) SINCE THIS SHOULD MATCH THE DATE THAT CARI GAVE 
				IN THE CACO391 DATASET*;

IF ALY=1;
IF VER=1 THEN VERFY = 1; 

*THERE IS ONE PERSON WHO HAD 2 DIFFERENT CANCER DIAGNOSES ON THE SAME DAY - I AM REMOVING THE EXTRA OBSERVATION THAT WAS ADDED FROM THE VERIFIED FILE FOR THE NON-LYMPHOMA DIAGNOSIS*;
IF ID = '25056001006041' AND HISTOGY='8046' THEN DELETE; 

RUN; 


DATA COHORT;
MERGE JB (IN=C)

	  ORG.SURVEY99MEN (IN=M KEEP=ID CIGNOW99 BEER99 BEERLT99 WINERE99 WINEWH99 LIQUOR99)
	  ORG.SURVEY99WOMEN (KEEP=ID CIGNOW99 BEER99 BEERLT99 WINERE99 WINEWH99 LIQUOR99)

      DER.SURVEY92MENKEYVAR2 (KEEP=ID REGION92 SMK92)
	  DER.SURVEY92FEMKEYVAR2 (KEEP=ID REGION92 SMK92)

	  DER.SURVEY97MENKEYVAR2 (KEEP=ID SMK97)
	  DER.SURVEY97FEMKEYVAR2 (KEEP=ID SMK97)

	  DER.SURVEY99MENKEYVAR (KEEP=ID BMI99)
	  DER.SURVEY99FEMKEYVAR (KEEP=ID BMI99)

	  LLDRIV.LLWOMENDER_MASTER (IN=LIFELINK KEEP=ID DRAWDATE AGELL)
	  LLDRIV.LLMENDER_MASTER   (IN=LIFELINK KEEP=ID DRAWDATE AGELL)

      ORIG82.CPS2FEMORIGINAL   (KEEP=ID HT EDUCATE)
      ORIG82.CPS2MENORIGINAL   (KEEP=ID HT EDUCATE)

	  MAST.CPS2SMGMST14M	(KEEP=ID NUT_97 NUT_99)
	  MAST.CPS2SMGMST14F	(KEEP=ID NUT_97 NUT_99);

BY ID; 
IF C=1;

*DELETING PARTICIPANT WHO REVOKED AUTHORIZATION TO PARTICIPATE, UNTIL MASTER FILE IS UPDATED WITH THIS INFO (NEXT MASTER FILE IS 15M AND 15F)*;
	IF ID = '42341019003091' THEN DELETE;

*CREATING GENDER VARIABLE*;
IF M=1 THEN GENDER=1; * MEN *;
ELSE GENDER=0;      * WOMEN *;

*CREATING NUMERIC VARIABLES FOR MODELING*;
SEX=GENDER*1; 
FORMAT SEX SEX.;

*CREATING A NEW RACE VARIABLE: WHITE/BLACK/OTHER AND MISSING*;
IF RACE IN ('1','2') THEN RACENEW=1;
ELSE IF RACE IN ('3','4') THEN RACENEW=2;
ELSE RACENEW=3;

FORMAT RACENEW RACENEW.;

*RECLASSIFING CASES AND CONTROLS FOR MODELING*;
IF CACO=2 THEN CACONEW=0;
ELSE IF CACO=1 THEN CACONEW=1;
FORMAT CACONEW CACOF.;

*YEARS BETWEEN DIAGNOSIS AND BLOOD DRAW*;
YEARDS = ROUND(INTCK('DAY',SPECDATE,DXDATE1)/365.25, 0.01); *DXDATE1 IS ONLY PRESENT FOR CASES, IT IS MISSING FOR ALL CONTROLS*; 
LABEL YEARDS = 'YEARS BETWEEN DIAGNOSIS AND BLOOD DRAW'; 

*AGE AT DIAGNOSIS*;
AGEDX = INTCK('YEAR', BDAYDATE, DXDATE); 
LABEL AGEDX = 'AGE AT DIAGNOSIS'; 

*** BODY MASS INDEX;
*** WHO CUTPOINTS FOR UNDER/NORMAL, OVERWEIGHT, OBESE;
IF .<BMI99<25.0 THEN BMIWHO=0;
ELSE IF 25.0<=BMI99<30.0 THEN BMIWHO=1;
ELSE IF 30.0<=BMI99 THEN BMIWHO=2;
ELSE IF BMI99=. THEN BMIWHO=9;

LABEL BMIWHO='Body Mass Index (BMI) in 1999'; FORMAT BMIWHO BMIWHO.;

*CREATING EDUCATION VARIABLE*;
IF EDUCATE IN ('1','2') THEN EDUCA = 1;
ELSE IF EDUCATE IN ('3',' ') THEN EDUCA = 2;
ELSE IF EDUCATE IN ('4','5') THEN EDUCA = 3;
ELSE EDUCA = 4;

FORMAT REGION92 $REGIONF. EDUCA EDUCAF.; 

*ADDED BRIANS CODE FOR SMOKING STATUS FOR 1999 - 12/18/13*;

***********************************;
* 1992 SMOKING VARIABLES		  *;
***********************************;

IF SMK92 = '1' THEN STAT92='1';						 *NEVER*;
	ELSE IF SMK92 IN('2','4') THEN STAT92 = '2';	 *CURRENT*;
	ELSE IF SMK92 IN('3','5') THEN STAT92 = '3';	 *FORMER*;
	ELSE IF SMK92 ='6' THEN STAT92 = '4';			 *EVER*;
	ELSE STAT92 = '9';

*************************;
* SMOKING STATUS AT UPDATES;
*************************;

**********************************
*	1: NEVER SMOKER					*
*	2: CURRENT SMOKER					*
*	3: FORMER SMOKER					*
*	9: MISSING							*
**********************************;

***** STATUS 1997 *****;
IF SMK97 IN(' ','8','9') THEN STAT97 = '9';
	ELSE STAT97 = SMK97;

***** CLEAN 1997 SMOKING STATUS *****;
IF STAT97 = '1' THEN DO;
	IF STAT92 IN('2','3','4') THEN STAT97 = '3';
	ELSE IF STAT92 = '9' THEN STAT97 = '4';
	END;

***** STATUS 1999 *****;
IF CIGNOW99 = '2' THEN STAT99 = '2';	*CURRENT SMOKER*;
	ELSE IF CIGNOW99 IN('*',' ') OR NUT_99 = '0' THEN STAT99 ='9';		* MISSING *;
	ELSE IF CIGNOW99 = '1' THEN DO;
		IF STAT97 = '1'
			AND STAT92 = '1'
				THEN STAT99 = '1';	* ALWAYS NEVER SMOKERS *;
		ELSE IF STAT97 IN('2','3')
			OR STAT92 IN('2','3','4')
				THEN STAT99 = '3';	* EVER SMOKERS - NOT CURRENT *;
		ELSE STAT99 = '4';	* MISSING SMOKING INFO AT SOME POINT *;
		END;

*COMBINING MISSING AND MISSING SMOKING INFO AT SOME POINT - USE THIS VARIABLE FOR ANALYSIS - ADDED BY JB 12/18/13*;
IF STAT99 IN ('4','9') THEN SMK99='9';
ELSE SMK99=STAT99;

FORMAT SMK99 SMOKE.;

***ALCOHOL INTAKE - CONTINUOUS***;
		**1999**;
			*REGULAR BEER, LIGHT BEER, RED WINE, WHITE WINE & LIQUOR*;
				*--FREQUENCY = 0-9*;
			ARRAY T1  (5)  BEER99  BEERLT99  WINERE99  WINEWH99  LIQUOR99;
			ARRAY T2  (5)  BEER99X BEERLT99X WINERE99X WINEWH99X LIQUOR99X;
			DO I = 1 TO DIM(T1);
					 IF T1{I} = '0' THEN T2{I} = 0;    *NEVER;
				ELSE IF T1{I} = '1' THEN T2{I} = 0.02; *<1 SVG/MON;
				ELSE IF T1{I} = '2' THEN T2{I} = 0.07; *1-3 SVGS/MON;
				ELSE IF T1{I} = '3' THEN T2{I} = 0.14; *1 SVG/WK;
				ELSE IF T1{I} = '4' THEN T2{I} = 0.43; *2-4 SVGS/WK;
				ELSE IF T1{I} = '5' THEN T2{I} = 0.8;  *5-6 SVGS/WK;
				ELSE IF T1{I} = '6' THEN T2{I} = 1;    *1 SVG/D;
				ELSE IF T1{I} = '7' THEN T2{I} = 2.5;  *2-3 SVGS/D;
				ELSE IF T1{I} = '8' THEN T2{I} = 4.5;  *4-5 SVGS/D;
				ELSE IF T1{I} = '9' THEN T2{I} = 6;    *6+ SVGS/D;
				ELSE IF T1{I} IN (' ','.','*','I') THEN T2{I} = 0; *MISSING/INVALID CATEGORIZED AS NEVER;
			END;
			**1999 TOTAL ALCOHOL [SVGS/DAY]**;
				ALCOHOL99 = SUM(BEER99X,BEERLT99X,WINERE99X,WINEWH99X,LIQUOR99X);
				ALCOHOL99 = ROUND(ALCOHOL99,0.01); LABEL ALCOHOL99 = '1999 Total Alcohol (Drinks/D)';


***ALCOHOL INTAKE CATEGORIES***;
		*NOTE: CONDENSED CATEGORIES FROM SUE G.'S (NON-DRINKERS,<1 DRINK/D,1 DRINK/D,2+ DRINKS/D) TO THE FOLLOWING CATEGORIES
			   BECAUSE THERE WERE ONLY 5 CASES IN THE '1 DRINK/D' CATEGORY, SO COMBINED IT WITH '<1 DRINK/D' AS PER MARJI M.*;
		**1999**;
			 IF ALCOHOL99 = 0 	   THEN ALC99 = 1; *0 DRINKS/D, REF;
		ELSE IF 0 < ALCOHOL99 <= 1 THEN ALC99 = 2; *<=1 DRINK/D;
		ELSE IF 1 < ALCOHOL99 <= 6 THEN ALC99 = 3; *>1 DRINK/DAY;
								   ELSE ALC99 = 9; *MISSING/INVALID;
		LABEL ALC99 = '1999 Alcohol Intake (Drinks/D)'; FORMAT ALC99 ALC.;

*12/5/2012, Lauren's wants to run 'others' as a group outside of three NHL subtypes(cllsll,fol,dlbcl); 
IF NHLSUB NOT IN ('2: CLL\SLL', '3: DLBCL', '4: Follicular') AND CACONEW=1 THEN OTHERNHL=1; 
ELSE IF CACONEW=0 THEN OTHERNHL=0; 

***NHL SUBTYPES***;
*CLL/SLL*;
IF NHLSUB='2: CLL\SLL' AND CACONEW=1 THEN CLLSLL=1;
ELSE IF CACONEW=0 THEN CLLSLL=0;
ELSE CLLSLL=.;

*DLBCL*;
IF NHLSUB='3: DLBCL' AND CACONEW=1 THEN DLBCL=1;
ELSE IF CACONEW=0 THEN DLBCL=0;
ELSE DLBCL=.;

*FOLLICULAR*;
IF NHLSUB='4: Follicular' AND CACONEW=1 THEN FOL=1;
ELSE IF CACONEW=0 THEN FOL=0;
ELSE FOL=.;

*MULTIPLE MYELOMA*;
IF NHLSUB='5: Multiple Myeloma' AND CACONEW=1 THEN MM=1;
ELSE IF CACONEW=0 THEN MM=0;
ELSE MM=.;

*COMBINING 3 MAIN NHL SUBTYPES INTO 1 CATEGORY*;
IF NHLSUB IN ('2: CLL\SLL', '3: DLBCL', '4: Follicular') AND CACONEW=1 THEN MAINNHL=1;
ELSE IF CACONEW=0 THEN MAINNHL=0;
ELSE MAINNHL=.;

LABEL MAINNHL='3 Main NHL Subtypes';

*PUTTING SUBTYPES INTO 1 CATEGORY FOR POLYTOMOUS LOG REG - 8/2/2013*;

IF CACONEW=0 THEN SUB1CAT=0;
ELSE IF NHLSUB='3: DLBCL' AND CACONEW=1 THEN SUB1CAT=1;
ELSE IF NHLSUB='2: CLL\SLL' AND CACONEW=1 THEN SUB1CAT=2;
ELSE IF NHLSUB='4: Follicular' AND CACONEW=1 THEN SUB1CAT=3;
ELSE IF CACONEW=1 AND NHLSUB NOT IN ('2: CLL\SLL', '3: DLBCL', '4: Follicular') THEN SUB1CAT=4;

FORMAT SUB1CAT SUB1CAT.;

*CATEGORIZING ANTIBODY LEVELS AS SERONEGATIVE/SEROPOSITIVE*;

if .< ZEBRA <= 100 then ZEBRA_NP=0;
else if ZEBRA > 100 then ZEBRA_NP=1;

if .< EBNA_TRUNCATED <= 250 then EBNA_TRU_NP=0;
else if EBNA_TRUNCATED > 250 then EBNA_TRU_NP=1;

if .< EA_D <= 100 then EA_D_NP=0;
else if EA_D > 100 then EA_D_NP=1;

if .< VCA_p18 <= 100 then VCA_p18_NP=0;
else if VCA_p18 > 100 then VCA_p18_NP=1;

FORMAT VCA_p18_NP EA_D_NP EBNA_TRU_NP ZEBRA_NP NPF.;

*CREATING A BINARY VARIABLE FOR BLOOD DRAW TO DIAGNOSIS DATE WITH CUTPOINTS AT 3 YEARS - LT - 7/11/13*;

IF CACONEW=1 AND YEARDS LT 3 THEN DXIN3Y=1; 
ELSE IF CACONEW=1 AND YEARDS GE 3 THEN DXIN3Y=2;

*************************************************************;
*EXCLUSION CASCADE											*;
*************************************************************;
***MISSING LAB DATA***;
IF ZEBRA = . OR BARCODE = '073529-209' THEN EXCLUSION=1;

***HODGKIN LYMPHOMA****;
ELSE IF NHLSUB = '1: Hodgkin' THEN EXCLUSION=2;

***MULTIPLE MYELOMA****;
ELSE IF NHLSUB ='5: Multiple Myeloma' OR ('9731'<=HISTOGY<='9734') OR MATCH IN (45, 35, 221, 180, 121) THEN EXCLUSION=3;

ELSE EXCLUSION = .;

LABEL EXCLUSION = 'Reason for exclusion';
FORMAT EXCLUSION EXCLUSION.;

*CREATING QUANTILES FOR LEVELS OF SEROREACTIVITY TO ANTIGENS*; *ADDING SERONEGATIVE GROUP INTO LESS THAN 25% GROUP - PER LT - 8/2/2013*;
***ZEBRA***;
 
 IF ZEBRA GT . AND ZEBRA LT 1085.86 then ZEBRA75=1;
 ELSE IF ZEBRA GE 1085.86 AND ZEBRA LT 4882.36 then ZEBRA75=2;
 ELSE IF ZEBRA GE 4882.36 THEN ZEBRA75=3;

***EBNA***;
 IF EBNA_TRUNCATED GT . AND EBNA_TRUNCATED LT 8009.67 then EBNA_TRU75=1;
 ELSE IF EBNA_TRUNCATED GE 8009.67 AND EBNA_TRUNCATED LT 11469.17 then EBNA_TRU75=2;
 ELSE IF EBNA_TRUNCATED GE 11469.17 THEN EBNA_TRU75=3;

***EAD***;
 IF EA_D GT . AND EA_D LT 701.528 THEN EA_D75=1;
 ELSE IF EA_D GE 701.528 AND EA_D LT 4289.03 THEN EA_D75=2;
 ELSE IF EA_D GE 4289.03 THEN EA_D75=3;

 ***VCA***;
 IF VCA_p18 GT . AND VCA_p18 LT 8634.61 THEN VCA_p1875=1;
 ELSE IF VCA_p18 GE 8634.61 AND VCA_p18 LT 13961.11 THEN VCA_p1875=2;
 ELSE IF VCA_p18 GE 13961.11 THEN VCA_p1875=3;

 FORMAT ZEBRA75 EBNA_TRU75 EA_D75 VCA_p1875 QUARTS.;

*CATEGORIZING DRAWDATE INTO 2 GROUPS FOR UNCONDITIONAL POLYTOMOUS MODEL (MAY 31, 2000 AND BELOW, AND GREATER THAN MAY 31, 2000) - PER LT 8/5/2013*;
 IF DRAWDATE LE '31may2000'd THEN DRAWDATE_CAT=0;
 ELSE DRAWDATE_CAT=1;

*CATEGORIZING AGE AT BLOOD DRAW INTO 5 CATEGORIES FOR UNCONDITIONAL POLYTOMOUS MODEL (I DIVIDED INTO QUINTILES) - PER LT 8/5/2013*;

 IF AGELL GT . AND AGELL LE 64 THEN AGELL_CAT=1;
 ELSE IF AGELL GE 65 AND AGELL LT 69 THEN AGELL_CAT=2;
 ELSE IF AGELL GE 69 AND AGELL LT 72 THEN AGELL_CAT=3; 
 ELSE IF AGELL GE 72 AND AGELL LT 75 THEN AGELL_CAT=4;
 ELSE AGELL_CAT=5;

*CREATING TIME VARIABLE FOR SPLINES*;
IF CACONEW=1 THEN TIME=1; 
ELSE IF CACONEW=0 THEN TIME=2; 

RUN;

/*
*THE CUTPOINTS FOR THE ANTIGEN QUANTILES ARE FROM THE CODE BELOW*;
***ZEBRA***;
PROC UNIVARIATE DATA=EBV.FINALCOHORT;
VAR ZEBRA;
WHERE ZEBRA_NP=1 AND CACONEW=0 AND EXCLUSION=.; *SEROPOSITIVE FOR ZEBRA (NOT NECESSARILY SEROPOSITIVE OVERALL, E.G. NOT CONSIDERING EBV_CLASS) CONTROLS*;
OUTPUT OUT=SUBTCAT_ZEB PCTLPTS= 25 75 PCTLPRE=PCT;
RUN;

***EBNA***;
PROC UNIVARIATE DATA=EBV.FINALCOHORT;
VAR EBNA_TRUNCATED;
WHERE EBNA_TRU_NP=1 AND CACONEW=0 AND EXCLUSION=.; *SEROPOSITIVE FOR EBNA (NOT NECESSARILY SEROPOSITIVE OVERALL, E.G. NOT CONSIDERING EBV_CLASS) CONTROLS*;
OUTPUT OUT=SUBTCAT_EBNA PCTLPTS= 25 75 PCTLPRE=PCT;
RUN;

***EAD***;
PROC UNIVARIATE DATA=EBV.FINALCOHORT;
VAR EA_D;
WHERE EA_D_NP=1 AND CACONEW=0 AND EXCLUSION=.; *SEROPOSITIVE FOR EBNA (NOT NECESSARILY SEROPOSITIVE OVERALL, E.G. NOT CONSIDERING EBV_CLASS) CONTROLS*;
OUTPUT OUT=SUBTCAT_EAD PCTLPTS= 25 75 PCTLPRE=PCT;
RUN;

***VCA***;
PROC UNIVARIATE DATA=EBV.FINALCOHORT;
VAR VCA_p18;
WHERE VCA_p18_NP=1 AND CACONEW=0 AND EXCLUSION=.; *SEROPOSITIVE FOR EBNA (NOT NECESSARILY SEROPOSITIVE OVERALL, E.G. NOT CONSIDERING EBV_CLASS) CONTROLS*;
OUTPUT OUT=SUBTCAT_VCA PCTLPTS= 25 75 PCTLPRE=PCT;
RUN;

*QUINTILE CUTPOINTS FOR AGE AT BLOODDRAW VARIABLE*;
PROC UNIVARIATE DATA=EBV.FINALCOHORT;
VAR AGELL;
OUTPUT OUT=SUBTCAT_AGE PCTLPTS= 20 40 60 80 PCTLPRE=PCT;
RUN;

*OUTPUTTING CUTPOINTS FOR ANTIGENS AND BLOODDRAW VARIABLE*;
ODS RTF FILE="D:\USER\JBLASE\EBV Program Review\NEW OUTPUT\CUTPOINTS.RTF";
PROC PRINT DATA = SUBTCAT_ZEB;RUN;
PROC PRINT DATA=SUBTCAT_EBNA; RUN;
PROC PRINT DATA=SUBTCAT_EAD; RUN;
PROC PRINT DATA=SUBTCAT_VCA; RUN;
PROC PRINT DATA = SUBTCAT_AGE;RUN;
ODS RTF CLOSE;
*/
*A TOTAL OF 3 CONTROLS BECAME CASES BUT 1 CASE WAS ELIMINATED BECAUSE IT WAS A MULTIPLE MYELOMA CASE*;
*CREATED CONTROL TABLE FOR DESCRIPTIVE TABLES*;
*THERE ARE 2 CONTROLS WHO BECAME CASES (AND ARE HENCE DUPLICATES)IN THIS DATASET, KEEP BOTH IN BECAUSE WE ARE DOING INCIDENCE DENSITY MATCHING AND 
PEOPLE ARE ANALYZED ACCORDING TO WHEN THEY WERE DIAGNOSED*;

DATA EBV.FINALCOHORT (DROP=CIGNOW99 BEER99 BEERLT99 WINERE99 WINEWH99 LIQUOR99 BEER99X BEERLT99X WINERE99X WINEWH99X LIQUOR99X I SMK92 SMK97 NUT_97 NUT_99 BMI99 STAT92 STAT97 STAT99); 
SET COHORT;

IF EXCLUSION = .;

RUN;

