**********************************************************************************;
*PROGRAM: S:\USER\JBLASE\Hema Malignancies & Parents Age\PROGRAM REVIEW\PROGRAMS\Heme and Parents Age Cohort.SAS  *;
*PURPOSE: Creates the analytic cohort for the analysis of Parents age at birth and NHL*;
*         Code based off analyses of NHL and Smoking HEMACOHORT4.sas from Ryan Diver            *;
*         Including NHL Deaths in this analysis                                  *;
*CREATED: JUNE 14, 2013 BY JENNY BLASE                                            *;
*MODIFIED: 11/11/13 - ADDED RYAN'S DEATH CODES FOR MYELOID LEUKEMIAS AND HEME NOS*;
**********************************************************************************;
OPTIONS MPRINT;
 
/*TITLE 'Hematologic Malignancies and Parents Age at Birth';
/*FOOTNOTE 'Program: S:\USER\JBLASE\Hema Malignancies & Parents Age\PROGRAM REVIEW\Heme and Parents Age Cohort.SAS'; */

LIBNAME PAGE 'D:\USER\JBLASE\Hema Malignancies & Parents Age\DATA';
LIBNAME MAST 'D:\CPS\MASTER';
LIBNAME MORT 'D:\CPS\MORTALITY';
LIBNAME VERF 'D:\CPS\NUTRITION\VERIFIED';
LIBNAME REPT 'D:\CPS\NUTRITION\REPORTED';
LIBNAME CPS2ORIG 'D:\CPS\CPS2\ORIGINAL'; 
LIBNAME CPS2DERV 'D:\CPS\CPS2\DERIVED';
LIBNAME NUTORIG 'D:\CPS\NUTRITION\ORIGINAL';
LIBNAME NUTDERV 'D:\CPS\NUTRITION\DERIVED';
LIBNAME PYEAR 'D:\CPS\NUTRITION\PYEAR';
LIBNAME LIBRARY 'D:\USER\JBLASE'; 


PROC FORMAT LIBRARY=LIBRARY;
VALUE HEMGRPS 0='0: Non-Case'
			  1='1: DLBCL'
			  2='2: CLL/SLL'
			  3='3: Follicular'
			  4='4: Multiple myeloma'
			  5='5: Other B'
			  6='6: T cell'
			  7='7: Other lymphoid'
			  8='8: Myeloid Leukemias'
			  9='9: Hodgkins lymphoma'
			  10='10: Lymphoma NOS - Unk lymphoid/myeloid'
			  11='11: Leukemia NOS'
              ;

VALUE NHL 0='Non-case'
		  1= 'Case'
		  ;
		  
VALUE SMOKEV 1='1: No'
             2='2: Yes'
			 9='9: Missing'
			 ;

VALUE CENSREAS 1='1: Last Survey Returned'
               2='2: Reported Hema Cancer, Not Verified'
			   3='3: Reported Other Cancer'
			   ;
VALUE ASCERTAIN 1='1: Self-Reported NHL, Verified'
                2='2: NHL Death, Verified'
				3='3: NHL Death, Not Verified'
				4='4: Self-Reported Other Cancer, Verified NHL'
				5='5: Other Cancer Death, Verified NHL'
				6='6: Case Dx in F-U, but Found Outside F-U,NON-CASE'
				;
VALUE EXCLUSN 1='1: Lost to Follow-up'
              2='2: Prevalent Cancer'
			  3='3: Missing Parents Age at Birth'
			  4='4: Reported NHL, Not Verified'
			  5= '5: Dx Date > 6 Months After Report of Cancer'
			  6= '6: Family history of hematologic malignancy'
			  99='99: Final Cohort'
			  ;
VALUE GENDER 0='0: Male'
             1='1: Female'
			 ;
VALUE RACENEW 1='1: White, Non-Hispanic'
              2='2: Non-White'
			  9='9: Missing'
			  ;
VALUE FAMHX 0='0: No History'
			1='1: Family History'
			;

VALUE BMI 1='1: <18.5'
          2='2: 18.5-<25'
		  3='3: 25-<30'
		  4='4: 30+'
		  9='9: Missing'
		  ;

VALUE ALC_BLK 1='1: Non-drinker'
          	  2='2: < 1/day'
		      3='3: 1-2/day'
		      4='4: >2/day'
		      9='9: Missing'
			  ;
VALUE NOYES 1='1: No'
            2='2: Yes'
			9='9: Missing'
			;
VALUE EDUCAF 
            1 = '1: Less than high school'
			2 = '2: High school'
			3 = '3: Some college/vocational school'
			4 = '4: College/graduate school'
			9 = '9: Missing'
			;
VALUE HT_Q 1='1: Q1'
           2='2: Q2'
		   3='3: Q3'
		   4='4: Q4'
		   5='5: Q5'
		   ;

VALUE NEVEV 1='1: Never'
            2='2: Ever'
			9='9: Missing'
			;

VALUE PARITY 1='1: 0'
             2='2: 1'
			 3='3: 2'
			 4='4: 3'
			 5='5: 4+'
			 9='9: Missing'
			 ;

VALUE SIT 1='1: <3'
          2='2: 3-5'
		  3='3: 6+'
		  9='9: Missing'
		  ;

VALUE SIBS 0='0: Only child'
		   1='1: One or more siblings'
		   ;

VALUE PAGEBTH_5CAT 1='1: <25 YEARS'
				   2='2: 25-29 YEARS' 
				   3='3: 30-34 YEARS'
		 		   4='4: 35-39 YEARS' 
				   5='5: 40 AND OVER'
				   9='9: Missing';
 
VALUE PAGEBTH_4CAT 1='1: <25 YEARS'
				   2='2: 25-29 YEARS' 
				   3='3: 30-34 YEARS'
		 		   4='4: 35+ YEARS' 
				   9='9: MISSING'
				   ; 

VALUE SEX_NEW 0= '0: Males'
		  1= '1: Females'
		  ;

VALUE MFINT 0='0: MOTHERS AND FATHERS < 21-REF'
			1='1: MOTHERS BELOW 21, FATHERS 21-24'
			2='2: MOTHERS BELOW 21, FATHERS 25-30'
			3='3: MOTHERS BELOW 21, FATHERS 31+'
			4='4: MOTHERS 21-24, FATHERS <21'
			5='5: MOTHERS 21-24, FATHERS 21-24'
			6='6: MOTHERS 21-24, FATHERS 25-29'
			7='7: MOTHERS 21-24, FATHERS 30-34'
			8='8: MOTHERS 21-24, FATHERS 35+'
			9='9: MOTHERS 25 AND OLDER, FATHERS <25'
			10='10: MOTHERS 25-29, FATHERS 25-29'
			11='11: MOTHERS 25-29, FATHERS 30-35'
			12='12: MOTHERS 25-29, FATHERS 36+'
			13='13: MOTHERS >30, FATHERS 25-29'
			14='14: MOTHERS 30-35, FATHERS 30-35'
			15='15: MOTHERS 30-35, FATHERS 36+'
			16='16: MOTHERS 36+, FATHERS 30-35'
			17='17: MOTHERS 36+, FATHERS 36-40'
			18='18: MOTHERS 36+, FATHERS 41+';

RUN;


***PULL IN THE REPORTED LEUKEMIA/LYMPHOMAS AND REPORTS OF OTHER CANCERS****;
%MACRO GETREPORTED(SURVYR=,FILEDT=);
	DATA REP&SURVYR(KEEP=ID INCCAN&SURVYR REPDXYR&SURVYR) REPOTH&SURVYR (KEEP=ID OTHCAN&SURVYR);
	SET REPT.CANCER&SURVYR.&FILEDT.REPORTED;
	IF INCCAN&SURVYR IN('C42','C77','800','810','820','830','840') THEN OUTPUT REP&SURVYR;
	ELSE DO;
	   OTHCAN&SURVYR.=INCCAN&SURVYR;
	   OUTPUT REPOTH&SURVYR;
	END;
	RUN;

	PROC SQL;
	TITLE2 "DUPLICATE REPORTED HEMA CANCERS IN THE &SURVYR REPORTED FILE";
	SELECT *, COUNT(INCCAN&SURVYR) AS NUMREP
	FROM REP&SURVYR
	GROUP BY ID
	HAVING CALCULATED NUMREP>=2;

	PROC SORT DATA=REP&SURVYR NODUPKEY; BY ID;RUN;
	PROC SORT DATA=REPOTH&SURVYR NODUPKEY; BY ID; RUN;
	TITLE2;
%MEND;

%GETREPORTED(SURVYR=97,FILEDT=SEP2007)
%GETREPORTED(SURVYR=99,FILEDT=SEP2007)
%GETREPORTED(SURVYR=01,FILEDT=MAR2011)
%GETREPORTED(SURVYR=03,FILEDT=MAR2011)
%GETREPORTED(SURVYR=05,FILEDT=MAR2011)
%GETREPORTED(SURVYR=07,FILEDT=MAR2011)
%GETREPORTED(SURVYR=09,FILEDT=FEB2013)
%GETREPORTED(SURVYR=11,FILEDT=FEB2013)

***********************************************;
*   PULL IN THE VERIFIED LEUKEMIA/LYMPHOMAS   *;
***********************************************;
%INCLUDE 'D:\USER\JBLASE\MACROS\Leukemia Groups.SAS';	

**RECODE ALL CANCERS IN VERIFIED AS HEMA OR NOT BASED ON INTERLYMPH PAPER GROUPS***; 
%INTLYMPH(FILE=VERF.FEB2013VERIFIED) 

**DROP NON-HEMA CANCERS**;
DATA HEMA;
SET HEMA_LUK;

IF ILGRP6 NE 99;
RUN;

**LOOK AT DUPLICATES***;
PROC SQL;
TITLE2 "DUPLICATE HEMA CANCERS IN THE VERIFIED FILE";
SELECT ID, DATASRC, INCCANV, SITEC, HISTOGY, ILGRP6 FORMAT=8.,DODYR, DODMO, DODDY, COUNT(ID) AS NUMVER
FROM HEMA
GROUP BY ID
HAVING CALCULATED NUMVER>=2;

**CLEAN OUT SOME DUPLICATES AND CALCULATE DXDATE***; 
DATA VER;
SET HEMA;

***THESE ARE DUPLICATE CANCERS THAT WILL BE REMOVED FROM THE VERIFIED FILE BY PETER IN THE NEXT VERSION****;
***OTHER DUPLICATES HAVE BEEN REVIEWED PREVIOUSLY AND ARE CONFIRMED TO BE: **;
****1: DIFFERENT CANCERS ****;
****2: DUPLICATE REGISTRY CANCERS THAT PETER DOES NOT CHANGE, BUT WILL BE SELECTED BASED ON DX DATE**;
IF ID='25010071219102' AND DATASRC='R' THEN DELETE;

***DROP HODGKINS IF HODGKINS AND NHL ON THE SAME EXACT DATE (KEEP NHL, FOR NHL ANALYSIS)***;
***MAY CHANGE IF INCLUDING HODGKIN****;
IF ID='29101005007091' AND INCCANV='C77' AND HISTOGY='9663' THEN DELETE; **HODGKINS - NS**; 
IF ID='36038138060031' AND INCCANV='C77' AND HISTOGY='9663' THEN DELETE; **HODGKINS - NS**;

****CLEANING AND CONVERTING DX DAY,MONTH, YEAR TO NUMERIC****;        
IF DODDY='99' THEN DODDYN=15; ELSE DODDYN= INPUT(DODDY,2.);          
IF DODMO='99' THEN DODMON=6; ELSE DODMON= INPUT(DODMO,2.);           
IF DODMON IN (4,6,9,11) AND DODDYN > 30 THEN DODDYN=30;              
IF DODMON =2 AND DODDYN > 28 THEN DODDYN=28;                         
DODYRN= INPUT(DODYR,4.);                                             
                                                                      
*****DATE OF DIAGNOSIS*****;                                          
DXDATE=MDY(DODMON,DODDYN,DODYRN);      

RUN;

***DROP OTHER DUPLICATES BASED ON DX DATE ORDER***;
PROC SORT DATA=VER; BY ID DXDATE; RUN;

DATA VERF; SET VER; BY ID DXDATE; IF FIRST.ID; RUN;

*************************************************************;
*CREATE ANALYTIC COHORT                                     *;
*************************************************************;

DATA PAGE.INITIALCOHORT;
MERGE VERF (IN=INVER)
	  REP97 REP99 REP01 REP03 REP05	REP07 REP09 REP11
	  REPOTH97 REPOTH99 REPOTH01 REPOTH03 REPOTH05 REPOTH07 REPOTH09 REPOTH11
	  MAST.CPS2SMGMST14M (IN=MASTM KEEP=ID NUT_: RACE SPOUSE_C AGE92M)
	  MAST.CPS2SMGMST14F (IN=MASTF KEEP=ID NUT_: RACE SPOUSE_C AGE92M)
	  MORT.SMGMRT14d121220 (IN=INMORT)
	  PYEAR.NUT92MENPYR1210 (KEEP=ID FAIL DATEDD BDAYDATE)
	  PYEAR.NUT92FEMPYR1210 (KEEP=ID FAIL DATEDD BDAYDATE)
	  CPS2ORIG.CPS2MENORIGINAL(KEEP=ID AGE_INT FSITE MSITE S1SITE S2SITE S3SITE S4SITE S5SITE S6SITE
							        EDUCATE HT PSMKHM PSMKWK PSMKOTH MARITAL AGEFM NTIMSM ASPIRINB DBT MAGEBTH FAGEBTH)
	  CPS2ORIG.CPS2FEMORIGINAL(KEEP=ID AGE_INT FSITE MSITE S1SITE S2SITE S3SITE S4SITE S5SITE S6SITE
									EDUCATE HT PSMKHM PSMKWK PSMKOTH MARITAL AGEFM NTIMSM ASPIRINB DBT MAGEBTH FAGEBTH)
      CPS2DERV.CPS2MENKEYVAR3 (KEEP=ID DTINT82 CA82 ALCUSE82 SMK82 TOTBRO TOTSIS)
	  CPS2DERV.CPS2FEMKEYVAR3 (KEEP=ID DTINT82 CA82 ALCUSE82 SMK82 PREGLIVE PRGAGE1B TOTBRO TOTSIS)
	  NUTORIG.SURVEY92MEN (KEEP=ID WEIGHT92 BEER92F WINE92F LIQ92F BEER92S WINE92S LIQ92S
								SMKEV92 SMKYR92 SMKNO92 SMKHOM92 SMKEWH92 MS92 DIABET92 SIT92 ASPDAY92 IBUDAY92 OTHDAY92 ACEDAY92 ASPPIL92 IBUPIL92 OTHPIL92 ACEPIL92 ASPNEV92 IBUNEV92 OTHNEV92 ACENEV92
								ASPYRS92 IBUYRS92 OTHYRS92 ACEYRS92)
	  NUTORIG.SURVEY92WOMEN (KEEP=ID WEIGHT92 BEER92F WINE92F LIQ92F BEER92S WINE92S LIQ92S
								  SMKEV92 SMKYR92 SMKNO92 SMKHOM92 SMKEWH92 MS92 DIABET92 SIT92 ASPDAY92 IBUDAY92 OTHDAY92 ACEDAY92 ASPPIL92 IBUPIL92 OTHPIL92 ACEPIL92 ASPNEV92 IBUNEV92 OTHNEV92 ACENEV92
									ASPYRS92 IBUYRS92 OTHYRS92 ACEYRS92)
	  NUTDERV.SURVEY92MENKEYVAR2 (KEEP=ID DTINT92 CA92 BMI92 SMK92 ASPUSE92 NSDUSE92 IBUUSE92 ONDUSE92 ALC92 INSUSE92)
	  NUTDERV.SURVEY92FEMKEYVAR2 (KEEP=ID DTINT92 CA92 BMI92 SMK92 ASPUSE92 NSDUSE92 IBUUSE92 ONDUSE92 ALC92 
								       MENOP92M INSUSE92)
	  NUTORIG.SURVEY97MEN (KEEP=ID DTINT97)
  	  NUTORIG.SURVEY97WOMEN (KEEP=ID DTINT97)
	  NUTORIG.SURVEY99MEN (KEEP=ID DTINT99 CIGNOW99 CIGNUM99)
	  NUTORIG.SURVEY99WOMEN (KEEP=ID DTINT99 CIGNOW99 CIGNUM99)
	  NUTORIG.SURVEY01MEN (KEEP=ID DTINT01 SURVEY01 CIGNOW01 CIGNUM01)
	  NUTORIG.SURVEY01WOMEN (KEEP=ID DTINT01 SURVEY01 CIGNOW01 CIGNUM01)
	  NUTORIG.SURVEY03MEN (KEEP=ID DTINT03 CIGNOW03 CIGNUM03)
	  NUTORIG.SURVEY03WOMEN (KEEP=ID DTINT03 CIGNOW03 CIGNUM03)
	  NUTORIG.SURVEY05MEN (KEEP=ID DTINT05 CIGNOW05 CIGNUM05)
	  NUTORIG.SURVEY05WOMEN (KEEP=ID DTINT05 CIGNOW05 CIGNUM05)
	  NUTORIG.SURVEY07MEN (KEEP=ID DTINT07 CIGNOW07 CIGNUM07)
	  NUTORIG.SURVEY07WOMEN (KEEP=ID DTINT07 CIGNOW07 CIGNUM07)
	  NUTORIG.SURVEY09MEN (KEEP=ID DTINT09)
	  NUTORIG.SURVEY09WOMEN (KEEP=ID DTINT09)
	  ;
BY ID;
 
IF (MASTM OR MASTF) AND NUT_92=1;

*DELETING PARTICIPANTS WHO REVOKED AUTHORIZATION TO PARTICIPATE, UNTIL MASTER FILE IS UPDATED WITH THIS INFO (NEXT MASTER FILE IS 15M AND 15F)*;
	IF ID IN ('42341019003091','55096001012162','36041001003092','53036026007041') THEN DELETE;

***************************************************************;
*DEFINE PARENTS' AGE AT BIRTH                                 *;
*WILL BE USED FOR EXCLUSIONS								  *;    
***************************************************************;
* 4 CATEGORIES *;
	***1=<24 YEARS, 2=25-29 YEARS, 3=30-34 YEARS, 4=35 AND OVER***;

IF FAGEBTH GT . AND FAGEBTH LE 24 THEN FAGEBTH_4CAT=1; *REFERENCE GROUP*;
ELSE IF FAGEBTH GE 25 AND FAGEBTH LE 29 THEN FAGEBTH_4CAT=2;
ELSE IF FAGEBTH GE 30 AND FAGEBTH LE 34 THEN FAGEBTH_4CAT=3;
ELSE IF FAGEBTH GE 35 THEN FAGEBTH_4CAT=4;
ELSE FAGEBTH_4CAT=.;

IF MAGEBTH GT . AND MAGEBTH LE 24 THEN MAGEBTH_4CAT=1; *REFERENCE GROUP*;
ELSE IF MAGEBTH GE 25 AND MAGEBTH LE 29 THEN MAGEBTH_4CAT=2;
ELSE IF MAGEBTH GE 30 AND MAGEBTH LE 34 THEN MAGEBTH_4CAT=3;
ELSE IF MAGEBTH GE 35 THEN MAGEBTH_4CAT=4;
ELSE MAGEBTH_4CAT=.;

FORMAT FAGEBTH_4CAT MAGEBTH_4CAT PAGEBTH_4CAT.;
LABEL MAGEBTH_4CAT='4 Category Mothers Age at Birth';
LABEL FAGEBTH_4CAT='4 Category Fathers Age at Birth';

*CONTINOUS MOTHERS AND FATHERS AGE AT BIRTH PER 5 YEARS*;
FAGEBTH_5YR=FAGEBTH/5;
MAGEBTH_5YR=MAGEBTH/5;

LABEL FAGEBTH_5YR="CONTINOUS FATHERS AGE AT BIRTH PER 5 YEARS";
LABEL MAGEBTH_5YR="CONTINOUS MOTHERS AGE AT BIRTH PER 5 YEARS";

*************************************;
*       DEATH INFORMATION           *;
*************************************;

********DEFINING DEATHS AND DEATH DATE*********;
DEAD=0;
IF INMORT AND DATEDD<=MDY(6,30,2009) THEN DO;
	DEAD=1;
	DEATHDT=DATEDD;
END;

*****RECODING PEOPLE WHO HAVE DEATH DATE BEFORE INTERVIEW DATE******;
*****2009-2010 NDI LINKAGE IS NOT FINAL, THEREFORE ASSUME THEY WILL BE RESURRECTED***;
IF DEAD=1 AND (NUT_09=1 AND DEATHDT<DTINT09) THEN DEAD=0;
IF DEAD=0 THEN DEATHDT=.;

***DEFINE CANCER PRIMARY CAUSE DEATH;
ANYCADTH=0;	
IF DEAD=1 THEN DO;
	IF (CODETYPE='2' AND ('140 '<=CODE1<='2099' OR '230 '<=CODE1<='2399')) OR
	   (CODETYPE='3' AND ('C00 '<=CODE1<='D099' OR 'D37 '<=CODE1<='D499')) THEN ANYCADTH=1; 
END;
LABEL ANYCADTH='CANCER DEATH';

*****************************************************;
* CLEAN UP SURVEY AND DX DATES BEFORE CENSORING     *;
*****************************************************;

***DIAGNOSIS DATES AFTER DEATH****; 
***INCORRECT DX DATE FOR 1 VERFIED CASE, NEW DATE PER PETER**;
*THERE ARE 2 PEOPLE WHO ARE CASES AND HAVE A DXDATE GREATER THAN DEATH DATE BUT BOTH ARE CENSORED - JB 8/22/2013*;
IF ID='08001003001062' THEN DXDATE=MDY(2,1,2003); 
***CHANGE DX DATES TO 1 DAY PRIOR TO DEATH, DATES ARE WRONG BECAUSE OF ASSUMPTIONS****; 
***WHEN MISSING PART OF A DATE VALUE (IE. UNKNOWN DAY OF DIAGNOSIS)***;
IF ID IN('16007012005072','22125004001051','24001020008012',
		 '24001048002011','55047001008091','59020002026022','59065002002092')
		 THEN DXDATE=DEATHDT-1;


*****CLEAN UP OF DIAGNOSIS DATE AFTER INTERVIEW DATE FOR A CANCER REPORT 1997*****;
DTINT97N=DTINT97;
DXDATEN=DXDATE;
IF INCCAN97 NE '' AND DXDATE>DTINT97>. THEN DO;
**WOMEN WHO HAVE AN INTERVIEW DATE OF 1/1/98 (AN ARBITRARY DATE GIVEN TO THOSE PEOPLE FOR WHOM
WE DO NOT HAVE A DATE) WE RECODE THEIR INTERVIEW DATE TO 1 DAY AFTER THEIR
DIAGNOSIS DATE**;
	IF DTINT97=MDY(1,1,1998) THEN DTINT97N=DXDATE+1;
**WOMEN WHO HAVE INTERVIEW DATE AND DIAGNOSIS DATE CLOSE BUT DIAGNOSIS DATE IS AFTER
INTERVIEW DATE, RECODE DIAGNOSIS DATE TO 1 DAY BEFORE INTERVIEW DATE**;
	ELSE IF (DXDATE-DTINT97)<180 THEN DXDATEN=DTINT97-1;
	ELSE DXDATEN=.B;
END;

*****CLEAN UP OF DIAGNOSIS DATE AFTER INTERVIEW DATE FOR A CANCER REPORT 1999*****;
**WOMEN WHO HAVE INTERVIEW DATE AND DIAGNOSIS DATE CLOSE BUT DIAGNOSIS DATE IS AFTER
INTERVIEW DATE, RECODE DIAGNOSIS DATE TO 1 DAY BEFORE INTERVIEW DATE**;
IF INCCAN99 NE '' AND DXDATE>DTINT99 THEN DO;
	IF (DXDATE-DTINT99)<180 THEN DXDATEN=DTINT99-1;
	ELSE DXDATEN=.B;
END;

*****CLEAN UP OF DIAGNOSIS DATE AFTER INTERVIEW DATE FOR A CANCER REPORT 2001*****;
**WOMEN WHO HAVE INTERVIEW DATE AND DIAGNOSIS DATE CLOSE BUT DIAGNOSIS DATE IS AFTER
INTERVIEW DATE, RECODE DIAGNOSIS DATE TO 1 DAY BEFORE INTERVIEW DATE**;
IF INCCAN01 NE '' AND DXDATE>DTINT01>. THEN DO;
	IF (DXDATE-DTINT01)<180 THEN DXDATEN=DTINT01-1;
	ELSE DXDATEN=.B;
END;

*****CLEAN UP OF DIAGNOSIS DATE AFTER INTERVIEW DATE FOR A CANCER REPORT 2003*****;
**WOMEN WHO HAVE INTERVIEW DATE AND DIAGNOSIS DATE CLOSE BUT DIAGNOSIS DATE IS AFTER
INTERVIEW DATE, RECODE DIAGNOSIS DATE TO 1 DAY BEFORE INTERVIEW DATE**;
IF INCCAN03 NE '' AND DXDATE>DTINT03>. THEN DO;
	IF (DXDATE-DTINT03)<180 THEN DXDATEN=DTINT03-1;
	ELSE DXDATEN=.B;
END;

*****CLEAN UP OF DIAGNOSIS DATE AFTER INTERVIEW DATE FOR A CANCER REPORT 2005*****;
**WOMEN WHO HAVE INTERVIEW DATE AND DIAGNOSIS DATE CLOSE BUT DIAGNOSIS DATE IS AFTER
INTERVIEW DATE, RECODE DIAGNOSIS DATE TO 1 DAY BEFORE INTERVIEW DATE**;
IF INCCAN05 NE '' AND DXDATE>DTINT05>. THEN DO;
	IF (DXDATE-DTINT05)<180 THEN DXDATEN=DTINT05-1;
	ELSE DXDATEN=.B;
END;

*****CLEAN UP OF DIAGNOSIS DATE AFTER INTERVIEW DATE FOR A CANCER REPORT 2007*****;
**WOMEN WHO HAVE INTERVIEW DATE AND DIAGNOSIS DATE CLOSE BUT DIAGNOSIS DATE IS AFTER
INTERVIEW DATE, RECODE DIAGNOSIS DATE TO 1 DAY BEFORE INTERVIEW DATE**;
IF INCCAN07 NE '' AND DXDATE>DTINT07>. THEN DO;
	IF (DXDATE-DTINT07)<180 THEN DXDATEN=DTINT07-1;
	ELSE DXDATEN=.B;
END;

****************************************************************;
*COVARIATE INFORMATION                                         *;
****************************************************************;
***AGE****; 
***USE RAW AGE92M VARIABLE***;
LABEL AGE92M='Age in 1992';

*MAKING AGE AT COHORT ENTRY CATEGORICAL VARIABLE - TERTILES*;
IF AGE92M=. THEN AGE92_ENT=.;
ELSE IF AGE92M LE 60 THEN AGE92_ENT=0;
ELSE IF AGE92M GE 61 AND AGE92M LE 66 THEN AGE92_ENT=1;
ELSE AGE92_ENT=2;

***GENDER***;
IF MASTM THEN GENDER=1;
ELSE IF MASTF THEN GENDER=2;
LABEL GENDER='Gender';
FORMAT GENDER GENDER.;

*MAKING SEX (0/1) VARIABLE FOR MODELS*;
IF GENDER=1 THEN SEX=0; *MEN*;
ELSE IF GENDER=2 THEN SEX=1; *WOMEN*;

*CONVERTING SEX TO A NUMERIC VARIABLE*;
SEX_NEW=INPUT(SEX,1.);

***RACE***;
**COLLAPSED RACE TO WHITE, NON-HISPANIC/NON-WHITE***;
IF RACE IN ('1') THEN RACENEW=1;
ELSE IF RACE IN ('2','3','4','5','6','7') THEN RACENEW=2;
ELSE IF RACE=' ' THEN RACENEW=9;
LABEL RACENEW='Race';
FORMAT RACENEW RACENEW.;

*CREATING DUMMY VARIBALE FOR RACENEW FOR SPLINE*; *JENNY - ERASE IF UNNECESSARY*;
RACE1=0; RACE2=0; RACE3=0;
IF RACENEW=1 THEN RACE1=1;
ELSE IF RACENEW=2 THEN RACE2=1;
ELSE IF RACENEW=9 THEN RACE3=1;

***EDUCATION (PROXY FOR SES)***;
*COLLAPSED EDUCATION INTO 4 CATEGORIES*;
IF EDUCATE IN ('1','2') THEN EDUCA = 1;
ELSE IF EDUCATE IN ('3') THEN EDUCA = 2;
ELSE IF EDUCATE IN ('4','5') THEN EDUCA = 3;
ELSE IF EDUCATE IN ('6','7') THEN EDUCA = 4;
ELSE IF EDUCATE=' ' THEN EDUCA=9;
LABEL EDUCA = 'Education Level';
FORMAT EDUCA EDUCAF.;

*CREATING A DUMMY VARIABLE FOR EDUCA FOR SPLINES*;
EDUCA1=0; EDUCA2=0; EDUCA3=0; EDUCA4=0; EDUCA5=0;

IF EDUCA=1 THEN EDUCA1=1;
ELSE IF EDUCA=2 THEN EDUCA2=1;
ELSE IF EDUCA=3 THEN EDUCA3=1;
ELSE IF EDUCA=4 THEN EDUCA4=1;
ELSE IF EDUCA=9 THEN EDUCA5=1;

***BMI (WHO)****;
IF .<BMI92<18.5 THEN BMI=1;
ELSE IF 18.5<=BMI92<25 THEN BMI=2;
ELSE IF 25<=BMI92<30 THEN BMI=3;
ELSE IF 30<=BMI92 THEN BMI=4;
ELSE IF BMI92=. THEN BMI=9;

LABEL BMI='Body Mass Index (BMI)'; FORMAT BMI BMI.;
LABEL BMI92='BMI in 1992';

***NEW ALCOHOL VARIABLES USING BLOCK FFQ DATA*****;
ARRAY ALCBEV(3) $ BEER92F WINE92F LIQ92F;
ARRAY ALCSRV(3) $ BEER92S WINE92S LIQ92S;
ARRAY ALCPD (3) BEERD WINED LIQD;
ARRAY ALCGM (3) BEERG WINEG LIQG;
**MATRIX OF CONVERSIONS BY AGE, GENDER, BEVERAGE, AND SERVING SIZE (S,M,L)**;
**FROM DIETSYS FOOD PORTION DATA**;
ARRAY CONV(2,2,3,3) _TEMPORARY_ (202 360 474 /*MID AGE WOMEN BEER*/
                                72 145 195  /*MID AGE WOMEN WINE*/
								28 43 69    /*MID AGE WOMEN LIQUOR*/
								270 360 474 /*MID AGE MEN BEER*/
								108 145 195 /*MID AGE MEN WINE*/
								28 42 69    /*MID AGE MEN LIQUOR*/
								180 360 474 /*OLD AGE WOMEN BEER*/
								72 108 145  /*OLD AGE WOMEN WINE*/
								28 42 69    /*OLD AGE WOMEN LIQUOR*/
								270 360 474 /*OLD AGE MEN BEER*/
								72 145 195  /*OLD AGE MEN WINE*/
								34 49 69)    /*OLD AGE MEN LIQUOR*/
								;

ARRAY ALCUSDA (3) BEERSPD WINESPD LIQSPD;

IF AGE92M<70 THEN AGECONV=1;
ELSE AGECONV=2;
IF GENDER=1 THEN GENCONV=2; **MEN ARE THE SECOND GENDER GROUP IN CONVERSION**;
ELSE GENCONV=1;

DO I=1 TO 3;
	**CONVERT TO BLOCK BEVERAGE SERVINGS/DAY***;
	IF ALCBEV(I)='1' THEN ALCPD(I)=0;
    ELSE IF ALCBEV(I)='2' THEN ALCPD(I)=0.066;
	ELSE IF ALCBEV(I)='3' THEN ALCPD(I)=0.142;
	ELSE IF ALCBEV(I)='4' THEN ALCPD(I)=0.43;
	ELSE IF ALCBEV(I)='5' THEN ALCPD(I)=0.78;
	ELSE IF ALCBEV(I)='6' THEN ALCPD(I)=1;
	ELSE IF ALCBEV(I)='7' THEN ALCPD(I)=2.5;
	ELSE IF ALCBEV(I)='8' THEN ALCPD(I)=4.5;
	ELSE IF ALCBEV(I)='9' THEN ALCPD(I)=6;
	ELSE ALCPD(I)=.;

	**CALCULATE BLOCK GRAMS***;
	**MODIFY AMOUNTS BY SERVING SIZE - AGE AND GENDER VALUES PER SUE 10/6/2009***;
    **CONVERT SERVING SIZE TO NUMERIC**;
	IF ALCSRV(I) IN('1','3') THEN SIZE=INPUT(ALCSRV(I),8.);
	ELSE SIZE=2;
    **APPLY AGE,GENDER,BEVERAGE,SIZE SPECIFIC GRAM CONVERSION TO THE SERVS PER DAY**;
	ALCGM(I)=ALCPD(I)*CONV(AGECONV,GENCONV,I,SIZE);

	***BACK CALCULATE TO USDA SERVINGS/DAY**;
	***USDA GRAMS TO SERVINGS CONVERSIONS FROM MARJI - Updated 3/3/2010***;
	IF I=1 THEN ALCUSDA(I)=ALCGM(I)/356;
	ELSE IF I=2 THEN ALCUSDA(I)=ALCGM(I)/118;
	ELSE IF I=3 THEN ALCUSDA(I)=ALCGM(I)/42;

END;

ALCSPD=SUM(BEERSPD,WINESPD,LIQSPD);
IF ALCSPD=0 THEN ALC_BLK=1;
ELSE IF 0<ALCSPD<1 THEN ALC_BLK=2;
ELSE IF 1<=ALCSPD<=2 THEN ALC_BLK=3;
ELSE IF 2<ALCSPD THEN ALC_BLK=4;
ELSE ALC_BLK=9;

LABEL ALC_BLK='Current Alcohol Use'; FORMAT ALC_BLK ALC_BLK.;
LABEL ALCSPD='Alcohol (servings/day)';

*****SMOKING STATUS AT COHORT ENTRY*****;
***1=NEVER SMOKER, 2=SMOKER, 9=MISSING (ONLY RELEVANT FOR 1992)****;

***1992***;
IF SMK92='1' AND SMK82 IN('1','7') THEN SMOKEV=1;
ELSE IF SMK92 IN('2','3','4','5','6') OR SMK82 NOT IN('1','7') THEN SMOKEV=2;
ELSE IF SMK92 IN('','7') THEN SMOKEV=9;

FORMAT SMOKEV SMOKEV.;
LABEL SMOKEV='Ever smoker at cohort entry';

***SITTING***; 
IF SIT92 IN('','*') THEN SIT=9;
ELSE IF SIT92 IN('1','2') THEN SIT=1;
ELSE IF SIT92 IN('3') THEN SIT=2;
ELSE IF SIT92 IN('4','5') THEN SIT=3;

LABEL SIT='Sitting(hours/day)'; FORMAT SIT SIT.;

***SIBLINGS VARIABLE***;
IF TOTBRO=0 AND TOTSIS=0 THEN SIBS=0;
ELSE IF TOTBRO GT 0 OR TOTSIS GT 0 THEN SIBS=1;

LABEL SIBS='Number of Siblings'; FORMAT SIBS SIBS.;

***ALL HEMA DEATHS****;

		***ICD-9 200,202-204,2733****;
		****Exclude: 202.3 Malignant Histiocytosis, 202.5 Letterer-Siwe Disease, 202.6 Mast Cell****;
		****Questionable: 202.8, 202.9, 204.2, 204.8***;

		***ICD-10 C82-C91***;
		****Questionable: C88.3, C88.7, C88.9****;

		***MYELOID LEUKEMIAS ADDED ('2050'<=CODE1<='2053' OR CODE1 IN ('2058','2059') OR 'C920'<=CODE1<='C925' OR CODE1 IN('C927','C929') - JB 8/22/2013***;
		***HODGKINS LYMPHOMA ADDED ("200"<=CODE1<="2053" OR "C81"<=CODE1<="C925" - INCORPORATED 2010-2019 AND C810-C819 INTO EXISTING CODE - JB 9/24/2013***;

NHLDTH=0; 
IF DEAD=1 THEN DO;
	IF ("200"<=CODE1<="2053" OR CODE1 IN('2733','2058','2059','2060','2070','2080','2081','2089') OR "C81"<=CODE1<="C925" OR CODE1 IN('C927','C929','C930','C931','C940','C943','C947','C950','C951','C959')) 
		AND (CODE1 NOT IN( "2023","2025","2026")) THEN DO; *EDITED 8/23/2013 JB - INCLUDED C920-C922 AS CASES, EDITED 9/24/13 - ADDED HODGKINS (2010-2019, C810-C819)*;
			NHLDTH=1; 
			NHLDCODE=CODE1;
	END;
	ELSE IF CODETYPE="2" AND ANYCADTH=0 THEN DO;
 		    IF (("200"<=CODE2<="2053" OR CODE2 IN('2733','2058','2059','2060','2070','2080','2081','2089')) AND (CODE2 NOT IN( "2023","2025","2026")))
			  	OR (("200"<=CODE3<="2053" OR CODE3 IN('2733','2058','2059','2060','2070','2080','2081','2089')) AND (CODE3 NOT IN( "2023","2025","2026")))
	     		THEN NHLDTH=2; 
			IF ("200"<=CODE2<="2053" OR CODE2 IN('2733','2058','2059','2060','2070','2080','2081','2089')) AND (CODE2 NOT IN( "2023","2025","2026")) THEN NHLDCODE=CODE2;
			ELSE NHLDCODE=CODE3;
			IF ("200"<=CODE2<="2053" OR CODE2 IN('2733','2058','2059','2060','2070','2080','2081','2089')) AND 
			   ("200"<=CODE3<="2053" OR CODE3 IN('2733','2058','2059','2060','2070','2080','2081','2089')) THEN DTHFLAG=1;
	END;
	ELSE IF CODETYPE="3" AND ANYCADTH=0 THEN DO;
		 IF ("C81"<=CODE2<="C925" OR CODE2 IN('C927','C929','C930','C931','C940','C943','C947','C950','C951','C959') OR "C81"<=CODE3<="C925" OR CODE3 IN('C927','C929','C930','C931','C940','C943','C947','C950','C951','C959')) 
				THEN NHLDTH=2;
			IF "C81"<=CODE2<="C925" OR CODE2 IN('C927','C929','C930','C931','C940','C943','C947','C950','C951','C959') THEN NHLDCODE=CODE2; *EDITED 8/23/2013 JB - INCLUDED C920-C922 AS CASES*; *EDITED 9/24/13 - ADDED HODGKINS (2010-2019, C810-C819)*;
			ELSE NHLDCODE=CODE3;
			IF ("C81"<=CODE2<="C925" OR CODE2 IN('C927','C929','C930','C931','C940','C943','C947','C950','C951','C959')) AND ("C81"<=CODE3<="C925" OR CODE3 IN('C927','C929','C930','C931','C940','C943','C947','C950','C951','C959')) 
				THEN DTHFLAG=1; *FLAGGING OBSERVATIONS THAT HAVE A 2ND AND 3RD COD AS NHL BUT NOT A FIRST*;
	END; *EDITED 8/23/2013 JB - INCLUDED C920-C921 AS CASES*; *EDITED 9/24/13 - ADDED HODGKINS (2010-2019, C810-C819)*;
END;

****CLEAN 2 SUBJECTS WITH MORE THAN 1 NHL DEATH CODE IN CODE2 AND CODE3****;
****USE MORE SPECIFIC CODE****;
*THERE ARE 8 TOTAL OF THESE SUBJECTS BUT THE OTHER 6 ARE EXCLUDED DUE TO PREVALENT CANCER - JB*;
IF ID='06005007001131' THEN NHLDCODE=CODE3; **CHANGE TO CLL FROM UNSPECIFIED LYMPHOMA***; 
IF ID='22144001042101' THEN NHLDCODE=CODE3; **CHANGE TO DLBCL FROM NHL,NOS***;

********************************************************************;             
** SETTING END OF FOLLOW-UP WITH CENSORING FOR 1992-2009 ANALYSIS **;             
********************************************************************;
********************CENSORING FOR ALL HEMA CASES********************;

YEARFU='XXXX';

******************LOST TO FOLLOW-UP (NO SURVEY RETURNS)**************;
	IF NUT_97=0 AND NUT_99=0 AND NUT_01=0 AND NUT_03=0 AND NUT_05=0 AND NUT_07=0 AND NUT_09=0
	   AND (DEATHDT=. OR DEATHDT>MDY(12,31,1997)) 
	   THEN YEARFU='LOST';

*******************CENSORING IN 1997****************************;

	*1997 LAST SURVEY RETURNED*;
	ELSE IF NUT_97=1 AND NUT_99=0 AND NUT_01=0 AND NUT_03=0 AND NUT_05=0 AND NUT_07=0 AND NUT_09=0 AND
	     (DEATHDT=. OR DEATHDT>MDY(12,31,1999)) THEN DO;
			YEARFU='1997';
			CENSREAS=1;
	END;

	*REPORTED HEMA CANCER ON LATER SURVEY (NOT VERIFIED OR DEATH) AND NO OTHER RETURNED SURVEYS BETWEEN*;
	*THERFORE THE OUTCOME STATUS IS UNKNOWN*;
	ELSE IF NUT_97=1 AND (NOT INVER) AND (NOT NHLDTH) AND
		    ( INCCAN99 NE '' OR 
			 (INCCAN01 NE '' AND NUT_99=0) OR
	         (INCCAN03 NE '' AND NUT_99=0 AND NUT_01=0) OR
			 (INCCAN05 NE '' AND NUT_99=0 AND NUT_01=0 AND NUT_03=0) OR
			 (INCCAN07 NE '' AND NUT_99=0 AND NUT_01=0 AND NUT_03=0 AND NUT_05=0) OR
			 (INCCAN09 NE '' AND NUT_99=0 AND NUT_01=0 AND NUT_03=0 AND NUT_05=0 AND NUT_07=0)
	        )  
		 THEN DO;
   		YEARFU='1997';
		CENSREAS=2;
	END;
	
	**REPORTED OTHER CANCER ON THE 1997 SURVEY***;
	ELSE IF NUT_97=1 AND OTHCAN97 NE '' THEN DO;
		YEARFU='1997';
		CENSREAS=3;
	END;

*******************CENSORING IN 1999****************************;

	*1999 LAST SURVEY RETURNED*;
	ELSE IF NUT_99=1 AND NUT_01=0 AND NUT_03=0 AND NUT_05=0 AND	NUT_07=0 AND NUT_09=0 AND
	     (DEATHDT=. OR DEATHDT>MDY(12,31,2001)) THEN DO;
		YEARFU='1999';
		CENSREAS=1;
	END;

	*REPORTED HEMA CANCER ON LATER SURVEY (NOT VERIFIED OR DEATH) AND NO OTHER RETURNED SURVEYS BETWEEN*;
	ELSE IF NUT_99=1 AND (NOT INVER) AND (NOT NHLDTH) AND
		    ( INCCAN01 NE '' OR
	         (INCCAN03 NE '' AND NUT_01=0) OR
			 (INCCAN05 NE '' AND NUT_01=0 AND NUT_03=0)	OR
			 (INCCAN07 NE '' AND NUT_01=0 AND NUT_03=0 AND NUT_05=0) OR
			 (INCCAN09 NE '' AND NUT_01=0 AND NUT_03=0 AND NUT_05=0 AND NUT_07=0)
	        )  
		 THEN DO;
		YEARFU='1999';
		CENSREAS=2;
	END;
	
	**REPORTED OTHER CANCER ON THE 1999 SURVEY***;
	ELSE IF NUT_99=1 AND OTHCAN99 NE '' THEN DO;
		YEARFU='1999';
		CENSREAS=3;
	END;

*******************CENSORING IN 2001****************************;

	*2001 LAST SURVEY RETURNED*;
	ELSE IF NUT_01=1 AND NUT_03=0 AND NUT_05=0 AND NUT_07=0 AND NUT_09=0 AND
	     (DEATHDT=. OR DEATHDT>MDY(10,31,2003)) THEN DO;
		YEARFU='2001';
		CENSREAS=1;
	END;

	*REPORTED HEMA CANCER ON LATER SURVEY (NOT VERIFIED OR DEATH) AND NO OTHER RETURNED SURVEYS BETWEEN*;
	ELSE IF NUT_01=1 AND (NOT INVER) AND (NOT NHLDTH) AND
		    ( INCCAN03 NE '' OR
	         (INCCAN05 NE '' AND NUT_03=0) OR
			 (INCCAN07 NE '' AND NUT_03=0 AND NUT_05=0) OR
			 (INCCAN09 NE '' AND NUT_03=0 AND NUT_05=0 AND NUT_07=0)
	        )  
		 THEN DO;
		YEARFU='2001';
		CENSREAS=2;
	END;
    
	**REPORTED OTHER CANCER ON THE 2001 SURVEY***;
	ELSE IF NUT_01=1 AND OTHCAN01 NE '' THEN DO;
		YEARFU='2001';
		CENSREAS=3;
	END;

*******************CENSORING IN 2003****************************;

	*2003 LAST SURVEY RETURNED*;
	ELSE IF NUT_03=1 AND NUT_05=0 AND NUT_07=0 AND NUT_09=0 AND
	     (DEATHDT=. OR DEATHDT>MDY(10,31,2005)) THEN DO;
		YEARFU='2003';
		CENSREAS=1;
	END;

	*REPORTED HEMA CANCER ON LATER SURVEY (NOT VERIFIED OR DEATH) AND NO OTHER RETURNED SURVEYS BETWEEN*;
	ELSE IF NUT_03=1 AND (NOT INVER) AND (NOT NHLDTH) AND
			(INCCAN05 NE '' OR 
			 (INCCAN07 NE '' AND NUT_05=0) OR
			 (INCCAN09 NE '' AND NUT_05=0 AND NUT_07=0)
             )  
		 THEN DO;
		YEARFU='2003';
		CENSREAS=2;
	END;
    
	**REPORTED OTHER CANCER ON THE 2003 SURVEY***;
	ELSE IF NUT_03=1 AND OTHCAN03 NE '' THEN DO;
		YEARFU='2003';
		CENSREAS=3;
	END;

 ******************CENSORING IN 2005*****************************;
	*2005 LAST SURVEY RETURNED*;
	ELSE IF NUT_05=1 AND NUT_07=0 AND NUT_09=0 AND 
			(DEATHDT=. OR DEATHDT>MDY(10,31,2007)) THEN DO;
		YEARFU='2005';
		CENSREAS=1;
	END;

	*REPORTED HEMA CANCER ON LATER SURVEY (NOT VERIFIED OR DEATH) AND NO OTHER RETURNED SURVEYS BETWEEN*;
	ELSE IF NUT_05=1 AND (NOT INVER) AND (NOT NHLDTH) AND 
			(INCCAN07 NE '' OR
			 (INCCAN09 NE '' AND NUT_07=0)
             )  
		 THEN DO;
		YEARFU='2005';
		CENSREAS=2;
	END;
    
	**REPORTED OTHER CANCER ON THE 2005 SURVEY***;
	ELSE IF NUT_05=1 AND OTHCAN05 NE '' THEN DO;
		YEARFU='2005';
		CENSREAS=3;
	END;

 ******************CENSORING IN 2007*****************************;
	*2007 LAST SURVEY RETURNED*;
	ELSE IF NUT_07=1 AND NUT_09=0 AND 
			(DEATHDT=. OR DEATHDT>MDY(06,30,2009)) THEN DO;
		YEARFU='2007';
		CENSREAS=1;
	END;

	*REPORTED HEMA CANCER ON LATER SURVEY (NOT VERIFIED OR DEATH) AND NO OTHER RETURNED SURVEYS BETWEEN*;
	ELSE IF NUT_07=1 AND (NOT INVER) AND (NOT NHLDTH) AND 
			(INCCAN09 NE ''
             )  
		 THEN DO;
		YEARFU='2007';
		CENSREAS=2;
	END;
    
	**REPORTED OTHER CANCER ON THE 2007 SURVEY***;
	ELSE IF NUT_07=1 AND OTHCAN07 NE '' THEN DO;
		YEARFU='2007';
		CENSREAS=3;
	END;

******************CENSORING IN 2009(END OF FOLLOW-UP)*************;
    *2009 LAST SURVEY RETURNED*;
	ELSE IF NUT_09=1 THEN YEARFU='2009';

********OTHERWISE THEY ARE CENSORED AT DEATH******;
	ELSE IF DEAD THEN YEARFU='DEAD';

LABEL CENSREAS='Reason for Censoring';
FORMAT CENSREAS CENSREAS.;

****ASSIGN THE END OF FOLLOW-UP DATE BASED ON CENSORING****;

	IF YEARFU='1997' THEN DO; 
		IF DTINT97N NE . THEN ENDFU=DTINT97N;
        ELSE ENDFU=MDY(12,31,1997);
    END;
    ELSE IF YEARFU='1999' THEN DO;
    	IF DTINT99 NE . THEN ENDFU=DTINT99;
        ELSE ENDFU=MDY(12,31,1999);
    END;
    ELSE IF YEARFU='2001' THEN DO;
    	IF DTINT01 NE . THEN ENDFU=DTINT01;
        ELSE ENDFU=MDY(12,31,2001);
    END;
    ELSE IF YEARFU='2003' THEN DO;
    	IF DTINT03 NE . THEN ENDFU=DTINT03;
        ELSE ENDFU=MDY(10,31,2003);
    END;
	ELSE IF YEARFU='2005' THEN DO;
		IF DTINT05 NE . THEN  ENDFU=DTINT05;
        ELSE ENDFU=MDY(10,31,2005);
    END;
	ELSE IF YEARFU='2007' THEN DO;
		IF DTINT05 NE . THEN  ENDFU=DTINT07;
        ELSE ENDFU=MDY(10,31,2007);
    END;
    ELSE IF YEARFU='2009' THEN ENDFU=MDY(6,30,2009);
	ELSE IF YEARFU='DEAD' THEN ENDFU=DEATHDT;
	ELSE IF YEARFU='LOST' THEN ENDFU=.L;

***************************************************************;
*DEFINE CASES BASED ON END OF FOLLOW-UP                       *;
***************************************************************;

*********THIS INCLUDES ALL HEMA (LYMPHNOS, HODGKINS, & NHL)***********;

***SELF-REPORTED CASES****;
HEMSELF=(INCCAN97 NE '' OR INCCAN99 NE '' OR INCCAN01 NE '' OR INCCAN03 NE '' OR
		 INCCAN05 NE '' OR INCCAN07 NE '' OR INCCAN09 NE '');

***VERIFIED NHL/HODGKIN CANCERS***;
*** MODIFIED BY TURNER PAPER LN(B) AND LN(T) ARE NOW NHLS - WRD 6/29/2012**;
ALL_HEMA=0; 
IF INVER AND 0<DXDATEN<=ENDFU THEN ALL_HEMA=1; ***VERIFIED HEMA***;
ELSE IF NOT INVER AND YEARFU='DEAD' AND NHLDTH NE 0 THEN ALL_HEMA=1; **HEMA DEATHS**;

***DEFINE CASE STATUS PRE-CENSORING - ALL CASES BEFORE 6/30/2009****;
ALL_HEMA_PC=0;
IF ILGRP6 NE . AND DXDATEN<=MDY(06,30,2009) THEN ALL_HEMA_PC=1; ***VERIFIED ALL HEMA***;
ELSE IF NOT INVER AND NHLDTH NE 0 AND DEATHDT<=MDY(06,30,2009) THEN ALL_HEMA_PC=1;  **ALL HEMA DEATHS**;

*********CASE ASCERTAINMENT*******;
IF ALL_HEMA=1 THEN DO;
	IF HEMSELF=1 AND INVER THEN ASCERTAIN=1; **SELF-REPORTED NHL AND VERIFIED***;
	ELSE IF NHLDTH NE 0 AND INVER THEN ASCERTAIN=2; **DIED OF NHL AND VERIFIED***;
	ELSE IF NHLDTH NE 0 THEN ASCERTAIN=3; **DIED OF NHL, NOT VERIFIED***;
	ELSE IF INVER AND (OTHCAN97 NE '' OR OTHCAN99 NE '' OR OTHCAN01 NE '' OR OTHCAN03 NE '' OR OTHCAN05 NE '' OR OTHCAN07 NE '' OR OTHCAN09 NE '') 
			THEN ASCERTAIN=4; *REPORTED OTHER CANCER, VERIFIED NHL**;
	ELSE IF INVER AND ANYCADTH=1 THEN ASCERTAIN=5; **DIED OF CANCER, VERIFIED NHL***;
	ELSE IF INVER THEN DO; ASCERTAIN=6; ALL_HEMA=0; END;**CASE FOUND AFTER FOLLOW-UP END FOR THIS STUDY 6/30/2009, RESET TO NON-CASE**;   
END;

***1 SUBJECT - NOT IN REPORTED BECAUSE DENIED CANCER PER P.BRIGGS - RECODE FROM ASCERTAIN=6***;
IF ID='25460011001102' THEN DO; ALL_HEMA=1; ALL_HEMA_PC=1; ASCERTAIN=4; END;

LABEL ASCERTAIN='Case Ascertainment';
FORMAT ASCERTAIN ASCERTAIN.;

****CODING SUB-TYPES****;
**HEMGRP - DEATHS ARE INCLUDED AS NHL, NOS***; *FURTHER REFINING NHL CATEGORIES*;
HEMGRP=0;
IF ALL_HEMA THEN DO;
   *ASSIGN SUBCATEGORIES OF B/T CELL - TIER 3 *;
   IF ILGRP6 IN(14:17) THEN HEMGRP=1; **DLBCL**;
   ELSE IF ILGRP6 IN(8:9) THEN HEMGRP=2; **CLL/SLL**;
   ELSE IF ILGRP6=22 THEN HEMGRP=3; **FOLLICULAR**;
   ELSE IF ILGRP6 IN (24,25) THEN HEMGRP=4; **MULTIPLE MYELOMA**; *ADDED PLASMA CELL NEOPLASMS ILGRP6=24 (9731/9734) - JB 7/19/2013*; 
   ELSE IF ILGRP6 IN(7,10:13,18:21,23,24,26,27,50) THEN HEMGRP=5; **OTHER B-CELL - ADDED 50 ON 6/29/2012***;
   ELSE IF ILGRP6 IN(28:43,51) THEN HEMGRP=6; **T-CELL - ADDED 51 ON 6/29/2012**;
   ELSE IF ILGRP6 IN(44:46) THEN HEMGRP=7; **NHL NOS, INCLUDES DEATH ONLY**;
   ELSE IF LUKGRP4 IN(1:4) THEN HEMGRP=8; **ALL MYELOID LEUKEMIAS**; *ADDED 7/18/13 BY JB - LUKGRP4 FROM YUSHENG'S LEUKEMIA GROUPS MACRO - SWITCHED LEUKEMIA NOS TO ITS OWN SUBGROUP 10/4/2013*;
   ELSE IF ILGRP6 IN (1:6) THEN HEMGRP=9; **HODGKIN LYMPHOMAS**; *ADDED 9/23/2013 BY JB*;
   ELSE IF ILGRP6 IN (47:49,52) OR ILGRP6=. THEN HEMGRP=10; **LYMPHOMA NOS - UNK LYMPHOID/MYELOID**; *ADDED 9/23/2013 BY JB - NO DEATH CODE FOR THIS CATEGORY*;
   ELSE IF LUKGRP4=5 THEN HEMGRP=11; **LEUKEMIA NOS - UNK LYMPHOID/MYELOID**;

END; 

***HEMGRP_DTH*****;
***GROUP DEATHS IN TO SUB-GROUPS IF POSSIBLE***;
*IF THE PERSON DIED FROM NHL ILGRP6 =.*;
HEMGRP_DTH=HEMGRP;
IF ALL_HEMA=1 AND ILGRP6=. THEN DO;
		IF CODETYPE='2' THEN DO;
		IF NHLDCODE='2000' THEN HEMGRP_DTH=1; **DLBCL**;
		ELSE IF NHLDCODE='2041' THEN HEMGRP_DTH=2; *CLL*;
		ELSE IF NHLDCODE='2020' THEN HEMGRP_DTH=3; *FOLLICULAR**;
		ELSE IF '2030'<=NHLDCODE<='2031' OR NHLDCODE='2038' THEN HEMGRP_DTH=4; **MULTIPLE MYELOMA**; *ADDED PLASMA CELL NEOPLASMS NHLDCODE='2038' - JB 7/23/2013*;
		ELSE IF NHLDCODE IN('2001','2002','2024','2028','2733') THEN HEMGRP_DTH=5; **OTHER B, BURKITT, HAIRY CELL, WALDENSTROM*;
		ELSE IF NHLDCODE IN('2021','2022') THEN HEMGRP_DTH=6; **T-CELL, MF AND SEZARY**;
		ELSE IF '2050'<=NHLDCODE<='2053' OR NHLDCODE IN('2058','2059','2060','2070')THEN HEMGRP_DTH=8; **MYELOID LEUKEMIAS**; *ADDED- JB 7/28/2013*; 
		ELSE IF '2010'<=NHLDCODE<='2012' OR '2014'<=NHLDCODE<='2017' OR NHLDCODE='2019' THEN HEMGRP_DTH=9; **HODGKIN LYMPHOMA**;
		ELSE HEMGRP_DTH=10; **HEMA NOS**; 
	END;
	ELSE IF CODETYPE='3' THEN DO;
		IF NHLDCODE='C833' THEN HEMGRP_DTH=1; **DLBCL**;
		ELSE IF NHLDCODE IN('C830','C911') THEN HEMGRP_DTH=2; **CLL/SLL**;
		ELSE IF 'C82'<=NHLDCODE<='C829' THEN HEMGRP_DTH=3; **FOLLICULAR**;
		ELSE IF NHLDCODE IN('C900','C901','C902') THEN HEMGRP_DTH=4; **MULTIPLE MYELOMA**; *ADDED PLASMA CELL NEOPLASMS NHLDCODE='C902' - JB 7/23/2013*;
		ELSE IF 'C83'<=NHLDCODE<='C839' OR NHLDCODE IN('C851','C852') 
			 OR 'C88'<=NHLDCODE<='C889' OR NHLDCODE IN('C910','C913','C914','C918') THEN HEMGRP_DTH=5; **OTHER B-CELL***;
		ELSE IF 'C84'<=NHLDCODE<='C849' OR 'C86'<=NHLDCODE<='C869' 
             OR NHLDCODE IN('C915','C916') THEN HEMGRP_DTH=6; ***T-CELL**;
		ELSE IF 'C920'<=NHLDCODE<='C925' OR NHLDCODE IN('C927','C929','C930','C931','C940','C943')THEN HEMGRP_DTH=8; **MYELOID LEUKEMIAS**; 
		ELSE IF 'C810'<=NHLDCODE<='C814' OR NHLDCODE IN('C817','C819') THEN HEMGRP_DTH=9; **HODGKIN LYMPHOMA**; *ADDED- JB 9/23/2013*;
		ELSE HEMGRP_DTH=10; **HEMA NOS***;
	END;
END;
*DID NOT ADD ANY DEATHCODES FOR LEUKEMIA NOS - JB 10/4/2013*;

LABEL HEMGRP='NHL Sub-types'
      HEMGRP_DTH='NHL Sub-types with Deaths'
      ; 
FORMAT HEMGRP HEMGRP_DTH HEMGRPS.;

*ADDING SEPERATE CATEGORIES FOR AML AND CML - 8/22/2013 - NOTE THESE ARE ONLY FOR THE DEATH CODES, NEED TO BE RECODED IF JUST USING HISTOLOGY CODES*;
AML_D=0; CML_D=0;
IF HEMGRP_DTH GT 0 THEN DO;
	IF LUKGRP4=1 THEN AML_D=1;
	ELSE IF LUKGRP4=2 THEN CML_D=1;
IF ALL_HEMA=1 AND ILGRP6=. THEN DO;
	IF CODETYPE='2' THEN DO;
		IF NHLDCODE = '2050' THEN AML_D=1; **ADDED AML - 8/22/2013 JB**;
		ELSE IF NHLDCODE = '2051' THEN CML_D=1; **ADDED CML - 8/22/2013 JB**;
	END;
	IF CODETYPE='3' THEN DO;
		IF NHLDCODE IN ('C920', 'C924', 'C925') THEN AML_D=1; **ADDED AML - 8/22/2013 JB**;
		ELSE IF NHLDCODE = 'C921' THEN CML_D=1; **ADDED CML - 8/22/2013 JB**;
		END;
	END;
END;

**INDIVIDUAL SUB-GROUP OUTCOME VARIABLES INCLUDING DEATHS**;
BCELL_D=(HEMGRP_DTH IN(1,2,3,4,5));
ALL_NHL=(HEMGRP_DTH IN (1,2,3,4,5,6,7)); *INDICATOR VARIABLE FOR ALL NHL (NOT INCLUDING MYELOID) - JB 8/23/2013*;
ALL_NHL_NOMM=(HEMGRP_DTH IN (1,2,3,5,6,7)); *INDICATOR VARIABLE FOR ALL NHL EXCLUDING MULTIPLE MYELOMA - JB 10/4/2013*;
ALL_OTHER= (HEMGRP_DTH IN (5,6,7,9,10,11)); *INDICATOR VARIABLE FOR ALL OTHER HEMAS NOT USED IN SUB-ANALYSES*;
LYMPHOID_D=(HEMGRP_DTH IN(1,2,3,4,5,6,7,9)); *ADDED 5/6/14*;

DLBCL_D=(HEMGRP_DTH=1);
CLLSLL_D=(HEMGRP_DTH=2);
FOLLIC_D=(HEMGRP_DTH=3);
MYELOMA_D=(HEMGRP_DTH=4);
OTHERB_D=(HEMGRP_DTH=5);
TCELL_D=(HEMGRP_DTH=6);
OTHLYMPH_D=(HEMGRP_DTH=7);
MYELUK_D=(HEMGRP_DTH=8);
HODGKIN_D=(HEMGRP_DTH=9);
LYMPHNOS_D=(HEMGRP_DTH=10);
LEUKNOS_D=(HEMGRP_DTH=11);

LABEL BCELL_D='B-Cell lymphoma'
	  ALL_NHL='All NHL including MM'
	  ALL_NHL_NOMM='All NHL excluding MM' 
	  ALL_OTHER='All other hemas not used in sub-analysis'
	  DLBCL_D='Diffuse large B-cell lymphoma (DLBCL)' 
	  CLLSLL_D='Chronic lymphocytic leukemia/Small lymphocytic lymphoma(CLL/SLL)'
	  FOLLIC_D='Follicular lymphoma'
	  MYELOMA_D='Multiple myeloma'
	  OTHERB_D='Other B-cell lymphoma'
	  TCELL_D='T-cell lymphoma'
	  OTHLYMPH_D='NHL, NOS'
	  MYELUK_D='Myeloid Leukemias'
	  AML_D='Acute Myeloid Leukemia'
	  CML_D= 'Chronic Myeloid Leukemia'
	  HODGKIN_D= 'Hodgkins Lymphoma'
	  LYMPHNOS_D= 'Unknown Lymphoid/Myeloid'
	  LEUKNOS_D= 'Unknown Leukemia'
	  LYMPHOID_D= 'Lymphoid neoplasms'
	  ;

*******************************************************************;
*CALCULATION OF FAIL TIME										  *;
*******************************************************************;
IF (ALL_HEMA=1) AND INVER THEN DATEFT=DXDATEN; **HODGKIN AND LYMPHOMA NOS ARE CENSORED AT DX DATE FOR NHL ANALYSES**;  
ELSE DATEFT=ENDFU; ***INCLUDES NHL DEATHS, ENDFU=DEATHDT FOR THEM***;

IF 0<DATEFT<DTINT92 THEN DO;
IF (ALL_HEMA=1) THEN DATEFT=.P;
ELSE DATEFT=.N;
END;
IF DATEFT=DTINT92 THEN DO;
IF (ALL_HEMA=1) THEN DATEFT=.P;
ELSE DATEFT=DTINT92+1;
END;

***FAIL TIME VARIABLE***;
FAILNEW=(DATEFT-DTINT92)/365.25;

***CLEAN SUBJECTS WITH MISSING SURVEY DATES****;
DATE97=DTINT97N;
IF DTINT97=. THEN DATE97=MDY(12,31,1997);

DATE99=DTINT99;
IF DTINT99=. THEN DATE99=MDY(12,31,1999);

DATE01=DTINT01;
IF DTINT01=. THEN DATE01=MDY(12,31,2001);

DATE03=DTINT03;
IF DTINT03=. THEN DATE03=MDY(10,31,2003);

DATE05=DTINT05;
IF DTINT05=. THEN DATE05=MDY(10,31,2005);

DATE07=DTINT07;
IF DTINT07=. THEN DATE07=MDY(10,31,2007);

DATE09=MDY(06,30,2009);

**FAIL TIMES AFTER DATEFT DEFAULT TO MISSING***;
IF DATEFT<=DATE97 THEN FAIL9297=(DATEFT-DTINT92)/365.25;
ELSE DO;
	FAIL9297=(DATE97-DTINT92)/365.25;
	IF DATEFT<=DATE99 THEN FAIL9299=(DATEFT-DTINT92)/365.25;
	ELSE DO;
		FAIL9299=(DATE99-DTINT92)/365.25;
		IF DATEFT<=DATE01 THEN FAIL9201=(DATEFT-DTINT92)/365.25;
		ELSE DO;
			FAIL9201=(DATE01-DTINT92)/365.25;
			IF DATEFT<=DATE03 THEN FAIL9203=(DATEFT-DTINT92)/365.25;
			ELSE DO;
				FAIL9203=(DATE03-DTINT92)/365.25;
				IF DATEFT<=DATE05 THEN FAIL9205=(DATEFT-DTINT92)/365.25;
				ELSE DO;
					FAIL9205=(DATE05-DTINT92)/365.25;
					IF DATEFT<=DATE07 THEN FAIL9207=(DATEFT-DTINT92)/365.25;
					ELSE DO;
						FAIL9207=(DATE07-DTINT92)/365.25;
						IF DATEFT<=DATE09 THEN FAIL9209=(DATEFT-DTINT92)/365.25;
						ELSE FAIL9209=(DATE09-DTINT92)/365.25;
					END;
				END;
			END;
		END;
	END;
END;

FAIL9799=FAIL9299-FAIL9297;
FAIL9901=FAIL9201-FAIL9299;
FAIL0103=FAIL9203-FAIL9201;
FAIL0305=FAIL9205-FAIL9203;
FAIL0507=FAIL9207-FAIL9205;
FAIL0709=FAIL9209-FAIL9207;

*CHECK FAIL TIME TOTALS*;
FAILCHECK=(FAILNEW NE SUM(FAIL9297,FAIL9799,FAIL9901,FAIL0103,FAIL0305,FAIL0507));

*************************************************************;
*EXCLUSION CASCADE											*;
*************************************************************;
***LOST TO FOLLOW-UP***;
EX1=0;
IF ENDFU=.L THEN EX1=1;

LABEL EX1='Lost to Follow-up';

***PREVALENT CANCER****;
EX2=0;
IF CA82 NOT IN('N','73') THEN EX2=1;
ELSE IF CA92 NE 0 THEN EX2=1;
ELSE IF DATEFT=.P THEN EX2=1;

LABEL EX2='Prevalent Cancer';

***MISSING MOTHER'S/FATHER'S/BOTH AGE AT BIRTH****;
EX3=0; 
IF MAGEBTH_4CAT =. OR FAGEBTH_4CAT = . THEN EX3=1; *THERE WERE 10 OBSERVATIONS THAT WERE MISSING AGE AT BIRTH AND HAD A FAMILY HX OF HEMA MALIGNANCIES. THEY ARE CATEGORIZED UNDER MISSING AGE AT BIRTH*;

LABEL EX3='Missing Parents Age at Birth';

***REPORTED HEME CANCER NOT VERIFIED (1ST INTERVAL)***;
EX4=0;
IF (NOT INVER) AND (NOT NHLDTH) AND 
			   ( INCCAN97 NE '' OR 
                (NUT_97=0 AND INCCAN99 NE '') OR
                (NUT_97=0 AND NUT_99=0 AND INCCAN01 NE '') OR
				(NUT_97=0 AND NUT_99=0 AND NUT_01=0 AND INCCAN03 NE '') OR
				(NUT_97=0 AND NUT_99=0 AND NUT_01=0 AND NUT_03=0 AND INCCAN05 NE '') OR
                (NUT_97=0 AND NUT_99=0 AND NUT_01=0 AND NUT_03=0 AND NUT_05=0 AND INCCAN07 NE '') OR
                (NUT_97=0 AND NUT_99=0 AND NUT_01=0 AND NUT_03=0 AND NUT_05=0 AND NUT_07=0 AND 
				 INCCAN09 NE '' AND REPDXYR09 NE 4)
			   )
	THEN EX4=1;

LABEL EX4='Reported Heme Cancer, Not Verified';

***BAD DIAGNOSIS DATE (> 6 MONTHS AFTER 1ST REPORT)****;
EX5=0;
IF DXDATEN=.B THEN EX5=1;

LABEL EX5='Dx Date > 6 Months After Report of Cancer';

***FAMILY HISTORY OF HEMATOLOGIC MALIGNANCY***;
ARRAY SITE(*) FSITE MSITE S1SITE S2SITE S3SITE S4SITE S5SITE S6SITE;

EX6=0;
DO I=1 TO DIM(SITE);
   IF SITE(I) IN('00','22','23','24','25') THEN DO;
	  EX6=1;
	END;
END;

LABEL EX6='Family History of Hematopoietic Cancer';
	  
FORMAT EX6 FAMHX.;

*****OVERALL EXCLUSION VARIABLE******;
ARRAY EXCL(*) EX1-EX6;

EXCLUSN=99;
EXNUM=0;
DO I=1 TO DIM(EXCL);
   EXNUM=EXNUM+1;
   IF EXCLUSN=99 AND EXCL(I)=1 THEN DO;
      EXCLUSN=EXNUM;
	  I=DIM(EXCL);
   END;
END;

RUN;


DATA PAGE.FINALCOHORT PAGE.MALENOSIBS PAGE.FEMNOSIBS PAGE.MALESIBS PAGE.FEMSIBS PAGE.NOSIBS;
SET PAGE.INITIALCOHORT;

IF EXCLUSN=99;

IF (SIBS=0 AND SEX_NEW=0) THEN OUTPUT PAGE.MALENOSIBS; *OUTPUTTING DATASET WITH MALE ONLY CHILDREN*;
IF (SIBS=0 AND SEX_NEW=1) THEN OUTPUT PAGE.FEMNOSIBS; *OUTPUTTING DATASET WITH FEMALE ONLY CHILDREN*;
IF (SIBS=1 AND SEX_NEW=0) THEN OUTPUT PAGE.MALESIBS; *OUTPUTTING DATASET WITH MALES WITH SIBLINGS*;
IF (SIBS=1 AND SEX_NEW=1) THEN OUTPUT PAGE.FEMSIBS; *OUTPUTTING DATASET WITH FEMALE WITH SIBLINGS*;
IF SIBS=0 THEN OUTPUT PAGE.NOSIBS; *OUTPUTTING DATASET WITH ONLY CHILDREN*;

OUTPUT PAGE.FINALCOHORT; *OUTPUTTING FULL DATASET*;

RUN;

/*
PROC FREQ DATA=ONE;
TABLES NUT_92*NUT_97*NUT_99*NUT_01*NUT_03*NUT_05*NUT_07*NUT_09/LIST MISSING;
WHERE EX1=1;
TITLE 'CHECK OF LOST TO FOLLOW-UP EXCLUSION';
RUN;

PROC FREQ DATA=ONE;
TABLES MAGEBTH*FAGEBTH*EX3/LIST MISSING;
RUN; 


*NOT 100% SURE WHAT THIS DOES*;
PROC FREQ DATA=ONE;
TABLES CA92/LIST MISSING;
WHERE EX2=1; 
TITLE 'CHECK OF THE PREVALENT CANCER EXCLUSION';
RUN;

PROC FREQ DATA=ONE;
TABLES CA82/MISSING;
WHERE EX2=1 AND CA92=0;
TITLE 'CHECK OF THE PREVALENT CANCER EXCLUSION';
RUN;

***NOTE***;
*2 AML CASES ARE NON-CASES (ID: 06016130004111, 25507001007031) BECAUSE ID: 06016130004111 RETURNED ALL SURVEYS BUT NEVER REPORTED THE CANCER AND DIED RIGHT AFTER FOLLOW UP. ID 25507001007031 HAS AN 
ASCERTAIN=6*;

PROC PRINT DATA=ONE;
WHERE EXCLUSN=99 AND HEMGRP_DTH=0 AND AML=1 AND CENSREAS=.;
VAR ID HISTOGY SITEC DATASRC INCCANV ILGRP6 DODYRN DODMON DODYRN DXDATE INCCAN: REP: NUT: CODE1 CODE2 CODE3 DATEDD DEAD YEARFU ASCERTAIN;
FORMAT DXDATEN DATEDD DATE9.;
RUN;


ODS HTML FILE= "D:\USER\JBLASE\Hema Malignancies & Parents Age\OUTPUT\DEATHCODE FREQUENCIES.XLS"; 
PROC FREQ DATA= PAGE.FINALCOHORT;
WHERE ILGRP6=. AND HEMA_TOT=1;
TABLES HEMGRP_DTH*CODETYPE*NHLDCODE/LIST NOCUM NOPERCENT NOROW NOCOL;
RUN;
ODS HTML CLOSE;


PROC FREQ DATA=PAGE.FINALCOHORT;
WHERE LYMPHNOS=1;
TABLES HISTOGY*HEMGRP_DTH/LIST;
RUN;

PROC FREQ DATA= PAGE.FINALCOHORT;
WHERE HEMA_TOT=1 AND ILGRP6=98;
TABLES LUKGRP4/LIST;
RUN;

PROC FREQ DATA= PAGE.FINALCOHORT;
TABLES HEMA_TOT;
RUN;

*MAKING TERTILE CUTPOINTS FOR AGE AT COHORT ENTRY VARIABLE*; 
PROC UNIVARIATE DATA=TWO;
VAR AGE92M;
OUTPUT OUT=SUBTCAT_AGE PCTLPTS= 33 67 PCTLPRE=PCT;
RUN;
PROC PRINT DATA = SUBTCAT_AGE;RUN;
*/
