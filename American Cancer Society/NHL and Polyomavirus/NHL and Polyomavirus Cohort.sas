
/*============================================================================================================
  Project Name     : NHL & POLYOMAVIRUS
  PI               : LAUREN TERAS
  Biostatistician  : Yusheng Zhai, Jenny Blase
  Program Objective: CREATE COHORT
  Dataset used     : Sera Dilution 1:1000, data prepared by LAUREN
  Last update      : 6/19/2012, 9/5/2013, 12/3/13 - Reworked by JB
  Updated		   : 12/18/2013 - Changed covariates for 
					 9/15/14 - Erased code that wasn't used, removed code for old antigen quartiles (included seronegatives in quartiles)
  ===========================================================================================================*/
OPTIONS MPRINT;
TITLE 'NHL and PLYMA' ; 
 
    FOOTNOTE1  "Program: D:\USER\JBLASE\NHL & Polyomavirus\Jenny\PROGRAM\NHL AND POLYOMAVIRUS COHORT.SAS "; 
    FOOTNOTE2  "Last run by Jenny Blase on &sysdate";

LIBNAME PLYMA  'D:\USER\JBLASE\NHL & Polyomavirus\Jenny\PROGRAM REVIEW\DATA';
LIBNAME ORIG82 'D:\CPS\CPS2\ORIGINAL';
LIBNAME ORG    'D:\CPS\NUTRITION\ORIGINAL';
LIBNAME DER    'D:\CPS\NUTRITION\DERIVED';
LIBNAME LLDRIV 'D:\CPS\BIOSPECIMENS\Lifelink\Derived';
LIBNAME VERIFY 'D:\CPS\NUTRITION\VERIFIED';
LIBNAME MAST   'D:\CPS\MASTER';
LIBNAME LIBRARY 'D:\USER\JBLASE';


/*PROC FORMAT LIBRARY=LIBRARY; */
/**/
/*VALUE  CACOF  1 = "1: CASE"*/
/*			  0 = "0: CONTROL"; */
/*		  		*/
/*VALUE BMIWHO 	0='0: <25.0 (UNDER/NORMAL)'				*/
/*			 	1='1: 25.0-<30.0 (OVERWEIGHT)'*/
/*			 	2='2: >=30.0 (OBESE)'*/
/*			 	9='9: UNKNOWN'; */
/*				*/
/*VALUE $ REGIONF '1' = '1: Northeast' */
/*				'2' = '2: West'      */
/*				'3' = '3: South'     */
/*				'4' = '4: MidWest'; */
/* */
/*VALUE EDUCAF    1 = '1: Less than high school'*/
/*				2 = '2: High school/missing education'*/
/*				3 = '3: Some college/vocational school'*/
/*				4 = '4: College/graduate school'; */
/**/
/*VALUE QUARTILES 1 = '1. <=25%'*/
/*                2 = '2. 26-50%'*/
/*				3 = '3. 51-75%'*/
/*				4 = '4. >75%'*/
/*				5 = '5. Seronegative';*/
/**/
/*VALUE SUB1CAT	0 = 'Control'*/
/*				1 = 'DLBCL'*/
/*				2 = 'CLL/SLL'*/
/*				3 = 'Follicular'*/
/*				4 = 'Other NHL'*/
/*				5 = 'Multiple Myeloma';*/
/*				*/
/*VALUE EXCLUSION 1 = 'Missing lab data'*/
/*				2 = 'Hodgkin Lymphoma'*/
/*				. ='Final Cohort';				*/
/**/
/*VALUE SERONP 0 = 'Sero-'*/
/*			 1 = 'Sero+';*/
/**/
/*VALUE SEX	0 = 'Female'*/
/*			1 = 'Male';*/
/**/
/*VALUE ALC	1 = '0 DRINKS/DAY'*/
/*			2 = '<=1 DRINKS/DAY'*/
/*			3 = '>1 DRINKS/DAY'*/
/*			9 = 'MISSING';*/
/**/
/*VALUE $ SMOKE '1' = 'NEVER'*/
/*			  '2' = 'CURRENT'*/
/*			  '3' = 'FORMER'*/
/*			  '9' = 'MISSING';*/
/**/
/*VALUE RACENEW 1 = 'White'*/
/*			  2 = 'Black'*/
/*			  3 = 'Other/Missing';*/
/**/
/*VALUE TERTILES 1 = '1. <=33%'*/
/*			   2 = '2. 34-66%'*/
/*			   3 = '3. >66%'*/
/*			   4 = '4. Seronegative';*/
/**/
/*VALUE MEDS	1 = '1. <=50%'*/
/*			2 = '2. >50%'*/
/*			3 = '3. Seronegative';*/
/**/
/*VALUE ALC 	1='1: Non-drinker'*/
/*          		2='2: < 1/day'*/
/*		  		3='3: 1-2/day'*/
/*		  		4='4: >2/day'*/
/*		  		9='9: Missing'*/
/*				;*/
/*	VALUE N_ALC 1='1: Non-drinker'*/
/*            	2='2: < 1/day'*/
/*		    	3='3: 1-2/day'*/
/*		    	4='4: >2-3/day'*/
/*		    	5='5: >3/day'*/
/*		    	9='9: Missing'*/
/*				;*/
/*	VALUE ALCWK 1='1: Non-drinker'*/
/*            	2='2: <1/week'*/
/*				3='3: 1-3/week'*/
/*				4='4: 4-6/week'*/
/*				5='5: 1-2/day'*/
/*				6='6: >2/day'*/
/*				9='9: Missing'*/
/*				;*/
/**/
/*	VALUE ALC_BLK 	1='1: Non-drinker'*/
/*          	  		2='2: < 1/day'*/
/*		      		3='3: 1-2/day'*/
/*		      		4='4: >2/day'*/
/*		      		9='9: Missing'*/
/*			  		;*/
/*	VALUE ALC82F  	1='1: Never Drinker'*/
/*	          		2='2: < 1/day'*/
/*			  		3='3: 1-<2/day'*/
/*			  		4='4: 2+/day'*/
/*			  		5='5: Former Drinker'*/
/*			  		9='9: Missing'*/
/*			  		;*/
/*	VALUE ALCCLN 	1='1: Never Drinker'*/
/*             		2='2: Former Drinker'*/
/*			 		3='3: Not Current - Unk 82'*/
/*				 	4='4: <1/day'*/
/*				 	5='5: 1-2/day'*/
/*			 		6='6: >2/day'*/
/*			 		9='9: Missing'*/
/*			 		;*/
/*RUN; */

*========================== MERGING BIOSPECIMEN DATA WITH CACO DATA AND REMOVING QC OBSERVATIONS =============================; 

PROC SORT DATA=PLYMA.INFNHL1000 OUT = INFNHL1000_TEMP; BY BARCODE; RUN; 
PROC SORT DATA=PLYMA.QCS OUT = QCS_TEMP; BY BARCODE; RUN; 
PROC SORT DATA=PLYMA.CACO391 OUT = CACO391; BY BARCODE; RUN;

DATA ALYDATA;
MERGE INFNHL1000_TEMP (KEEP=BARCODE BK_VP1 HPyV6_VP1 HPyV7_VP1 JC_VP1 KI_VP1 LPV_VP1 TSV_VP1 WU_VP1 MCV344_VP1) QCS_TEMP (IN=B KEEP=BARCODE SUBJID) CACO391; 
BY BARCODE; 
IF B=0; 

	*ONE ID HAS DIFFERENT DXDATE IN THE FILE CARI PREPARED AND THE VERIFIED FILE; 
	IF ID='24001048002011' THEN DXDATE=MDY(06,15,2002); 

RUN; 


DATA VERIFY; 
SET VERIFY.FEB2013VERIFIED; 

	***CLEAN AND CONVERT DX DAY, MONTH, AND YEAR TO NUMERIC DATA**;
	IF DODDY IN ('99','XX') THEN DODDYN=15; ELSE DODDYN=INPUT(DODDY,2.);              
	IF DODMO='99' THEN DODMON=6; ELSE DODMON=INPUT(DODMO,2.);               
	IF DODMON IN (4,6,9,11) AND DODDYN > 30 THEN DODDYN=30; 
	ELSE IF DODMON = 2 AND DODDYN > 28 THEN DODDYN=28; 
	 
	DODYRN=INPUT(DODYR,4.);  

	DATEVRF=MDY(DODMON,DODDYN,DODYRN); 
    
    DXDATE = DATEVRF; 

RUN; 

PROC SORT DATA=VERIFY; BY ID  DXDATE; RUN; 
PROC SORT DATA=ALYDATA; BY ID  DXDATE; RUN; 

DATA JB; 
MERGE ALYDATA(IN=ALY)
      VERIFY (IN=VER KEEP=DATASRC HISTOGY ID DXDATE); 

BY ID DXDATE; *MERGING BY DXDATE ENSURES THAT THE PEOPLE WHO HAVE MULTIPLE VERIFIED CANCER DIAGNOSES ARE MATCHED TO THE CORRECT DIAGNOSIS (THE NHL) SINCE THIS SHOULD MATCH THE DATE THAT CARI GAVE 
				IN THE CACO391 DATASET*;

IF ALY=1; 
IF VER=1 THEN VERFY=1;  

*THERE IS ONE PERSON WHO HAD 2 DIFFERENT CANCER DIAGNOSES ON THE SAME DAY - I AM REMOVING THE EXTRA OBSERVATION THAT WAS ADDED FROM THE VERIFIED FILE FOR THE NON-LYMPHOMA DIAGNOSIS*;
IF ID = '25056001006041' AND HISTOGY='8046' THEN DELETE; 

RUN; 


DATA COHORT; 
MERGE JB (IN=C)

	  ORG.SURVEY99MEN (IN=M KEEP=ID CIGNOW99 BEER99 BEERLT99 WINERE99 WINEWH99 LIQUOR99)
	  ORG.SURVEY99WOMEN (KEEP=ID CIGNOW99 BEER99 BEERLT99 WINERE99 WINEWH99 LIQUOR99)

      DER.SURVEY92MENKEYVAR2 (KEEP=ID REGION92 SMK92)
	  DER.SURVEY92FEMKEYVAR2 (KEEP=ID REGION92 SMK92)

	  DER.SURVEY97MENKEYVAR2 (KEEP=ID SMK97)
	  DER.SURVEY97FEMKEYVAR2 (KEEP=ID SMK97)

	  DER.SURVEY99MENKEYVAR (KEEP=ID BMI99)
	  DER.SURVEY99FEMKEYVAR (KEEP=ID BMI99)

	  LLDRIV.LLWOMENDER_MASTER (IN=LIFELINK KEEP=ID DRAWDATE AGELL MEDWKLL MEDCOMLL OSTERLL)
	  LLDRIV.LLMENDER_MASTER   (IN=LIFELINK KEEP=ID DRAWDATE AGELL MEDWKLL MEDCOMLL OSTERLL)

      ORIG82.CPS2FEMORIGINAL   (KEEP=ID HT EDUCATE)
      ORIG82.CPS2MENORIGINAL   (KEEP=ID HT EDUCATE) 

	  MAST.CPS2SMGMST14M	(KEEP=ID NUT_97 NUT_99)
	  MAST.CPS2SMGMST14F	(KEEP=ID NUT_97 NUT_99); 

BY ID; 
IF C=1; 

*DELETING PARTICIPANT WHO REVOKED AUTHORIZATION TO PARTICIPATE, UNTIL MASTER FILE IS UPDATED WITH THIS INFO (NEXT MASTER FILE IS 15M AND 15F)*;
	IF ID = '42341019003091' THEN DELETE;

*SWITCHING ONE OBSERVATION FROM OTHER NHL TO DLBCL PER REVIEWER COMMENTS - 4/22/14*;
	IF ID = '06051053003032' THEN NHLSUB = '3: DLBCL';

*CREATING GENDER VARIABLE*;
IF M=1 THEN GENDER=1; * MEN *;
ELSE GENDER=0; * WOMEN *;

*CREATING NUMERIC VARIABLES FOR MODELING*;
SEX_NEW=GENDER*1; 

FORMAT SEX_NEW SEX.;

*CREATING A NEW RACE VARIABLE: WHITE/BLACK/OTHER AND MISSING*;
IF RACE IN ('1','2') THEN RACENEW=1;
ELSE IF RACE IN ('3','4') THEN RACENEW=2;
ELSE RACENEW=3;

FORMAT RACENEW RACENEW.;

*RECLASSIFING CASES AND CONTROLS FOR MODELING*;
IF CACO=2 THEN CACONEW=0;
ELSE IF CACO=1 THEN CACONEW=1;

FORMAT CACONEW CACOF.;

*YEARS BETWEEN DIAGNOSIS AND BLOOD DRAW*;
YEARDS = ROUND(INTCK('DAY',SPECDATE,DXDATE)/365.25, 0.01); *DXDATE1 IS ONLY PRESENT FOR CASES, IT IS MISSING FOR ALL CONTROLS*;
LABEL YEARDS = 'YEARS BETWEEN DIAGNOSIS AND BLOOD DRAW'; 

*AGE AT DIAGNOSIS*;
AGEDX = INTCK('YEAR', BDAYDATE, DXDATE); 
LABEL AGEDX = 'AGE AT DIAGNOSIS'; 

*** BODY MASS INDEX;
*** WHO CUTPOINTS FOR UNDER/NORMAL, OVERWEIGHT, OBESE USING BMI IN 1999;
IF .<BMI99<25.0 THEN BMIWHO=0;
ELSE IF 25.0<=BMI99<30.0 THEN BMIWHO=1;
ELSE IF 30.0<=BMI99 THEN BMIWHO=2;
ELSE IF BMI99=. THEN BMIWHO=9;

LABEL BMIWHO='Body Mass Index (BMI)'; FORMAT BMIWHO BMIWHO.;

*RECODING EDUCATION VARIABLE*;
IF EDUCATE IN ('1','2') THEN EDUCA = 1;
ELSE IF EDUCATE IN ('3',' ') THEN EDUCA = 2;
ELSE IF EDUCATE IN ('4','5') THEN EDUCA = 3;
ELSE EDUCA = 4;

FORMAT REGION92 $REGIONF. EDUCA EDUCAF.; 

*ADDED BRIANS CODE FOR SMOKING STATUS FOR 1999 - 12/18/13*;

***********************************;
* 1992 SMOKING VARIABLES		  *;
***********************************;

IF SMK92 = '1' THEN STAT92='1';						 *NEVER*;
	ELSE IF SMK92 IN('2','4') THEN STAT92 = '2';	 *CURRENT*;
	ELSE IF SMK92 IN('3','5') THEN STAT92 = '3';	 *FORMER*;
	ELSE IF SMK92 ='6' THEN STAT92 = '4';			 *EVER*;
	ELSE STAT92 = '9';

*************************;
* SMOKING STATUS AT UPDATES;
*************************;

**********************************
*	1: NEVER SMOKER					*
*	2: CURRENT SMOKER					*
*	3: FORMER SMOKER					*
*	9: MISSING							*
**********************************;

***** STATUS 1997 *****;
IF SMK97 IN(' ','8','9') THEN STAT97 = '9';
	ELSE STAT97 = SMK97;

***** CLEAN 1997 SMOKING STATUS *****;
IF STAT97 = '1' THEN DO;
	IF STAT92 IN('2','3','4') THEN STAT97 = '3';
	ELSE IF STAT92 = '9' THEN STAT97 = '4';
	END;

***** STATUS 1999 *****;
IF CIGNOW99 = '2' THEN STAT99 = '2';	*CURRENT SMOKER*;
	ELSE IF CIGNOW99 IN('*',' ') OR NUT_99 = '0' THEN STAT99 ='9';		* MISSING *;
	ELSE IF CIGNOW99 = '1' THEN DO;
		IF STAT97 = '1'
			AND STAT92 = '1'
				THEN STAT99 = '1';	* ALWAYS NEVER SMOKERS *;
		ELSE IF STAT97 IN('2','3')
			OR STAT92 IN('2','3','4')
				THEN STAT99 = '3';	* EVER SMOKERS - NOT CURRENT *;
		ELSE STAT99 = '4';	* MISSING SMOKING INFO AT SOME POINT *;
		END;

*COMBINING MISSING AND MISSING SMOKING INFO AT SOME POINT - USE THIS VARIABLE FOR ANALYSIS - ADDED BY JB 12/18/13*;
IF STAT99 IN ('4','9') THEN SMK99='9';
ELSE SMK99=STAT99;

FORMAT SMK99 SMOKE.;

***ALCOHOL INTAKE - CONTINUOUS***;
		**1999**;
			*REGULAR BEER, LIGHT BEER, RED WINE, WHITE WINE & LIQUOR*;
				*--FREQUENCY = 0-9*;
			ARRAY T1  (5)  BEER99  BEERLT99  WINERE99  WINEWH99  LIQUOR99;
			ARRAY T2  (5)  BEER99X BEERLT99X WINERE99X WINEWH99X LIQUOR99X;
			DO I = 1 TO DIM(T1);
					 IF T1{I} = '0' THEN T2{I} = 0;    *NEVER;
				ELSE IF T1{I} = '1' THEN T2{I} = 0.02; *<1 SVG/MON;
				ELSE IF T1{I} = '2' THEN T2{I} = 0.07; *1-3 SVGS/MON;
				ELSE IF T1{I} = '3' THEN T2{I} = 0.14; *1 SVG/WK;
				ELSE IF T1{I} = '4' THEN T2{I} = 0.43; *2-4 SVGS/WK;
				ELSE IF T1{I} = '5' THEN T2{I} = 0.8;  *5-6 SVGS/WK;
				ELSE IF T1{I} = '6' THEN T2{I} = 1;    *1 SVG/D;
				ELSE IF T1{I} = '7' THEN T2{I} = 2.5;  *2-3 SVGS/D;
				ELSE IF T1{I} = '8' THEN T2{I} = 4.5;  *4-5 SVGS/D;
				ELSE IF T1{I} = '9' THEN T2{I} = 6;    *6+ SVGS/D;
				ELSE IF T1{I} IN (' ','.','*','I') THEN T2{I} = 0; *MISSING/INVALID CATEGORIZED AS NEVER;
			END;
			**1999 TOTAL ALCOHOL [SVGS/DAY]**;
				ALCOHOL99 = SUM(BEER99X,BEERLT99X,WINERE99X,WINEWH99X,LIQUOR99X);
				ALCOHOL99 = ROUND(ALCOHOL99,0.01); LABEL ALCOHOL99 = '1999 Total Alcohol (Drinks/D)';


***ALCOHOL INTAKE CATEGORIES***;
		*NOTE: CONDENSED CATEGORIES FROM SUE G.'S (NON-DRINKERS,<1 DRINK/D,1 DRINK/D,2+ DRINKS/D) TO THE FOLLOWING CATEGORIES
			   BECAUSE THERE WERE ONLY 5 CASES IN THE '1 DRINK/D' CATEGORY, SO COMBINED IT WITH '<1 DRINK/D' AS PER MARJI M.*;
		**1999**;
			 IF ALCOHOL99 = 0 	   THEN ALC99 = 1; *0 DRINKS/D, REF;
		ELSE IF 0 < ALCOHOL99 <= 1 THEN ALC99 = 2; *<=1 DRINK/D;
		ELSE IF 1 < ALCOHOL99 <= 6 THEN ALC99 = 3; *>1 DRINK/DAY;
								   ELSE ALC99 = 9; *MISSING/INVALID;
		LABEL ALC99 = '1999 Alcohol Intake (Drinks/D)'; FORMAT ALC99 ALC.;

** SEPARATING CASES BY LESS THAN 3 YEARS BETWEEN BLOOD DRAW AND DIAGNOSIS AND >=3 YEARS; 
IF CACONEW=1 AND YEARDS <3 THEN DXIN3Y=1; 
ELSE IF CACONEW=1 THEN DXIN3Y=2;

**SEPARATING CASES BY LESS THAN MEDIAN (3.7 YEARS) BETWEEN BLOOD DRAW AND DIAGNOSIS AND >=3.7 YEARS;
**ADDED 4/29/14 - PER SUE AND LT**;
IF CACONEW=1 AND YEARDS <3.65 THEN DXINMED=1;
ELSE IF CACONEW=1 THEN DXINMED=2; 

** SEPARATING CASES BY LESS THAN 5 YEARS BETWEEN BLOOD DRAW AND DIAGNOSIS AND >=5 YEARS; 
IF CACONEW=1 AND YEARDS <5  THEN DXIN5Y=1; 
ELSE IF CACONEW=1 THEN DXIN5Y=2;

*CREATING INDICATOR VARIABLE FOR MULTIPLE MYELOMA*; 
IF CACONEW=1 AND (NHLSUB = '5: Multiple Myeloma' OR HISTOGY IN ('9731', '9734') OR MATCH IN (45, 35, 221, 180, 121)) THEN MMNHL = 1; 
ELSE IF CACONEW=0 THEN MMNHL=0; 

*MAKING AN OTHER NHL CATEGORY FOR FREQUENCIES- THIS EXCLUDES MULTIPLE MYELOMAS*;
IF MMNHL NE 1 THEN DO;
IF (NHLSUB NOT IN ('2: CLL\SLL', '3: DLBCL', '4: Follicular')) AND (CACONEW=1) THEN OTHER=1; 
ELSE IF CACONEW=0 THEN OTHER=0;
END;

*PUTTING SUBTYPES INTO 1 CATEGORY FOR POLYTOMOUS LOG REG*;
IF CACONEW=0 THEN SUB1CAT=0;
ELSE IF NHLSUB='3: DLBCL' AND CACONEW=1 THEN SUB1CAT=1;
ELSE IF NHLSUB='2: CLL\SLL' AND CACONEW=1 THEN SUB1CAT=2;
ELSE IF NHLSUB='4: Follicular' AND CACONEW=1 THEN SUB1CAT=3;
ELSE IF OTHER=1 THEN SUB1CAT=4;
ELSE IF MMNHL=1 THEN SUB1CAT=5;

FORMAT SUB1CAT SUB1CAT.;

*CATEGORIZING DRAWDATE INTO 2 GROUPS FOR UNCONDITIONAL POLYTOMOUS MODEL (MAY 31, 2000 AND BELOW, AND GREATER THAN MAY 31, 2000) - PER LT 8/5/2013 (FOR EBV ANALYSIS)*;
 IF DRAWDATE LE '31may2000'd THEN DRAWDATE_CAT=0;
 ELSE DRAWDATE_CAT=1;

*CATEGORIZING AGE AT BLOOD DRAW INTO 5 CATEGORIES FOR UNCONDITIONAL POLYTOMOUS MODEL (I DIVIDED INTO QUINTILES) - PER LT 8/5/2013 (FOR EBV ANALYSIS)*;

 IF AGELL GT . AND AGELL LE 64 THEN AGELL_CAT=1;
 ELSE IF AGELL GE 65 AND AGELL LT 69 THEN AGELL_CAT=2;
 ELSE IF AGELL GE 69 AND AGELL LT 72 THEN AGELL_CAT=3; 
 ELSE IF AGELL GE 72 AND AGELL LT 75 THEN AGELL_CAT=4;
 ELSE AGELL_CAT=5;

*CREATING TIME VARIABLE FOR SPLINES*;
IF CACONEW=1 THEN TIME=1; 
ELSE IF CACONEW=0 THEN TIME=2; 

*CREATING SEROPOSITIVE/SERONEGATIVE VARIABLES FOR ALL ANTIGENS (<=250: SERONEGATIVE, >250 SEROPOSITIVE)*;

IF BK_VP1 LE 250 THEN BK_VP1_NP=0;
ELSE BK_VP1_NP=1;

IF JC_VP1 LE 250 THEN JC_VP1_NP=0;
ELSE JC_VP1_NP=1;

IF WU_VP1 LE 250 THEN WU_VP1_NP=0;
ELSE WU_VP1_NP=1;

IF KI_VP1 LE 250 THEN KI_VP1_NP=0;
ELSE KI_VP1_NP=1;

IF MCV344_VP1 LE 250 THEN MCV344_VP1_NP=0;
ELSE MCV344_VP1_NP=1;

IF HPyV6_VP1 LE 250 THEN HPyV6_VP1_NP=0;
ELSE HPyV6_VP1_NP=1;

IF HPyV7_VP1 LE 250 THEN HPyV7_VP1_NP=0;
ELSE HPyV7_VP1_NP=1;

IF LPV_VP1 LE 250 THEN LPV_VP1_NP=0;
ELSE LPV_VP1_NP=1;

IF TSV_VP1 LE 250 THEN TSV_VP1_NP=0;
ELSE TSV_VP1_NP=1;

FORMAT BK_VP1_NP JC_VP1_NP WU_VP1_NP KI_VP1_NP MCV344_VP1_NP HPyV6_VP1_NP HPyV7_VP1_NP LPV_VP1_NP TSV_VP1_NP SERONP.;


*********************************;
*	MEDIANS AMONG SEROPOSITIVES	*; 
*	ADDED 4/28/14				*;
*********************************;

***BK_VP1***; 
 IF BK_VP1 GT 250 AND BK_VP1 LE 3463.17 then BKV_MED=1;
 ELSE IF BK_VP1 GT 3463.17 then BKV_MED=2;
 ELSE IF BK_VP1 GT . AND BK_VP1 LE 250 THEN BKV_MED=3; *SERONEGATIVE*;

***JC_VP1***;
 IF JC_VP1 GT 250 AND JC_VP1 LE 1920.69 then JCV_MED=1;
 ELSE IF JC_VP1 GT 1920.69 then JCV_MED=2;
 ELSE IF JC_VP1 GT . AND JC_VP1 LE 250 THEN JCV_MED=3; *SERONEGATIVE*;

***WU_VP1***;
 IF WU_VP1 GT 250 AND WU_VP1 LE 5813.08 then WU_MED=1;
 ELSE IF WU_VP1 GT 5813.08 then WU_MED=2;
 ELSE IF WU_VP1 GT . AND WU_VP1 LE 250 THEN WU_MED=3; *SERONEGATIVE*;

 ***KI_VP1***;
 IF KI_VP1 GT 250 AND KI_VP1 LE 3578.17 then KI_MED=1;
 ELSE IF KI_VP1 GT 3578.17 then KI_MED=2;
 ELSE IF KI_VP1 GT . AND KI_VP1 LE 250 THEN KI_MED=3; *SERONEGATIVE*;
 
 ***MCV344_VP1***;
 IF MCV344_VP1 GT 250 AND MCV344_VP1 LE 5494.97 then MCV_MED=1;
 ELSE IF MCV344_VP1 GT 5494.97 then MCV_MED=2;
 ELSE IF MCV344_VP1 GT . AND MCV344_VP1 LE 250 THEN MCV_MED=3; *SERONEGATIVE*;

 ***HPyV6_VP1***;
 IF HPyV6_VP1 GT 250 AND HPyV6_VP1 LE 5556.25 then HPyV6_MED=1;
 ELSE IF HPyV6_VP1 GT 5556.25 then HPyV6_MED=2;
 ELSE IF HPyV6_VP1 GT . AND HPyV6_VP1 LE 250 THEN HPyV6_MED=3; *SERONEGATIVE*;

  ***HPyV7_VP1***;
 IF HPyV7_VP1 GT 250 AND HPyV7_VP1 LE 3539.69 then HPyV7_MED=1;
 ELSE IF HPyV7_VP1 GT 3539.69 then HPyV7_MED=2;
 ELSE IF HPyV7_VP1 GT . AND HPyV7_VP1 LE 250 THEN HPyV7_MED=3; *SERONEGATIVE*;
 
  ***TSV_VP1***;
 IF TSV_VP1 GT 250 AND TSV_VP1 LE 4787.44 then TSV_MED=1;
 ELSE IF TSV_VP1 GT 4787.44 then TSV_MED=2;
 ELSE IF TSV_VP1 GT . AND TSV_VP1 LE 250 THEN TSV_MED=3; *SERONEGATIVE*;
 
FORMAT BKV_MED JCV_MED WU_MED KI_MED MCV_MED HPyV6_MED HPyV7_MED TSV_MED MEDS.;

*************************************;
*	QUARTILES AMONG SEROPOSITIVES	*;
*	ADDED 4/30/14					*;
*************************************;
***BK_VP1***; 
 IF BK_VP1 GT 250 AND BK_VP1 LE 1220.17 then BKV_QPOS=1;
 ELSE IF BK_VP1 GT 1220.17 AND BK_VP1 LE 3463.17 then BKV_QPOS=2;
 ELSE IF BK_VP1 GT 3463.17 AND BK_VP1 LE 6873.67 THEN BKV_QPOS=3;
 ELSE IF BK_VP1 GT 6873.67 THEN BKV_QPOS=4;
 ELSE IF BK_VP1 GT . AND BK_VP1 LE 250 THEN BKV_QPOS=5; *SERONEGATIVE*;

 ***JC_VP1***; 
 IF JC_VP1 GT 250 AND JC_VP1 LE 662.944 then JCV_QPOS=1;
 ELSE IF JC_VP1 GT 662.944 AND JC_VP1 LE 1920.69 then JCV_QPOS=2;
 ELSE IF JC_VP1 GT 1920.69 AND JC_VP1 LE 4735.94 THEN JCV_QPOS=3;
 ELSE IF JC_VP1 GT 4735.94 THEN JCV_QPOS=4;
 ELSE IF JC_VP1 GT . AND JC_VP1 LE 250 THEN JCV_QPOS=5; *SERONEGATIVE*;

***WU_VP1***; 
 IF WU_VP1 GT 250 AND WU_VP1 LE 3281.58 then WU_QPOS=1;
 ELSE IF WU_VP1 GT 3281.58 AND WU_VP1 LE 5813.08 then WU_QPOS=2;
 ELSE IF WU_VP1 GT 5813.08 AND WU_VP1 LE 8089.58 THEN WU_QPOS=3;
 ELSE IF WU_VP1 GT 8089.58 THEN WU_QPOS=4;
 ELSE IF WU_VP1 GT . AND WU_VP1 LE 250 THEN WU_QPOS=5; *SERONEGATIVE*;

 ***KI_VP1***; 
 IF KI_VP1 GT 250 AND KI_VP1 LE 1874.17 then KI_QPOS=1;
 ELSE IF KI_VP1 GT 1874.17 AND KI_VP1 LE 3578.17 then KI_QPOS=2;
 ELSE IF KI_VP1 GT 3578.17 AND KI_VP1 LE 6044.17 THEN KI_QPOS=3;
 ELSE IF KI_VP1 GT 6044.17 THEN KI_QPOS=4;
 ELSE IF KI_VP1 GT . AND KI_VP1 LE 250 THEN KI_QPOS=5; *SERONEGATIVE*;

***MCV344_VP1***; 
 IF MCV344_VP1 GT 250 AND MCV344_VP1 LE 1922.97 then MCV_QPOS=1;
 ELSE IF MCV344_VP1 GT 1922.97 AND MCV344_VP1 LE 5494.97 then MCV_QPOS=2;
 ELSE IF MCV344_VP1 GT 5494.97 AND MCV344_VP1 LE 8040.47 THEN MCV_QPOS=3;
 ELSE IF MCV344_VP1 GT 8040.47 THEN MCV_QPOS=4;
 ELSE IF MCV344_VP1 GT . AND MCV344_VP1 LE 250 THEN MCV_QPOS=5; *SERONEGATIVE*;

***HPyV6_VP1***; 
 IF HPyV6_VP1 GT 250 AND HPyV6_VP1 LE 2790.25 then HPyV6_QPOS=1;
 ELSE IF HPyV6_VP1 GT 2790.25 AND HPyV6_VP1 LE 5556.25 then HPyV6_QPOS=2;
 ELSE IF HPyV6_VP1 GT 5556.25 AND HPyV6_VP1 LE 8567.25 THEN HPyV6_QPOS=3;
 ELSE IF HPyV6_VP1 GT 8567.25 THEN HPyV6_QPOS=4;
 ELSE IF HPyV6_VP1 GT . AND HPyV6_VP1 LE 250 THEN HPyV6_QPOS=5; *SERONEGATIVE*;

***HPyV7_VP1***; 
 IF HPyV7_VP1 GT 250 AND HPyV7_VP1 LE 1530.69 then HPyV7_QPOS=1;
 ELSE IF HPyV7_VP1 GT 1530.69 AND HPyV7_VP1 LE 3539.69 then HPyV7_QPOS=2;
 ELSE IF HPyV7_VP1 GT 3539.69 AND HPyV7_VP1 LE 5924.69 THEN HPyV7_QPOS=3;
 ELSE IF HPyV7_VP1 GT 5924.69 THEN HPyV7_QPOS=4;
 ELSE IF HPyV7_VP1 GT . AND HPyV7_VP1 LE 250 THEN HPyV7_QPOS=5; *SERONEGATIVE*;

***TSV_VP1***; 
 IF TSV_VP1 GT 250 AND TSV_VP1 LE 2107.44 then TSV_QPOS=1;
 ELSE IF TSV_VP1 GT 2107.44 AND TSV_VP1 LE 4787.44 then TSV_QPOS=2;
 ELSE IF TSV_VP1 GT 4787.44 AND TSV_VP1 LE 7262.94 THEN TSV_QPOS=3;
 ELSE IF TSV_VP1 GT 7262.94 THEN TSV_QPOS=4;
 ELSE IF TSV_VP1 GT . AND TSV_VP1 LE 250 THEN TSV_QPOS=5; *SERONEGATIVE*;

FORMAT BKV_QPOS JCV_QPOS WU_QPOS KI_QPOS MCV_QPOS HPyV6_QPOS HPyV7_QPOS TSV_QPOS QUARTILES.;

*********************************************;
*	TRENDS - QUARTILES AMONG SEROPOSITIVES	*;
*	ADDED 5/1/14							*;
*********************************************;

*BK_TREND_QPOS*;
IF BKV_QPOS=1 THEN BK_TREND_QPOS=615.17;
ELSE IF BKV_QPOS=2 THEN BK_TREND_QPOS=2212.17;
ELSE IF BKV_QPOS=3 THEN BK_TREND_QPOS=5152.17;
ELSE IF BKV_QPOS=4 THEN BK_TREND_QPOS=9724.67;

*JC_TREND_QPOS*;
IF JCV_QPOS=1 THEN JC_TREND_QPOS=436.19;
ELSE IF JCV_QPOS=2 THEN JC_TREND_QPOS=1133.19;
ELSE IF JCV_QPOS=3 THEN JC_TREND_QPOS=3199.19;
ELSE IF JCV_QPOS=4 THEN JC_TREND_QPOS=6620.19;

*WU_TREND_QPOS*;
IF WU_QPOS=1 THEN WU_TREND_QPOS=1924.33;
ELSE IF WU_QPOS=2 THEN WU_TREND_QPOS=4547.83;
ELSE IF WU_QPOS=3 THEN WU_TREND_QPOS=6693.58;
ELSE IF WU_QPOS=4 THEN WU_TREND_QPOS=10049.58;

*KI_TREND_QPOS*;
IF KI_QPOS=1 THEN KI_TREND_QPOS=1072.42;
ELSE IF KI_QPOS=2 THEN KI_TREND_QPOS=2690.17;
ELSE IF KI_QPOS=3 THEN KI_TREND_QPOS=4795.17;
ELSE IF KI_QPOS=4 THEN KI_TREND_QPOS=8116.17;

*MCV344_TREND_QPOS*;
IF MCV_QPOS=1 THEN MCV344_TREND_QPOS=635.47;
ELSE IF MCV_QPOS=2 THEN MCV344_TREND_QPOS=4070.97;
ELSE IF MCV_QPOS=3 THEN MCV344_TREND_QPOS=6817.47;
ELSE IF MCV_QPOS=4 THEN MCV344_TREND_QPOS=9290.47;

*HPyV6_TREND_QPOS*;
IF HPyV6_QPOS=1 THEN HPyV6_TREND_QPOS=1002.25;
ELSE IF HPyV6_QPOS=2 THEN HPyV6_TREND_QPOS=4386.25;
ELSE IF HPyV6_QPOS=3 THEN HPyV6_TREND_QPOS=7000.75;
ELSE IF HPyV6_QPOS=4 THEN HPyV6_TREND_QPOS=10400.00;

*HPyV7_TREND_QPOS*;
IF HPyV7_QPOS=1 THEN HPyV7_TREND_QPOS=631.94;
ELSE IF HPyV7_QPOS=2 THEN HPyV7_TREND_QPOS=2603.19;
ELSE IF HPyV7_QPOS=3 THEN HPyV7_TREND_QPOS=4645.44;
ELSE IF HPyV7_QPOS=4 THEN HPyV7_TREND_QPOS=7442.44;

*TSV_TREND_QPOS*;
IF TSV_QPOS=1 THEN TSV_TREND_QPOS=1067.94;
ELSE IF TSV_QPOS=2 THEN TSV_TREND_QPOS=3508.94;
ELSE IF TSV_QPOS=3 THEN TSV_TREND_QPOS=6041.94;
ELSE IF TSV_QPOS=4 THEN TSV_TREND_QPOS=8915.69;

FORMAT BK_TREND_QPOS JC_TREND_QPOS WU_TREND_QPOS KI_TREND_QPOS MCV344_TREND_QPOS HPyV6_TREND_QPOS HPyV7_TREND_QPOS TSV_TREND_QPOS QUARTILES.;


*************************************************************;
*EXCLUSION CASCADE											*;
*************************************************************; 

***MISSING LAB DATA (& IMPACTED CASE)- 1 CASE, 3 CONTROLS***;
IF BK_VP1 = . OR BARCODE = '073529-209' THEN EXCLUSION=1;

***HODGKIN LYMPHOMA****;
ELSE IF NHLSUB = '1: Hodgkin' THEN EXCLUSION=2;

*FINAL COHORT*;
ELSE EXCLUSION=.;

LABEL EXCLUSION = 'Reason for exclusion';
FORMAT EXCLUSION EXCLUSION.;


RUN; 
 
*PERMANENT FINAL DATASET*;

DATA PLYMA.FINALCOHORT (DROP=CIGNOW99 BEER99 BEERLT99 WINERE99 WINEWH99 LIQUOR99 BEER99X BEERLT99X WINERE99X WINEWH99X LIQUOR99X I); 
SET COHORT;

IF EXCLUSION = .;

RUN;


*****************************************************************************************************;
* USING MEDIANS INSTEAD OF TERTILES BECAUSE CELLS WERE TOO SMALL USING TERTILES - PER LT - 4/28/14	*;
*****************************************************************************************************;

*OBTAINING CUTPOINTS FROM SEROPOSITIVE CONTROLS TO DIVIDE ANTIGENS BY MEDIANS AMONG SEROPOSITIVE*;

%MACRO MEDS (ANTIGEN=); 

PROC UNIVARIATE DATA=PLYMA.FINALCOHORT;
WHERE CACONEW=0 AND &ANTIGEN._NP=1;
VAR &ANTIGEN;
OUTPUT OUT=MEDS_&ANTIGEN PCTLPTS=50 PCTLPRE=&ANTIGEN._PCT;
RUN;

%MEND MEDS;

%MEDS (ANTIGEN=BK_VP1)
%MEDS (ANTIGEN=JC_VP1)
%MEDS (ANTIGEN=WU_VP1)
%MEDS (ANTIGEN=KI_VP1)
%MEDS (ANTIGEN=MCV344_VP1)
%MEDS (ANTIGEN=HPyV6_VP1)
%MEDS (ANTIGEN=HPyV7_VP1)
%MEDS (ANTIGEN=TSV_VP1)
;

ODS RTF FILE="D:\USER\JBLASE\NHL & Polyomavirus\Jenny\PROGRAM REVIEW\OUTPUT\MEDIAN CUTPOINTS - &SYSDATE..RTF";
	TITLE 'CUTPOINTS FOR ANTIGEN MEDIANS';
PROC PRINT DATA=MEDS_BK_VP1; RUN;
PROC PRINT DATA=MEDS_JC_VP1; RUN;
PROC PRINT DATA=MEDS_WU_VP1; RUN;
PROC PRINT DATA=MEDS_KI_VP1; RUN;
PROC PRINT DATA=MEDS_MCV344_VP1; RUN;
PROC PRINT DATA=MEDS_HPyV6_VP1; RUN;
PROC PRINT DATA=MEDS_HPyV7_VP1; RUN;
PROC PRINT DATA=MEDS_TSV_VP1; RUN;
ODS RTF CLOSE;

*OBTAINING CUTPOINTS FROM SEROPOSITIVE CONTROLS TO DIVIDE ANTIGENS BY QUARTILES AMONG SEROPOSITIVE*;

%MACRO QUARTS_POS (ANTIGEN=); 

PROC UNIVARIATE DATA=PLYMA.FINALCOHORT;
WHERE CACONEW=0 AND &ANTIGEN._NP=1;
VAR &ANTIGEN;
OUTPUT OUT=QUARTSPOS_&ANTIGEN PCTLPTS=25 50 75 PCTLPRE=&ANTIGEN.PCT;
RUN;

%MEND QUARTS_POS;

%QUARTS_POS (ANTIGEN=BK_VP1)
%QUARTS_POS (ANTIGEN=JC_VP1)
%QUARTS_POS (ANTIGEN=WU_VP1)
%QUARTS_POS (ANTIGEN=KI_VP1)
%QUARTS_POS (ANTIGEN=MCV344_VP1)
%QUARTS_POS (ANTIGEN=HPyV6_VP1)
%QUARTS_POS (ANTIGEN=HPyV7_VP1)
%QUARTS_POS (ANTIGEN=TSV_VP1)
;

ODS RTF FILE="D:\USER\JBLASE\NHL & Polyomavirus\Jenny\PROGRAM REVIEW\OUTPUT\QUARTILE CUTPOINTS - &SYSDATE..RTF";
	TITLE 'CUTPOINTS FOR ANTIGEN QUARTILES AMONG SEROPOSITVES';
PROC PRINT DATA=QUARTSPOS_BK_VP1; RUN;
PROC PRINT DATA=QUARTSPOS_JC_VP1; RUN;
PROC PRINT DATA=QUARTSPOS_WU_VP1; RUN;
PROC PRINT DATA=QUARTSPOS_KI_VP1; RUN;
PROC PRINT DATA=QUARTSPOS_MCV344_VP1; RUN;
PROC PRINT DATA=QUARTSPOS_HPyV6_VP1; RUN;
PROC PRINT DATA=QUARTSPOS_HPyV7_VP1; RUN;
PROC PRINT DATA=QUARTSPOS_TSV_VP1; RUN;
ODS RTF CLOSE;

PROC SORT DATA=PLYMA.FINALCOHORT OUT=JB; BY MATCH; RUN;
PROC FREQ DATA=JB;
BY MATCH;
TABLES CACONEW*BK_VP1_NP;
RUN;


*OBTAINING MEDIANS FOR TREND TEST AMONG SEROPOSITIVE ANTIGEN QUARTILES*;

%MACRO MEDIANS (QUART=, ANTIGEN=, DATASET=); 

PROC SORT DATA=&DATASET OUT=TWO; BY &QUART; RUN;

PROC UNIVARIATE DATA=TWO;
BY &QUART;
WHERE CACONEW=0 AND &ANTIGEN._NP=1;
VAR &ANTIGEN;
OUTPUT OUT=MED_&QUART PCTLPTS=50 PCTLPRE=&QUART._PCT;
RUN;

%MEND MEDIANS;

*MEDIANS FOR SEROPOSITIVE QUARTILES*;

%MEDIANS (QUART=BKV_QPOS, ANTIGEN=BK_VP1, DATASET=PLYMA.FINALCOHORT);
%MEDIANS (QUART=JCV_QPOS, ANTIGEN=JC_VP1, DATASET=PLYMA.FINALCOHORT);
%MEDIANS (QUART=WU_QPOS, ANTIGEN=WU_VP1, DATASET=PLYMA.FINALCOHORT);
%MEDIANS (QUART=KI_QPOS, ANTIGEN=KI_VP1, DATASET=PLYMA.FINALCOHORT);
%MEDIANS (QUART=MCV_QPOS, ANTIGEN=MCV344_VP1, DATASET=PLYMA.FINALCOHORT);
%MEDIANS (QUART=HPyV6_QPOS, ANTIGEN=HPyV6_VP1, DATASET=PLYMA.FINALCOHORT);
%MEDIANS (QUART=HPyV7_QPOS, ANTIGEN=HPyV7_VP1, DATASET=PLYMA.FINALCOHORT);
%MEDIANS (QUART=TSV_QPOS, ANTIGEN=TSV_VP1, DATASET=PLYMA.FINALCOHORT);


ODS RTF FILE="D:\USER\JBLASE\NHL & Polyomavirus\Jenny\PROGRAM REVIEW\OUTPUT\MEDIANS FOR TREND TEST - &SYSDATE..RTF";
	TITLE 'MEDIANS FOR ALL ANTIGENS BY SEROPOSITIVE QUARTILE FOR TREND TEST';
PROC PRINT DATA=MED_BKV_QPOS; RUN;
PROC PRINT DATA=MED_JCV_QPOS; RUN;
PROC PRINT DATA=MED_WU_QPOS; RUN;
PROC PRINT DATA=MED_KI_QPOS; RUN;
PROC PRINT DATA=MED_MCV_QPOS; RUN;
PROC PRINT DATA=MED_HPyV6_QPOS; RUN;
PROC PRINT DATA=MED_HPyV7_QPOS; RUN;
PROC PRINT DATA=MED_TSV_QPOS; RUN;
ODS RTF CLOSE;
