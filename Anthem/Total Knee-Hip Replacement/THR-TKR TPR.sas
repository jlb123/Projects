*****************************;
*	TARGET PRICING REPORT	*;
*****************************;
*NOTE: DOUBLE CHECK THESE NUMBERS BEFORE USING. THIS IS AN OLD PROGRAM THAT HASNT BEEN USED FOR A LONG TIME*;

options compress=yes MPRINT SYMBOLGEN;

*MUST FILL THESE FOR EACH REPORT*;
*ALL INPUTS MUST BE ONE WORD - CAN HAVE AN UNDERSCORE IF NECESSARY*;

%LET BUNDLE=TKR;
%LET PROVIDER=TRIPOINT; 
%LET STARTDATE=1015;
%LET ENDDATE=0916;
%LET IDVAR=billg_tax_id_hoppa;
%LET FILEPATH=/ephc/ebp/nobackup/users/blase/;
%LET RELEASE=R10;
%LET ADMINID=%STR('341425870');
%LET ST=OH;

%LET DATASET=&ST._&BUNDLE._&STARTDATE._&ENDDATE._&RELEASE;
libname &BUNDLE._LIB "/ephc/ebp/backup/data/phi/tjr/%LOWCASE(&ST)/%LOWCASE(&BUNDLE)";


*ALL VALID EPISODES FOR MARKET AND PROVIDER OF INTEREST*;

DATA &BUNDLE._MARKET &BUNDLE._&PROVIDER;
SET &BUNDLE._LIB.&DATASET;


/*TKR/THR WATERFALL CRITERIA*/

/* TKR AND THR -- VALID Flagging criteria applied here */
if 
DRG_flg=1 AND /* Exclude episodes with the correct surg proc code but incorrect DRG (i.e., 462)*/
			  /* DRG inclusion criteria for index procedure but only apply to IP episodes */
			  /* allows for ASC episodes to not be excluded b/c DRG criteria is not applicable */
ind_flg=1 AND /* index trigger occurred within designated index period exclude 2nd index trigger */
			  /* occurring during experience period but outside of index period -- ind_flg=2 */
cap_flg in (0,2) AND  /* product exclusion criteria episodes with capitation-- cap_flg=1 */
ex_dx_flg=0 AND 	/* clinical historical exclusion criteria */
dx_flg=1 AND 		/* Dx inclusion criteria */
lob_lvl_flg in (1,2,3,4) AND /* Program Participation Exclusions of MCARE and MCAID */					
enrl_flg=1 AND /* continuous enrollment criteria */
lob_flg=1 AND  /* continuous enrolllment in same plan criteria */
age_flg=1 AND  /* minimum age at index service date criteria */
WP_Primary=0 AND /* Anthem primary criteria */
blt_flg=0 AND   /* bilateral exclusion criteria */
hosp_only_flg=0 /* product exclusion criteria */

THEN VALID=1;
ELSE VALID=0;


*READMISSION FLAG*;
IF READMIT_FLG=1 AND (compl_flg=1 OR revise_flg=1) THEN READMISSION=1;
ELSE READMISSION=0;

IF &IDVAR IN (&ADMINID) AND VALID=1 THEN OUTPUT &BUNDLE._&PROVIDER;
IF VALID=1 THEN OUTPUT &BUNDLE._MARKET; 

RUN;


*CREATING MACRO VARIABLES FOR THE TOTAL NUMBER OF VALID OBSERVATIONS (TO CALCULATE PERCENTAGES) AND MARKET
COMPLICATIONS/REVISIONS/READMISSIONS.
NOTE: THESE NUMBERS WILL STAY THE SAME REGARDLESS OF RENEGOTIATIONS*;
PROC SQL;
SELECT DISTINCT 
COUNT(*), SUM(bundlecost_tot_PCR<4000)
INTO :N_&PROVIDER._&BUNDLE,:LOW_REM 
FROM &BUNDLE._&PROVIDER;

*DENOMINATOR FOR COMPLICATION/REVISION/READMISSION RATES*;
SELECT DISTINCT 
COUNT(*)
INTO :N_MARKET_&BUNDLE 
FROM &BUNDLE._MARKET;

*CALCULATING RICHMOND MARKET READMISSION/REVISION/COMPLICATION RATES*;
CREATE TABLE MARKET_COMPS AS
SELECT 
/*COMPLICATION RATE*/
SUM(COMPL_FLG)/&&N_MARKET_&BUNDLE AS COMPL_MARKET FORMAT=PERCENT8.1 LABEL="Market Complication Rate",
/*READMISSION RATE*/
SUM(READMISSION)/&&N_MARKET_&BUNDLE AS READMIT_MARKET FORMAT=PERCENT8.1 LABEL="Market Readmission Rate",
/*REVISION RATE*/
SUM(REVISE_FLG)/&&N_MARKET_&BUNDLE AS REVISE_MARKET FORMAT=PERCENT8.1 LABEL="Market Revision Rate",
1 AS MERGEVAR
FROM &BUNDLE._MARKET;


*CALCULATING NUMBER OF INITIAL EPISODES (BEFORE EXCLUSION CRITERIA APPLIED)*;
CREATE TABLE INITIAL_EPS AS 
SELECT
/* GENERATE TOPLINE -- Initial Episodes */
SUM (IND_FLG=1 AND DRG_FLG=1 AND LOB_LVL_FLG ne 9) AS INITIAL_EPISODES LABEL='Topline Number of Total Baseline Episodes',
1 AS MERGEVAR
FROM &BUNDLE._LIB.&DATASET
WHERE &IDVAR IN (&ADMINID);

*CREATING MACRO VARIABLE FOR HIGH COST*;
SELECT 
MEAN(bundlecost_tot_PCR)+(2*(STD(bundlecost_tot_PCR)))
INTO :HIGHCAP_&PROVIDER._&BUNDLE
FROM &BUNDLE._MARKET;

*CREATING VARIABLE FOR HIGH COST FOR OUTPUTTING*;
CREATE TABLE HIGHCOST AS
SELECT
/*HIGH OUTLIER CAP*/
MEAN(bundlecost_tot_PCR)+(2*(STD(bundlecost_tot_PCR))) AS HIGHCAP_VAR_&PROVIDER FORMAT=DOLLAR11. 
LABEL='High Outlier Cap',
1 AS MERGEVAR
FROM &BUNDLE._MARKET; 

CREATE TABLE T1 AS 
SELECT bundlecost_tot_PCR, 
CASE
/*ADJUSTING COSTS TO HIGH CAP*/
WHEN bundlecost_tot_PCR>&&HIGHCAP_&PROVIDER._&BUNDLE THEN &&HIGHCAP_&PROVIDER._&BUNDLE 
ELSE bundlecost_tot_PCR
END AS NEWBUNDLE_TOT,
/*Total Outlier Adjusted Allowed Dollars for Valid Episodes*/
SUM(CALCULATED NEWBUNDLE_TOT) AS NEWBUNDLE_SUM
FROM &BUNDLE._&PROVIDER
/*ONLY FOR CASES OVER $4K, IF UNDER THEY ARE EXCLUDED*/
WHERE bundlecost_tot_PCR GE 4000;

*CREATING MACRO VARIABLE FOR THE SUM OF THE NEW BUNDLE TO USE BELOW*;
SELECT DISTINCT NEWBUNDLE_SUM
INTO :NEWBUNDLE_SUM
FROM T1;

/*MACRO VARIABLE FOR INITIAL TOPLINE EPISODES FOR PROVIDER*/
SELECT
/* GENERATE TOPLINE -- Initial Episodes */
SUM (IND_FLG=1 AND DRG_FLG=1 AND LOB_LVL_FLG ne 9) 
INTO :N_INITIAL
FROM &BUNDLE._LIB.&DATASET
WHERE &IDVAR IN (&ADMINID);

CREATE TABLE T2 AS
SELECT 
/*NUMBER OF VALID BASELINE EPISODES*/
COUNT(*) AS ELIG_EPS LABEL='Number of Valid Baseline Episodes',
/*Total Actual Baseline Allowed Dollars for Valid Episodes (prior to outlier adjustment)*/
SUM(bundlecost_tot_PCR) AS BL_TOT FORMAT=DOLLAR11. 
LABEL='Total Actual Baseline Allowed Dollars for Valid Episodes (prior to outlier adjustment)',
/*Number of Low Outlier Episodes Removed*/
SUM(bundlecost_tot_PCR<4000) AS LOW_REMOVED LABEL='Number of Low Outlier Episodes Removed',
/*Number of High Outlier Episodes Adjusted*/
SUM(bundlecost_tot_PCR>&&HIGHCAP_&PROVIDER._&BUNDLE) AS HIGH_CAPPED 
LABEL='Number of High Outlier Episodes Adjusted',
/*SIMPLE HISTORICAL CASE RATE - ADJUSTING FOR RICHMOND RENEGOTIATION*/
MEAN(index_prof_SURG_PCR) AS HIST_CSRATE FORMAT=DOLLAR11. LABEL='Historical Case Rate',
/*COMPLICATION RATE*/
SUM(COMPL_FLG)/&&N_&PROVIDER._&BUNDLE AS COMPL_&PROVIDER FORMAT=PERCENT8.1 LABEL="&PROVIDER Complication Rate",
/*READMISSION RATE*/
SUM(READMISSION)/&&N_&PROVIDER._&BUNDLE AS READMIT_&PROVIDER FORMAT=PERCENT8.1
LABEL="&PROVIDER Readmission Rate",
/*REVISION RATE*/
SUM(REVISE_FLG)/&&N_&PROVIDER._&BUNDLE AS REVISE_&PROVIDER FORMAT=PERCENT8.1
LABEL="&PROVIDER Revision Rate",
/*OUTLIER ADJUSTED AMOUNT*/
CALCULATED BL_TOT-&NEWBUNDLE_SUM AS ADJ_AMT FORMAT=DOLLAR11. LABEL="Outlier Adjusted Amount",
1 AS MERGEVAR,

/*MP1 DATA*/
/*CALCULATING PROPOSED EPISODE TARGET TO USE IN FORMULA BELOW*/
/*Outlier Adjusted Cost per Valid Episode (CELL E22 - INTERNAL)*/
&NEWBUNDLE_SUM/(&&N_&PROVIDER._&BUNDLE-&LOW_REM) AS ADJCOST_PEREP FORMAT=DOLLAR11. 
LABEL='Outlier Adjusted Cost per Valid Episode',
/*PROPOSED EPISODE TARGET (CELL E24 - INTERNAL)*/
CALCULATED ADJCOST_PEREP*0.94 AS EP_TARGET FORMAT=DOLLAR11. 
	LABEL= "Proposed Episode Target", /*COST PER VALID EPISODE WITH 6 PERCENT HAIRCUT*/ 
/*CELL H39*/
(&&N_&PROVIDER._&BUNDLE/2)*CALCULATED EP_TARGET AS H39 FORMAT=DOLLAR11. LABEL="Aggregate Target Budget",

(&N_INITIAL/2)*CALCULATED HIST_CSRATE AS TAA_EXCLUSIVE FORMAT=DOLLAR11. 
	LABEL='Total Allowed Amount exclusive of fee schedule adjustment',

/*DOING THIS FOR ALL 3 MODELS*/
/*CELL I39*/
(CALCULATED H39*0.94) AS I39_MOD3 FORMAT=DOLLAR11. LABEL="Aggregate Actual - Model 3", /*MODEL 3 - 6 PERCENT*/
(CALCULATED H39*0.96) AS I39_MOD2 FORMAT=DOLLAR11. LABEL="Aggregate Actual - Model 2", /*MODEL 2 - 4 PERCENT*/
(CALCULATED H39*0.98) AS I39_MOD1 FORMAT=DOLLAR11. LABEL="Aggregate Actual - Model 1", /*MODEL 1 - 2 PERCENT*/

/*estimated shared savings potential opportunity (40 percent)*/
(CALCULATED H39- CALCULATED I39_MOD3)*0.4 AS SSPO_MOD3 FORMAT=DOLLAR11. 
	LABEL="Shared Savings Potential Opp - Model 3", /*MODEL 3 - 6 PERCENT*/
(CALCULATED H39- CALCULATED I39_MOD2)*0.4 AS SSPO_MOD2 FORMAT=DOLLAR11. 
	LABEL="Shared Savings Potential Opp - Model 2", /*MODEL 2 - 4 PERCENT*/
(CALCULATED H39- CALCULATED I39_MOD1)*0.4 AS SSPO_MOD1 FORMAT=DOLLAR11. 
	LABEL="Shared Savings Potential Opp - Model 1", /*MODEL 1 - 2 PERCENT*/

/*CALCULATING PERCENTAGE INCREASE TO EQUAL EXACT SHARED SAVINGS OPPORTUNITY*/
CEIL((((CALCULATED SSPO_MOD3+CALCULATED TAA_EXCLUSIVE)/CALCULATED TAA_EXCLUSIVE)-1)*100) AS 
CASERATE_ADJ_MOD3 LABEL="Case Rate Adjustment - Model 3", /*MODEL 3 - 6 PERCENT*/
CEIL((((CALCULATED SSPO_MOD2+CALCULATED TAA_EXCLUSIVE)/CALCULATED TAA_EXCLUSIVE)-1)*100) AS 
CASERATE_ADJ_MOD2 LABEL="Case Rate Adjustment - Model 2", /*MODEL 2 - 4 PERCENT*/
CEIL((((CALCULATED SSPO_MOD1+CALCULATED TAA_EXCLUSIVE)/CALCULATED TAA_EXCLUSIVE)-1)*100) AS 
CASERATE_ADJ_MOD1 LABEL="Case Rate Adjustment - Model 1"/*MODEL 1 - 2 PERCENT*/

FROM &BUNDLE._&PROVIDER;


CREATE TABLE FINAL_&BUNDLE (DROP=MERGEVAR H39 TAA_EXCLUSIVE I39_MOD3 I39_MOD2 I39_MOD1 SSPO_MOD3 SSPO_MOD2 
SSPO_MOD1) AS
SELECT *
FROM INITIAL_EPS AS IE, T2 AS TABLE2, MARKET_COMPS AS MC, HIGHCOST AS HC
WHERE IE.MERGEVAR=TABLE2.MERGEVAR=MC.MERGEVAR=HC.MERGEVAR; 
QUIT; 


OPTIONS ORIENTATION=LANDSCAPE;
ODS RTF FILE="&FILEPATH &PROVIDER &BUNDLE TPR - &SYSDATE..RTF"; 

TITLE "&PROVIDER &BUNDLE &RELEASE"; 
TITLE2 "FOR IDS: &ADMINID";
PROC PRINT DATA=FINAL_&BUNDLE LABEL; RUN;

ODS RTF CLOSE;

TITLE; TITLE2;
